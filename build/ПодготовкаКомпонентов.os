
#Использовать 1commands
#Использовать fs

#Область ПрограммныйИнтерфейс

Процедура Подготовить() Экспорт

	ИменаПроектов = Новый Массив();
	ИменаПроектов.Добавить("Chronometer");
	ИменаПроектов.Добавить("BenchmarkOneScript.Extensions");

	ИменаФайловКомпонентов = Новый Массив();
	ИменаФайловКомпонентов.Добавить("1script_Chronometer.dll");
	ИменаФайловКомпонентов.Добавить("1script_BenchmarkOneScriptExtensions.dll");

	СобратьКомпоненты(ИменаПроектов);
	ПереместитьКомпоненты(ИменаПроектов, ИменаФайловКомпонентов);
	ПроверитьНаличиеКомпонентов(ИменаФайловКомпонентов);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СобратьКомпоненты(ИменаПроектов)

	ТекущийКаталог = ТекущийКаталог();

	Для Каждого ИмяПроекта Из ИменаПроектов Цикл
		
		КаталогРелиза = ОбъединитьПути(ТекущийКаталог, "src/" + ИмяПроекта + "/bin/Release");
		ФС.ОбеспечитьПустойКаталог(КаталогРелиза);

		Команда = Новый Команда;
		Команда.УстановитьСтрокуЗапуска(СтрШаблон("dotnet build src/%1 -c Release", ИмяПроекта));
		Команда.ПоказыватьВыводНемедленно(Истина);
		Команда.УстановитьКодировкуВывода(КодировкаТекста.UTF8);
		
		КодВозврата = Команда.Исполнить();
		Если Не КодВозврата = 0 Тогда
			ВызватьИсключение "Не удалось выполнить сборку .NET компоненты";
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ПереместитьКомпоненты(ИменаПроектов, ИменаФайловКомпонентов)

	СоответствиеПапок = Новый Соответствие();
	СоответствиеПапок.Вставить("net4", "net48");
	СоответствиеПапок.Вставить("dotnet", "net6.0");
	
	КаталогКомпонентов = ОбъединитьПути(ТекущийКаталог(), "Components");

	ФС.ОбеспечитьПустойКаталог(КаталогКомпонентов);

	Для Каждого ПараИменПапок Из СоответствиеПапок Цикл

		ИмяКаталогаПриемник = ПараИменПапок.Ключ;
		ИмяКаталогаИсточник = ПараИменПапок.Значение;

		КаталогПриемник = ОбъединитьПути(КаталогКомпонентов, ИмяКаталогаПриемник);
		ФС.ОбеспечитьПустойКаталог(КаталогПриемник);

		Для Каждого ИмяПроекта Из ИменаПроектов Цикл

			КаталогСборки = ОбъединитьПути(ТекущийКаталог(), "src/" + ИмяПроекта + "/bin/Release", ИмяКаталогаИсточник);

			Для Каждого ИмяФайла Из ИменаФайловКомпонентов Цикл

				ПутьКФайлуИсточник = ОбъединитьПути(КаталогСборки, ИмяФайла);
				ПутьКФайлуНазначение = ОбъединитьПути(КаталогПриемник, ИмяФайла);

				Если ФС.ФайлСуществует(ПутьКФайлуИсточник) Тогда
					ПереместитьФайл(ПутьКФайлуИсточник, ПутьКФайлуНазначение);
				КонецЕсли;

			КонецЦикла;
			
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

Процедура ПроверитьНаличиеКомпонентов(ИменаФайлов)

	ТекущийКаталог = ТекущийКаталог();
	Папки = СтрРазделить("net4,dotnet", ",");

	Для Каждого ИмяПапки Из Папки Цикл
		Для Каждого ИмяФайла Из ИменаФайлов Цикл
			ПутьКФайлу = ОбъединитьПути(ТекущийКаталог, "Components", ИмяПапки, ИмяФайла);
			Если Не ФС.ФайлСуществует(ПутьКФайлу) Тогда
				ВызватьИсключение СтрШаблон("Отсутсвует .NET компонента %1 в папке Components/%2", ИмяФайла, ИмяПапки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти