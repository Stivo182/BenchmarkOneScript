#Использовать delegate
#Использовать fluent

// Создает экземпляр колонки
//
// Возвращаемое значение:
//   КолонкаОтчетаБенчмарков
Функция Создать() Экспорт
	
	Разрядность = 2;

	Колонка = Новый КолонкаОтчетаБенчмарков(КолонкиОтчетаБенчмарков.RatioSD);
	Колонка.Заголовок = "RatioSD";
	Колонка.Описание = 
		"Стандартное отклонение соотношений времени выполнения относительно эталона ([StdDev] / [Baseline StdDev])";
	Колонка.СтильКоэффициента = СтилиКоэффициентаБенчмарка.Значение;
	Колонка.ЭтоЧисло = Истина;
	Колонка.ФорматнаяСтрока = ПредставленияПоказателейБенчмарков.ФорматнаяСтрокаЧисла(Разрядность);
	Колонка.ЗначениеДелегат = Делегаты.Создать(ЭтотОбъект, "Значение");
	Колонка.ФункцияСкрытия = "Элемент -> Элемент.Значение >= 0 И Элемент.Значение < 0.02";

	Возврат Колонка;

КонецФункции

// Возвращает значение колонки
//
// Параметры:
//   СтрокаРезультата - СтрокаТаблицыЗначений - см. МенеджерРасшифровкиРезультатовБенчмарков.НоваяТаблицаРасшифровки
//   Колонка - КолонкаОтчетаБенчмарков
//
// Возвращаемое значение:
//   Произвольный
Функция Значение(СтрокаРезультата, Колонка) Экспорт // BSLLS:UnusedParameters-off
	
	Если СтрокаРезультата.ЭтоЭталон Тогда
		Возврат 0;
	КонецЕсли;

	Если СтрокаРезультата.Эталон = Неопределено Тогда
		Возврат "?";
	КонецЕсли;

	Статистика = СтрокаРезультата.Статистика.Разделить(СтрокаРезультата.Эталон.Статистика);

	Возврат Статистика.СтандартноеОтклонение;

КонецФункции