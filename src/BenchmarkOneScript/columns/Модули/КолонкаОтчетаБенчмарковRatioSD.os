#Использовать fluent

// Создает экземпляр колонки
//
// Возвращаемое значение:
//   КолонкаОтчетаБенчмарков
Функция Создать() Экспорт
	
	Разрядность = 2;

	Колонка = Новый КолонкаОтчетаБенчмарков(КолонкиОтчетаБенчмарков.RatioSD);
	Колонка.Заголовок = "RatioSD";
	Колонка.Описание = 
		"Стандартное отклонение соотношений времени выполнения относительно эталона ([StdDev] / [Baseline StdDev])";
	Колонка.СтильКоэффициента = СтилиКоэффициентаБенчмарка.Значение;
	Колонка.ЭтоЧисло = Истина;
	Колонка.ФорматнаяСтрока = ПредставленияПоказателейБенчмарков.ФорматнаяСтрокаЧисла(Разрядность);
	Колонка.ДелегатЗначение = Новый Действие(ЭтотОбъект, "Значение");
	Колонка.ФункцияСкрытия = "Элемент -> Элемент.Значение >= 0 И Элемент.Значение < 0.02";

	Возврат Колонка;

КонецФункции

// Возвращает значение колонки
//
// Параметры:
//   РезультатЗапускаБенчмарка - РезультатЗапускаБенчмаркаДто
//   Колонка - КолонкаОтчетаБенчмарков
//
// Возвращаемое значение:
//   Произвольный
Функция Значение(РезультатЗапускаБенчмарка, Колонка) Экспорт // BSLLS:UnusedParameters-off
	
	Если РезультатЗапускаБенчмарка.ЭтоЭталон Тогда
		Возврат 0;
	КонецЕсли;

	Если РезультатЗапускаБенчмарка.Эталон = Неопределено Тогда
		Возврат "?";
	КонецЕсли;

	Статистика = РезультатЗапускаБенчмарка.Статистика.Разделить(РезультатЗапускаБенчмарка.Эталон.Статистика);

	Возврат Статистика.СтандартноеОтклонение;

КонецФункции