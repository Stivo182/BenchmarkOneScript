#Использовать delegate
#Использовать fluent

// Создает экземпляр колонки
//
// Возвращаемое значение:
//   КолонкаОтчетаБенчмарков
Функция Создать() Экспорт
	
	Разрядность = 2;

	Колонка = Новый КолонкаОтчетаБенчмарков(КолонкиОтчетаБенчмарков.КоэффициентПроизводительности);
	Колонка.Заголовок = "Ratio";
	Колонка.Описание = "Среднее значение соотношений времени выполнения относительно эталона ([Mean] / [Baseline Mean])";
	Колонка.СтильКоэффициента = СтилиКоэффициентаБенчмарка.Значение;
	Колонка.ЭтоЧисло = Истина;
	Колонка.ФорматнаяСтрока = ПредставленияПоказателейБенчмарков.ФорматнаяСтрокаЧисла(Разрядность);
	Колонка.ЗначениеДелегат = Делегаты.Создать(ЭтотОбъект, "Значение");

	Возврат Колонка;

КонецФункции

// Возвращает значение колонки
//
// Параметры:
//   СтрокаРезультата - СтрокаТаблицыЗначений - см. МенеджерРасшифровкиРезультатовБенчмарков.НоваяТаблицаРасшифровки
//   Колонка - КолонкаОтчетаБенчмарков
//
// Возвращаемое значение:
//   Произвольный
Функция Значение(СтрокаРезультата, Колонка) Экспорт // BSLLS:UnusedParameters-off
	
	Если СтрокаРезультата.ЭтоЭталон Тогда
		Возврат 1;
	КонецЕсли;

	Если СтрокаРезультата.Эталон = Неопределено Тогда
		Возврат "?";
	КонецЕсли;

	Статистика = СтрокаРезультата.Статистика.Разделить(СтрокаРезультата.Эталон.Статистика);

	Возврат Статистика.Среднее;

КонецФункции