// BSLLS:UnusedLocalVariable-off
// BSLLS:ExportVariables-off

&Несериализуемое
Перем ДескрипторБенчмарка Экспорт; // ДескрипторБенчмарка

&Сериализуемое("Type")
&Тип("Тип")
Перем Тип; // Тип

&Сериализуемое("Method")
&Тип("Строка")
Перем Метод; // Строка

&Сериализуемое("Category")
&Тип("Строка")
Перем Категория; // Строка

&Несериализуемое
Перем Параметры Экспорт; // Массив из ПараметрБенчмарка

&Несериализуемое
Перем Эталон Экспорт; // РезультатЗапускаБенчмаркаДто, Неопределено

&Сериализуемое("Baseline")
&Тип("Булево")
Перем ЭтоЭталон Экспорт; // Булево

&Сериализуемое("Statistics")
&Тип("СтатистикаБенчмарка")
Перем Статистика Экспорт; // СтатистикаБенчмарка

&Сериализуемое("Runtime")
&Тип("ИсполняющаяСредаБенчмарковДто")
Перем ИсполняющаяСреда Экспорт; // ИсполняющаяСредаБенчмарковДто

&Сериализуемое("Measurements")
&Тип("Массив")
&ДляКаждого
&Тип("РезультатИтерацииБенчмаркаДто")
Перем Замеры Экспорт; // Массив из РезультатИтерацииБенчмаркаДто

Процедура ПриСозданииОбъекта()

	Метод = "";
	Категория = "";
	Параметры = Новый Массив();
	ЭтоЭталон = Ложь;
	Статистика = Новый СтатистикаБенчмарка();
	ИсполняющаяСреда = Новый ИсполняющаяСредаБенчмарковДто();
	Замеры = Новый Массив();
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

#Область Сериализация

&ПередСериализацией
Процедура ПередСериализацией() Экспорт

	Тип = ДескрипторБенчмарка.ТипОбъекта();
	Метод = ДескрипторБенчмарка.Метод();
	Категория = ДескрипторБенчмарка.Категория();

КонецПроцедуры

&ПослеДесериализации
Процедура ПослеДесериализации(ПрочитанныеСвойства) Экспорт

	ДесериализоватьПараметры(ПрочитанныеСвойства);

КонецПроцедуры

&Сериализуемое("Parameters")
Функция ПолучитьПодготовленныеКСериализацииПараметры() Экспорт

	ПодготовленныеПараметры = Новый Структура();

	Для Каждого Параметр Из Параметры Цикл
		Значение = СериализацияОбъектовБенчмаркинга.ПодготовитьЗначение(Параметр.Значение);
		ПодготовленныеПараметры.Вставить(Параметр.Имя, Значение);
	КонецЦикла;

	Возврат ПодготовленныеПараметры;

КонецФункции

Процедура ДесериализоватьПараметры(ПрочитанныеСвойства)

	ДескрипторБенчмарка = Новый ДескрипторБенчмарка(Тип, Метод);
	ДескрипторБенчмарка.УстановитьКатегорию(ПрочитанныеСвойства["Category"]);
	ДескрипторБенчмарка.ИспользоватьКакЭталон(ПрочитанныеСвойства["Baseline"]);

	ИменаПараметровБенчмарка = ДескрипторБенчмарка.ИменаПараметров();
	КоличествоПараметров = ИменаПараметровБенчмарка.Количество();

	Для Каждого ДанныеПараметр Из ПрочитанныеСвойства["Parameters"] Цикл

		Имя = ДанныеПараметр.Ключ;
		Значение = ДанныеПараметр.Значение;

		ИндексПараметра = ИменаПараметровБенчмарка.Найти(Имя);
		ЭтоПараметрМетода = Не ИндексПараметра = Неопределено;

		ПараметрБенчмарка = Новый ПараметрБенчмарка(Имя, Значение, ЭтоПараметрМетода);
		Параметры.Добавить(ПараметрБенчмарка);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти