#Использовать "../../../../"

&Аргумент(Имя = "FILE", Описание = "Имя файла класса бенчмарков в текущем каталоге, либо полный путь к нему")
&ТСтрока
Перем ПутьКФайлу;

&КомандаПриложения(Имя = "run", Описание = "Запускает бенчмарки")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ВыполнениеКоманды 
Процедура Запустить() Экспорт
	
	Файл = ФайлКласса();
	ИмяКласса = Файл.ИмяБезРасширения;
	ПодключитьСценарий(Файл.ПолноеИмя, ИмяКласса);

	Бенчмаркинг.Запустить(Тип(ИмяКласса));

КонецПроцедуры

Функция ФайлКласса()

	ВариантыПутей = Новый Массив();
	ВариантыПутей.Добавить(ПутьКФайлу);
	ВариантыПутей.Добавить(ОбъединитьПути(ТекущийКаталог(), ПутьКФайлу));
	ВариантыПутей.Добавить(ОбъединитьПути(ТекущийКаталог(), ПутьКФайлу + ".os"));

	Для Каждого Путь Из ВариантыПутей Цикл
		Файл = Новый Файл(Путь);
		Если Файл.Существует() Тогда
			Возврат Файл;
		КонецЕсли;
	КонецЦикла;

	ВызватьИсключение СтрШаблон("Файл '%1' не существует", ПутьКФайлу);		

КонецФункции