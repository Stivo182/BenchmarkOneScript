
#Использовать 1commands
#Использовать fs

Процедура Подготовить() Экспорт

	ИменаПроектов = Новый Массив();
	ИменаПроектов.Добавить("GcStats");
	ИменаПроектов.Добавить("Chronometer");

	ИменаФайловБиблиотек = Новый Массив();
	ИменаФайловБиблиотек.Добавить("1script_GcStats.dll");
	ИменаФайловБиблиотек.Добавить("1script_Chronometer.dll");
	ИменаФайловБиблиотек.Добавить("Perfolizer.dll");

	СобратьБиблиотекуDotNET(ИменаПроектов);
	ПодготовитьКаталогСКомпонентами(ИменаПроектов, ИменаФайловБиблиотек);
	ПроверитьНаличиеБиблиотекиDotNET(ИменаФайловБиблиотек);

КонецПроцедуры

Процедура СобратьБиблиотекуDotNET(ИменаПроектов)

	Для Каждого ИмяПроекта Из ИменаПроектов Цикл
		
		КаталогРелиза = ОбъединитьПути(ТекущийКаталог(), "src/" + ИмяПроекта + "/bin/Release");
		ФС.ОбеспечитьПустойКаталог(КаталогРелиза);

		Команда = Новый Команда;
		Команда.УстановитьСтрокуЗапуска(СтрШаблон("dotnet build src/%1 -c Release", ИмяПроекта));
		Команда.ПоказыватьВыводНемедленно(Истина);
		Команда.УстановитьКодировкуВывода(КодировкаТекста.UTF8);
		
		КодВозврата = Команда.Исполнить();
		Если Не КодВозврата = 0 Тогда
			ВызватьИсключение "Не удалось выполнить сборку .NET библиотеки";
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ПодготовитьКаталогСКомпонентами(ИменаПроектов, ИменаФайловБиблиотек)

	СоответствиеПапок = Новый Соответствие();
	СоответствиеПапок.Вставить("net4", "net48");
	СоответствиеПапок.Вставить("dotnet", "net6.0");
	
	КаталогСКомпонентами = ОбъединитьПути(ТекущийКаталог(), "Components");

	ФС.ОбеспечитьПустойКаталог(КаталогСКомпонентами);

	Для Каждого Соответствие Из СоответствиеПапок Цикл
		ИмяКаталогаOscript = Соответствие.Ключ;
		ИмяКаталогаDotnet = Соответствие.Значение;

		ПутьККаталгуOscript = ОбъединитьПути(КаталогСКомпонентами, ИмяКаталогаOscript);
		ФС.ОбеспечитьПустойКаталог(ПутьККаталгуOscript);

		Для Каждого ИмяПроекта Из ИменаПроектов Цикл
			ПутьККаталгуDotnet = ОбъединитьПути(ТекущийКаталог(), "src/" + ИмяПроекта + "/bin/Release", ИмяКаталогаDotnet);

			Для Каждого ИмяФайла Из ИменаФайловБиблиотек Цикл
				ПутьИсточник = ОбъединитьПути(ПутьККаталгуDotnet, ИмяФайла);
				ПутьПриемник = ОбъединитьПути(ПутьККаталгуOscript, ИмяФайла);

				Если ФС.ФайлСуществует(ПутьИсточник) Тогда
					ПереместитьФайл(ПутьИсточник, ПутьПриемник);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Процедура ПроверитьНаличиеБиблиотекиDotNET(ИменаФайлов)

	Для Каждого ИмяПапки Из СтрРазделить("net4,dotnet", ",") Цикл
		Для Каждого ИмяФайла Из ИменаФайлов Цикл
			ПутьКФайлу = ОбъединитьПути(ТекущийКаталог(), "Components", ИмяПапки, ИмяФайла);
			Если Не ФС.ФайлСуществует(ПутьКФайлу) Тогда
				ВызватьИсключение СтрШаблон("Отсутсвует .NET библиотека %1 в папке Components/%2", ИмяФайла, ИмяПапки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры