// BSLLS:MissingVariablesDescription-off

#Использовать "../../../../"
#Использовать fs
#Использовать logos

&Опция(Имя = "r recursive", Описание = "Искать бенчмарки во вложенных каталогах")
&Флаг
&ПоУмолчанию(Ложь)
Перем Рекурсивно;

&Аргумент(Имя = "FILE", Описание = "Каталог, полный путь к файлу или имя файла в текущей директории")
&ТСтрока
Перем ПутьКФайлу;

Перем _Лог;

&КомандаПриложения(Имя = "run", Описание = "Запускает бенчмарки")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ВыполнениеКоманды 
Процедура Запустить() Экспорт
			
	Если ФС.КаталогСуществует(ПутьКФайлу) Тогда
		Бенчмаркинг.ЗапуститьИзКаталога(ПутьКФайлу, Рекурсивно);
		Возврат;
	КонецЕсли;

	Файл = ФайлКласса();
	Если Файл = Неопределено Тогда
		_Лог.Ошибка("Файл <%1> не существует", ПутьКФайлу);
		Возврат;
	КонецЕсли;

	Тип = Бенчмаркинг.ПодключитьКласс(Файл.ПолноеИмя);
	Если Не Тип = Неопределено Тогда
		Бенчмаркинг.Запустить(Тип);
	КонецЕсли;

КонецПроцедуры

Функция ФайлКласса()

	ВариантыПутей = Новый Массив();
	ВариантыПутей.Добавить(ПутьКФайлу);
	ВариантыПутей.Добавить(ОбъединитьПути(ТекущийКаталог(), ПутьКФайлу));
	ВариантыПутей.Добавить(ОбъединитьПути(ТекущийКаталог(), ПутьКФайлу + ".os"));

	Для Каждого Путь Из ВариантыПутей Цикл
		Файл = Новый Файл(Путь);
		Если Файл.Существует() Тогда
			Возврат Файл;
		КонецЕсли;
	КонецЦикла;

КонецФункции

_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.cli");