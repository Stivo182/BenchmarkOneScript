// Copyright © 2025 Nikita Fedkin (nixel2007@gmail.com).
// Адаптированная версия библиотеки jason (https://github.com/nixel2007/jason)

#Использовать annotations
#Использовать logos
#Использовать collectionos

#Область ОписаниеПеременных

// Лог
//
Перем Лог;

// Рефлектор
//
Перем Рефлектор;

// Множество - список известных пользовательских типов
//
Перем КэшИзвестныхТипов;

// Множество - список примитивных типов
//
Перем ПримитивныеТипы;

// Соответствие - Пользовательские конвертеры для типа
//
Перем Конвертеры;

#КонецОбласти

#Область Конструктор

Процедура ПриСозданииОбъекта(пКонвертеры = Неопределено)
	
	Рефлектор = Новый Рефлектор();
	Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.СериализаторJson");
	
	КэшИзвестныхТипов = Новый МножествоСоответствие;
	ИзвестныеТипы = Рефлектор.ИзвестныеТипы(Новый Структура("Пользовательский", Истина));
	Для Каждого ИзвестныйТип Из ИзвестныеТипы Цикл
		КэшИзвестныхТипов.Добавить(ИзвестныйТип.Значение);
	КонецЦикла;

	ПримитивныеТипы = Новый МножествоСоответствие;
	ПримитивныеТипы.Добавить(Тип("Строка"));
	ПримитивныеТипы.Добавить(Тип("Булево"));
	ПримитивныеТипы.Добавить(Тип("Дата"));
	ПримитивныеТипы.Добавить(Тип("Число"));
	ПримитивныеТипы.Добавить(Тип("Null"));
	ПримитивныеТипы.Добавить(Тип("Неопределено"));

	Если пКонвертеры = Неопределено Тогда
		Конвертеры = Новый Соответствие();
	Иначе
		Конвертеры = пКонвертеры;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Сериализация объекта в JSON-строку.
//
// Параметры:
//   Объект - Произвольный - Сериализуемый объект. Поддерживаются пользовательские классы,
//                           коллекции и примитивы.
// Возвращаемое значение:
//   Строка
//
Функция Сериализовать(Объект) Экспорт

	ОбъектСериализации = ПреобразоватьОбъектСериализации(Объект);

	Запись = Новый ЗаписьJSON();
	Запись.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Unix, Символы.Таб));
	ЗаписатьJSON(Запись, ОбъектСериализации);

	Возврат Запись.Закрыть();

КонецФункции

Функция ПреобразоватьОбъектСериализации(Объект, СериализуемыеКлючи = Неопределено) Экспорт

	ТипОбъекта = ТипЗнч(Объект);
	
	Если Конвертеры[ТипОбъекта] <> Неопределено Тогда
		Результат = Конвертеры[ТипОбъекта].Сериализовать(Объект, ЭтотОбъект);
	ИначеЕсли ТипОбъекта = Тип("Массив") Тогда
		Результат = ПреобразоватьМассив(Объект);
	ИначеЕсли ТипОбъекта = Тип("Структура") ИЛИ ТипОбъекта = Тип("Соответствие") Тогда
		Результат = ПреобразоватьСтруктуру(Объект);
	ИначеЕсли ТипОбъекта = Тип("ТаблицаЗначений") Тогда
		Результат = ПреобразоватьТаблицуЗначений(Объект, СериализуемыеКлючи);
	ИначеЕсли ЭтоПримитивныйТип(ТипОбъекта) Тогда
		Результат = ПреобразоватьПримитивныйТип(Объект);
	ИначеЕсли ТипОбъекта = Тип("Тип") Тогда
		Результат = ПреобразоватьТип(Объект);
	Иначе
		Результат = ПреобразоватьКомплексныйТип(Объект);
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоПримитивныйТип(ТипОбъекта)
	Возврат ПримитивныеТипы.Содержит(ТипОбъекта);
КонецФункции

Функция ПреобразоватьМассив(Массив)
	Результат = Новый Массив;

	Для Каждого Элемент Из Массив Цикл
		ПреобразованныйЭлемент = ПреобразоватьОбъектСериализации(Элемент);
		Результат.Добавить(ПреобразованныйЭлемент);
	КонецЦикла;
		
	Возврат Результат;
КонецФункции

Функция ПреобразоватьСтруктуру(Структура) Экспорт
	Результат = Новый Соответствие();

	Для Каждого Элемент Из Структура Цикл
		ПреобразованноеЗначение = ПреобразоватьОбъектСериализации(Элемент.Значение);
		Результат.Вставить(Элемент.Ключ, ПреобразованноеЗначение);
	КонецЦикла;

	Возврат Результат;
КонецФункции

Функция ПреобразоватьТаблицуЗначений(ТаблицаЗначений, СериализуемыеКлючи = Неопределено) Экспорт
	
	Результат = Новый Массив();

	Если СериализуемыеКлючи = Неопределено Тогда
		СериализуемыеКлючи = Новый Структура();
	КонецЕсли;
	
	СериализоватьВсеКолонки = СериализуемыеКлючи.Количество() = 0;

	Для Каждого СтрокаТаблицы Из ТаблицаЗначений Цикл

		ЗначенияКолонок = Новый Структура();

		Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл

			Если СериализуемыеКлючи.Свойство(Колонка.Имя) Тогда
				ИмяКлюча = СериализуемыеКлючи[Колонка.Имя];
			ИначеЕсли СериализоватьВсеКолонки Тогда
				ИмяКлюча = Колонка.Имя;
			Иначе
				Продолжить;
			КонецЕсли;

			ПреобразованноеЗначение = ПреобразоватьОбъектСериализации(СтрокаТаблицы[Колонка.Имя]);
			ЗначенияКолонок.Вставить(ИмяКлюча, ПреобразованноеЗначение);

		КонецЦикла;
		
		Результат.Добавить(ЗначенияКолонок);

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПреобразоватьПримитивныйТип(Объект) Экспорт
	Возврат Объект;
КонецФункции

Функция ПреобразоватьТип(Объект) Экспорт
	Возврат Строка(Объект);
КонецФункции

Функция ПреобразоватьКомплексныйТип(Объект) Экспорт

	ТипОбъекта = ТипЗнч(Объект);

	ЭтоПользовательскийТип = КэшИзвестныхТипов.Содержит(ТипОбъекта);

	Если НЕ ЭтоПользовательскийТип Тогда
		Лог.Предупреждение("Неизвестный тип объекта для преобразования: %1", ТипОбъекта);
		Возврат Неопределено;
	КонецЕсли;

	Результат = Новый Соответствие();

	ВызватьМетодПередСериализацией(Объект);
	ПрочитатьСериализуемыеПоля(Объект, Результат);
	ПрочитатьДополнительныеПоля(Объект, Результат);

	Возврат Результат;

КонецФункции

Процедура ВызватьМетодПередСериализацией(Объект)
		
	ТаблицаМетодов = Рефлектор.ПолучитьТаблицуМетодов(ТипЗнч(Объект));

	Для Каждого СвойстваМетода Из ТаблицаМетодов Цикл

		АннотацияПередСериализацией = РаботаСАннотациями.НайтиАннотацию(СвойстваМетода.Аннотации, "ПередСериализацией");
		Если АннотацияПередСериализацией = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Рефлектор.ВызватьМетод(Объект, СвойстваМетода.Имя);

	КонецЦикла;

КонецПроцедуры

Процедура ПрочитатьСериализуемыеПоля(Объект, Результат)
		
	СвойстваОбъекта = Рефлектор.ПолучитьТаблицуСвойств(ТипЗнч(Объект), Истина);

	Для Каждого СвойствоОбъекта Из СвойстваОбъекта Цикл
		// Проверяем, не помечено ли поле как несериализуемое
		Несериализуемое = РаботаСАннотациями.НайтиАннотацию(СвойствоОбъекта.Аннотации, "Несериализуемое");
		Если Несериализуемое <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Обязательное = Ложь;
		ИмяСвойства = СвойствоОбъекта.Имя;
		АннотацияСериализуемое = РаботаСАннотациями.НайтиАннотацию(СвойствоОбъекта.Аннотации, "Сериализуемое");
		Если АннотацияСериализуемое <> Неопределено Тогда
			ИмяСвойства = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияСериализуемое, 
				"Значение",
				СвойствоОбъекта.Имя);

			Обязательное = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияСериализуемое,
				"Обязательное", 
				Обязательное);
		КонецЕсли;

		СериализуемыеКлючи = ПолучитьСериализуемыеКлючи(СвойствоОбъекта);

		ЗначениеСвойства = Рефлектор.ПолучитьСвойство(Объект, СвойствоОбъекта.Имя);
		ЗначениеСвойства = ПреобразоватьОбъектСериализации(ЗначениеСвойства, СериализуемыеКлючи);
		Если Обязательное ИЛИ ЗначениеСвойства <> Неопределено Тогда
			Результат.Вставить(ИмяСвойства, ЗначениеСвойства);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ПрочитатьДополнительныеПоля(Объект, Результат)
	
	ТаблицаМетодов = Рефлектор.ПолучитьТаблицуМетодов(ТипЗнч(Объект));

	Для Каждого СвойстваМетода Из ТаблицаМетодов Цикл

		АннотацияСериализуемое = РаботаСАннотациями.НайтиАннотацию(СвойстваМетода.Аннотации, "Сериализуемое");
		Если АннотацияСериализуемое = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ИмяСвойства = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияСериализуемое, "Значение", "");
		Если Не ЗначениеЗаполнено(ИмяСвойства) Тогда
			Продолжить;
		КонецЕсли;

		Обязательное = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияСериализуемое, "Обязательное", Ложь);

		ЗначениеСвойства = Рефлектор.ВызватьМетод(Объект, СвойстваМетода.Имя);
		ЗначениеСвойства = ПреобразоватьОбъектСериализации(ЗначениеСвойства);
		Если Обязательное ИЛИ ЗначениеСвойства <> Неопределено Тогда
			Результат.Вставить(ИмяСвойства, ЗначениеСвойства);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция ПолучитьСериализуемыеКлючи(СвойствоОбъекта)
			
	СериализуемыеКлючи = Новый Структура();

	АннотацииСериализуемыеКлючи = РаботаСАннотациями.НайтиАннотации(СвойствоОбъекта.Аннотации, "СериализуемыйКлюч");
	Для Каждого Аннотация Из АннотацииСериализуемыеКлючи Цикл
		КоличествоПараметров = Аннотация.Параметры.Количество();
		Если КоличествоПараметров = 1 Тогда
			СериализуемыеКлючи.Вставить(Аннотация.Параметры[0].Значение, Аннотация.Параметры[0].Значение);
		ИначеЕсли КоличествоПараметров > 1 Тогда
			СериализуемыеКлючи.Вставить(Аннотация.Параметры[0].Значение, Аннотация.Параметры[1].Значение);
		КонецЕсли;
	КонецЦикла;

	Возврат СериализуемыеКлючи;

КонецФункции

#КонецОбласти