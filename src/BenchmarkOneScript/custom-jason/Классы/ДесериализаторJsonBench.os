// Copyright © 2025 Nikita Fedkin (nixel2007@gmail.com).
// Адаптированная версия библиотеки jason (https://github.com/nixel2007/jason)

#Использовать annotations
#Использовать validate

#Область ОписаниеПеременных

// Лог
//
Перем Лог;

// Рефлектор
//
Перем Рефлектор;

// Множество - список известных пользовательских типов
//
Перем КэшИзвестныхТипов;

// Множество - список примитивных типов
//
Перем ПримитивныеТипы;

// Соответствие - Пользовательские конвертеры для типа
//
Перем Конвертеры;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта(пКонвертеры = Неопределено)
	
	Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.ДесериализаторJson");
	Рефлектор = Новый Рефлектор();

	КэшИзвестныхТипов = Новый МножествоСоответствие;
	ИзвестныеТипы = Рефлектор.ИзвестныеТипы(Новый Структура("Пользовательский", Истина));
	Для Каждого ИзвестныйТип Из ИзвестныеТипы Цикл
		КэшИзвестныхТипов.Добавить(ИзвестныйТип.Значение);
	КонецЦикла;

	ПримитивныеТипы = Новый МножествоСоответствие;
	ПримитивныеТипы.Добавить(Тип("Строка"));
	ПримитивныеТипы.Добавить(Тип("Булево"));
	ПримитивныеТипы.Добавить(Тип("Дата"));
	ПримитивныеТипы.Добавить(Тип("Число"));
	ПримитивныеТипы.Добавить(Тип("Null"));
	ПримитивныеТипы.Добавить(Тип("Неопределено"));

	Если пКонвертеры = Неопределено Тогда
		Конвертеры = Новый Соответствие();
	Иначе
		Конвертеры = пКонвертеры;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Десериализация JSON-строки в объект.
//
// Параметры:
//   Строка      - Строка - JSON-строка, которую необходимо десериализовать.
//                          Поддерживаются объекты, массивы и примитивы.
//   ТипОбъекта  - Тип     - Тип, в который нужно десериализовать JSON-строку (необязателен).
//   ЧитатьВСоответствие - Булево - если Истина, чтение нетипизированного объекта JSON будет выполнено в `Соответствие`.
//                                  Если Ложь, нетипизированные объекты будут считываться в объект типа `Структура`.
//                                  Значение по умолчанию: Ложь
// Возвращаемое значение:
//   Произвольный - Если тип не задан, возвращается значение, прочитанное платформенным методом ПрочитатьJSON;
//                  иначе преобразованный объект целевого типа.
//
Функция Десериализовать(Строка, ТипОбъекта = Неопределено, ЧитатьВСоответствие = Ложь) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(Строка);

	ДесериализованныйОбъект = ПрочитатьJSON(ЧтениеJSON, ЧитатьВСоответствие);
	ЧтениеJSON.Закрыть();

	// Если тип не задан - возвращаем прочитанный объект как есть
	Если ТипОбъекта = Неопределено Тогда
		Возврат ДесериализованныйОбъект;
	КонецЕсли;
	
	Объект = ПреобразоватьОбъектДесериализации(ДесериализованныйОбъект, ТипОбъекта);
	
	Возврат Объект;

КонецФункции

Функция ПреобразоватьОбъектДесериализации(ДесериализованныйОбъект, 
	ТипОбъекта, ТипЭлементов = Неопределено, ДесериализуемыеКлючи = Неопределено) Экспорт

	Если Конвертеры[ТипОбъекта] <> Неопределено Тогда
		Возврат Конвертеры[ТипОбъекта].ПреобразоватьОбъектДесериализации(ЭтотОбъект, ДесериализованныйОбъект);
	ИначеЕсли ЭтоПримитивныйТип(ТипОбъекта) Тогда
		Возврат ПреобразоватьПримитивныйТип(ДесериализованныйОбъект, ТипОбъекта);
	ИначеЕсли ТипОбъекта = Тип("Массив") Тогда
		Возврат ПреобразоватьМассив(ДесериализованныйОбъект, ТипОбъекта, ТипЭлементов);
	ИначеЕсли ТипОбъекта = Тип("Соответствие") Тогда
		Возврат ПреобразоватьОбъект(ДесериализованныйОбъект, ТипОбъекта, ТипЭлементов);
	ИначеЕсли ТипОбъекта = Тип("Структура") Тогда
		Возврат ПреобразоватьОбъект(ДесериализованныйОбъект, ТипОбъекта, ТипЭлементов);
	ИначеЕсли ТипОбъекта = Тип("ТаблицаЗначений") Тогда
		Возврат ПреобразоватьТаблицуЗначений(ДесериализованныйОбъект, ТипОбъекта, ТипЭлементов, ДесериализуемыеКлючи);
	ИначеЕсли ТипОбъекта = Тип("Тип") Тогда
		Возврат ПреобразоватьТип(ДесериализованныйОбъект);
	ИначеЕсли ЭтоПользовательскийТип(ТипОбъекта) Тогда
		Возврат ПреобразоватьПользовательскийТип(ДесериализованныйОбъект, ТипОбъекта);
	Иначе
		Лог.Предупреждение("Неизвестный тип объекта: %1", ТипОбъекта);
	КонецЕсли;	

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоПримитивныйТип(ТипОбъекта)
	Возврат ПримитивныеТипы.Содержит(ТипОбъекта);
КонецФункции

Функция ЭтоПользовательскийТип(ТипОбъекта)
	Возврат КэшИзвестныхТипов.Содержит(ТипОбъекта);
КонецФункции

Функция ПреобразоватьПримитивныйТип(Объект, ТипОбъекта)
	Возврат Объект;
КонецФункции

Функция ПреобразоватьТип(Объект)
	Возврат Тип(Объект);
КонецФункции

Функция ПреобразоватьМассив(Объект, ТипОбъекта, ТипЭлементов)
	
	Результат = Новый Массив;

	Для Каждого Элемент Из Объект Цикл
		ОжидаемыйТип = ?(ЗначениеЗаполнено(ТипЭлементов), ТипЭлементов, ТипЗнч(Элемент));
		ПреобразованныйЭлемент = ПреобразоватьОбъектДесериализации(Элемент, ОжидаемыйТип);
		Результат.Добавить(ПреобразованныйЭлемент);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПреобразоватьТаблицуЗначений(Объект, ТипОбъекта, ТипЭлементов, ДесериализуемыеКлючи)

	Если Объект.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Результат = Новый ТаблицаЗначений;
	ДобавленныеКолонки = Новый МножествоСоответствие;

	Для Каждого ЗначенияКолонок Из Объект Цикл

		Если Не ЭтоСтруктураИлиСоответствие(ЗначенияКолонок) Тогда
			Продолжить;
		КонецЕсли;

		СтрокаТаблицы = Результат.Добавить();
		Для Каждого КлючИЗначение Из ЗначенияКолонок Цикл

			ИмяКолонки = КлючИЗначение.Ключ;
			ЗначениеКолонки = КлючИЗначение.Значение;
			
			Если ДесериализуемыеКлючи.Свойство(ИмяКолонки) Тогда
				ИмяКолонки = ДесериализуемыеКлючи[ИмяКолонки];
			КонецЕсли;

			Если Не ДобавленныеКолонки.Содержит(ИмяКолонки) Тогда
				Результат.Колонки.Добавить(ИмяКолонки);
				ДобавленныеКолонки.Добавить(ИмяКолонки);
			КонецЕсли;

			ПреобразованныйЭлемент = ПреобразоватьОбъектДесериализации(ЗначениеКолонки, ТипЗнч(ЗначениеКолонки));
			СтрокаТаблицы[ИмяКолонки] = ПреобразованныйЭлемент;

		КонецЦикла;

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПреобразоватьОбъект(Объект, ТипОбъекта, ТипЭлементов)

	Результат = Новый(ТипОбъекта);

	Для Каждого КлючИЗначение Из Объект Цикл
		ОжидаемыйТип = ?(ЗначениеЗаполнено(ТипЭлементов), ТипЭлементов, ТипЗнч(КлючИЗначение.Значение));
		ПреобразованноеЗначение = ПреобразоватьОбъектДесериализации(КлючИЗначение.Значение, ОжидаемыйТип);
		Результат.Вставить(КлючИЗначение.Ключ, ПреобразованноеЗначение);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПреобразоватьПользовательскийТип(Объект, ТипОбъекта)

	Результат = Новый(ТипОбъекта);
	СвойстваОбъекта = Рефлектор.ПолучитьТаблицуСвойств(ТипОбъекта, Истина);
	СвойстваПоИменамПолей = Новый Соответствие();

	Для Каждого Свойство Из СвойстваОбъекта Цикл
		АннотацияСериализуемое = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Сериализуемое");
		ИмяСвойства = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияСериализуемое, "Значение", Свойство.Имя);
		
		СвойстваПоИменамПолей.Вставить(ИмяСвойства, Свойство);
	КонецЦикла;

	Если ЭтоСтруктураИлиСоответствие(Объект) Тогда
		
		Для Каждого КлючИЗначение Из Объект Цикл

			Свойство = СвойстваПоИменамПолей[КлючИЗначение.Ключ];
			Если Свойство = Неопределено Тогда
				Продолжить;
			КонецЕсли;

			ИмяСвойства = Свойство.Имя;
			ЗначениеСвойства = КлючИЗначение.Значение;

			ТипыСвойства = ПолучитьТипыСвойства(Свойство, ЗначениеСвойства);
			ТипСвойства = ТипыСвойства.БазовыйТип;
			ТипЭлементов = ТипыСвойства.ТипЭлементов;

			ДесериализуемыеКлючи = ПолучитьДесериализуемыеКлючи(Свойство);
			ЗначениеСвойства = ПреобразоватьОбъектДесериализации(ЗначениеСвойства, 
				ТипСвойства,
				ТипЭлементов,
				ДесериализуемыеКлючи);

			Если ЗначениеСвойства = Неопределено Тогда
				Продолжить;
			КонецЕсли;

			Если ТипСвойства <> ТипЗнч(ЗначениеСвойства) Тогда
				Лог.Ошибка("Тип свойства '%1' не соответствует ожидаемому типу '%2'", ТипЗнч(ЗначениеСвойства), ТипСвойства);
				Продолжить;
			КонецЕсли;

			Рефлектор.УстановитьСвойство(Результат, ИмяСвойства, ЗначениеСвойства);
			
		КонецЦикла;

	КонецЕсли;

	ВызватьМетодПослеДесериализации(Результат, Объект);

	Возврат Результат;

КонецФункции

Функция ПолучитьТипыСвойства(Свойство, ЗначениеСвойства)

	ОписаниеСвойства = Новый Структура();
	ОписаниеСвойства.Вставить("БазовыйТип", ТипЗнч(ЗначениеСвойства));
	ОписаниеСвойства.Вставить("ТипЭлементов");

	КонтейнерАннотаций = Новый КонтейнерАннотаций;
	КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияТип"));
	КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияДляКаждого"));

	ТекущийРежимДляКаждого = Неопределено;

	Для Каждого Аннотация Из Свойство.Аннотации Цикл
		
		ОпределениеАннотации = КонтейнерАннотаций.ПолучитьОпределениеАннотации(Аннотация.Имя);
		Если ОпределениеАннотации = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ОбъектАннотации = ОпределениеАннотации.СоздатьОбъектАннотации(Аннотация);

		// Директива &ДляКаждого - переключает режим обхода
		Если ТипЗнч(ОбъектАннотации) = Тип("АннотацияДляКаждого") Тогда
			
			ТекущийРежимДляКаждого = ОбъектАннотации.Режим();

		ИначеЕсли ТипЗнч(ОбъектАннотации) = Тип("АннотацияТип") Тогда

			ТипСвойства = ОбъектАннотации.ТипЗначения();

			Если ТекущийРежимДляКаждого = Неопределено Тогда
				ОписаниеСвойства.БазовыйТип = ТипСвойства;
			ИначеЕсли ТекущийРежимДляКаждого = "Значение" Тогда
				ОписаниеСвойства.ТипЭлементов = ТипСвойства;
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Возврат ОписаниеСвойства;

КонецФункции

Функция ПолучитьДесериализуемыеКлючи(СвойствоОбъекта)
			
	ДесериализуемыеКлючи = Новый Структура();

	АннотацииСериализуемыеКлючи = РаботаСАннотациями.НайтиАннотации(СвойствоОбъекта.Аннотации, "СериализуемыйКлюч");
	Для Каждого Аннотация Из АннотацииСериализуемыеКлючи Цикл
		КоличествоПараметров = Аннотация.Параметры.Количество();
		Если КоличествоПараметров > 1 Тогда
			ДесериализуемыеКлючи.Вставить(Аннотация.Параметры[1].Значение, Аннотация.Параметры[0].Значение);
		КонецЕсли;
	КонецЦикла;

	Возврат ДесериализуемыеКлючи;

КонецФункции

Процедура ВызватьМетодПослеДесериализации(ПользовательскийОбъект, ПрочитанныеСвойства)
		
	ТаблицаМетодов = Рефлектор.ПолучитьТаблицуМетодов(ТипЗнч(ПользовательскийОбъект));

	Для Каждого СвойстваМетода Из ТаблицаМетодов Цикл

		АннотацияПослеДесериализации = РаботаСАннотациями.НайтиАннотацию(СвойстваМетода.Аннотации, "ПослеДесериализации");
		Если АннотацияПослеДесериализации = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ПараметрыМетода = Новый Массив();

		Если СвойстваМетода.КоличествоПараметров = 1 Тогда
			ПараметрыМетода.Добавить(ЭтотОбъект);
		ИначеЕсли СвойстваМетода.КоличествоПараметров = 2 Тогда
			ПараметрыМетода.Добавить(ЭтотОбъект);
			ПараметрыМетода.Добавить(ПрочитанныеСвойства);
		КонецЕсли;

		Рефлектор.ВызватьМетод(ПользовательскийОбъект, СвойстваМетода.Имя, ПараметрыМетода);

	КонецЦикла;

КонецПроцедуры

Функция ЭтоСтруктураИлиСоответствие(Значение)
	Возврат ТипЗнч(Значение) = Тип("Структура") Или ТипЗнч(Значение) = Тип("Соответствие");
КонецФункции

#КонецОбласти