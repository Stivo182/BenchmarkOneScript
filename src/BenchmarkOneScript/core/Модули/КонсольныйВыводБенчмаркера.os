#Использовать coloratos

#Область ПрограммныйИнтерфейс

// Выводит текст с автоматическим переводом строки и поддержкой цветового форматирования.
//
// Параметры:
//   Текст - Строка - Выводимый текст
//   Цвет  - Строка - Цвет текста
Процедура ВывестиСтроку(Текст, Цвет = Неопределено) Экспорт

	Если Бенчмаркинг.ЭтоВоркер() И Не Цвет = Неопределено Тогда
		ЦветнойВывод.ВывестиСтроку(ТекстСЦветом(Текст, Цвет));
	Иначе
		ЦветнойВывод.ВывестиСтроку(ПодготовитьТекст(Текст), Цвет);
	КонецЕсли;

КонецПроцедуры

// Выводит текст без перевода строки с поддержкой цветового форматирования.
//
// Параметры:
//   Текст - Строка - Выводимый текст
//   Цвет  - Строка - Цвет текста
Процедура Вывести(Текст, Цвет = Неопределено) Экспорт

	Если Бенчмаркинг.ЭтоВоркер() И Не Цвет = Неопределено Тогда
		ЦветнойВывод.Вывести(ТекстСЦветом(Текст, Цвет));
	Иначе
		ЦветнойВывод.Вывести(ПодготовитьТекст(Текст), Цвет);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьТекст(Знач Текст)
	Возврат СтрЗаменить(Текст, "|!#color=", "|#color=");
КонецФункции

Функция ТекстСЦветом(Текст, Цвет)

	Шаблон = "(%1|!#color=%2)";
	МассивСтрок = СтрРазделить(Текст, Символы.ПС);

	Для Инд = 0 По МассивСтрок.ВГраница() Цикл
		МассивСтрок[Инд] = СтрШаблон(Шаблон, МассивСтрок[Инд], Цвет);
	КонецЦикла;

	Возврат СтрСоединить(МассивСтрок, Символы.ПС);

КонецФункции

#КонецОбласти