#Использовать annotations
#Использовать reflector

#Область ПрограммныйИнтерфейс

// Извлекает конфигурацию бенчмарков
//
// Параметры:
//   ИсточникБенчмарков - Произвольный - Тип или экземпляр класса бенчмарков
//   Конфигурация - КонфигурацияБенчмарков - Конфигурация, в которую будут записаны данные 
//
// Возвращаемое значение:
//   КонфигурацияБенчмарков
Функция Извлечь(ИсточникБенчмарков, Конфигурация = Неопределено) Экспорт
	
	Если Конфигурация = Неопределено Тогда
		КонфигурацияБенчмарков = Новый КонфигурацияБенчмарков();
	Иначе
		КонфигурацияБенчмарков = Конфигурация;
	КонецЕсли;

	РефлекторОбъекта = Новый РефлекторОбъекта(ИсточникБенчмарков);
	ТаблицаМетодов = РефлекторОбъекта.ПолучитьТаблицуМетодов(, Ложь);
	ТаблицаСвойств = РефлекторОбъекта.ПолучитьТаблицуСвойств();
	
	СвойстваКонструктора = ТаблицаМетодов.Найти("ПриСозданииОбъекта", "Имя");

	Если Не СвойстваКонструктора = Неопределено Тогда
		ПрочитатьАннотациюСтратегия(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюКоличествоИтераций(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюКоличествоВызововЗаИтерацию(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюКоличествоПрогревочныхИтераций(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюМинимальноеВремяИтерации(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюМинимальноеКоличествоВызововЗаИтерацию(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюСортировка(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюМониторингПамяти(СвойстваКонструктора, КонфигурацияБенчмарков);	
		ПрочитатьАннотацииКолонок(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюКаталогАртефактов(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюЭкспортMarkdown(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюЭкспортJson(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюЭкспортHtml(СвойстваКонструктора, КонфигурацияБенчмарков);
		ПрочитатьАннотациюИсполняющаяСреда(СвойстваКонструктора, КонфигурацияБенчмарков);
	КонецЕсли;

	Для Каждого СвойстваПоля Из ТаблицаСвойств Цикл
		ПрочитатьАннотациюПараметры(СвойстваПоля, КонфигурацияБенчмарков);
		ПрочитатьАннотациюИсточникПараметров(СвойстваПоля, КонфигурацияБенчмарков);
	КонецЦикла;

	ПрочитатьОбработчикиСобытия(СобытияБенчмарков.ПередВсеми, РефлекторОбъекта, КонфигурацияБенчмарков);
	ПрочитатьОбработчикиСобытия(СобытияБенчмарков.ПослеВсех, РефлекторОбъекта, КонфигурацияБенчмарков);
	ПрочитатьОбработчикиСобытия(СобытияБенчмарков.ПередКаждым, РефлекторОбъекта, КонфигурацияБенчмарков);
	ПрочитатьОбработчикиСобытия(СобытияБенчмарков.ПослеКаждого, РефлекторОбъекта, КонфигурацияБенчмарков);

	Возврат КонфигурацияБенчмарков;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПрочитатьАннотациюСтратегия(СвойстваКонструктора, Конфигурация)

	Если РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "СтратегияХолодныйЗапуск") Тогда
		Стратегия = СтратегииЗапускаБенчмарка.ХолодныйЗапуск;
	ИначеЕсли РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "СтратегияПропускнаяСпособность") Тогда
		Стратегия = СтратегииЗапускаБенчмарка.ПропускнаяСпособность;
	Иначе
		Возврат;
	КонецЕсли;

	Конфигурация.УстановитьСтратегию(Стратегия);

КонецПроцедуры

Процедура ПрочитатьАннотациюКоличествоИтераций(СвойстваКонструктора, Конфигурация)

	КоличествоИтераций = РаботаСАннотациямиБенчмарков.ЗначениеАннотацииКакЧисло(
		СвойстваКонструктора,
		"КоличествоИтераций"
	);

	Если Не КоличествоИтераций = Неопределено Тогда
		Конфигурация.УстановитьКоличествоИтераций(КоличествоИтераций);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюКоличествоВызововЗаИтерацию(СвойстваКонструктора, Конфигурация)

	КоличествоВызововЗаИтерацию = РаботаСАннотациямиБенчмарков.ЗначениеАннотацииКакЧисло(
		СвойстваКонструктора,
		"КоличествоВызововЗаИтерацию"
	);

	Если Не КоличествоВызововЗаИтерацию = Неопределено Тогда
		Конфигурация.УстановитьКоличествоВызововЗаИтерацию(КоличествоВызововЗаИтерацию);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюКоличествоПрогревочныхИтераций(СвойстваКонструктора, Конфигурация)

	КоличествоПрогревочныхИтераций = РаботаСАннотациямиБенчмарков.ЗначениеАннотацииКакЧисло(
		СвойстваКонструктора,
		"КоличествоПрогревочныхИтераций"
	);

	Если Не КоличествоПрогревочныхИтераций = Неопределено Тогда
		Конфигурация.УстановитьКоличествоПрогревочныхИтераций(КоличествоПрогревочныхИтераций);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюМинимальноеВремяИтерации(СвойстваКонструктора, Конфигурация)

	МинимальноеВремяИтерации = РаботаСАннотациямиБенчмарков.ЗначениеАннотацииКакЧисло(
		СвойстваКонструктора,
		"МинимальноеВремяИтерации"
	);

	Если Не МинимальноеВремяИтерации = Неопределено Тогда
		Конфигурация.УстановитьМинимальноеВремяИтерации(МинимальноеВремяИтерации);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюМинимальноеКоличествоВызововЗаИтерацию(СвойстваКонструктора, Конфигурация)

	МинимальноеКоличествоВызововЗаИтерацию = РаботаСАннотациямиБенчмарков.ЗначениеАннотацииКакЧисло(
		СвойстваКонструктора,
		"МинимальноеКоличествоВызововЗаИтерацию"
	);
	
	Если Не МинимальноеКоличествоВызововЗаИтерацию = Неопределено Тогда
		Конфигурация.УстановитьМинимальноеКоличествоВызововЗаИтерацию(МинимальноеКоличествоВызововЗаИтерацию);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюПараметры(СвойстваПоля, Конфигурация)

	Аннотации = РаботаСАннотациями.ПолучитьАннотации(СвойстваПоля, "Параметры");

	Для Каждого Аннотация Из Аннотации Цикл

		Параметры = РаботаСАннотациями.ПолучитьЗначенияПараметровАннотации(Аннотация, "Значение");

		Для Каждого Значение Из Параметры Цикл
			Конфигурация.ДобавитьПараметр(СвойстваПоля.Имя, Значение);
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

Процедура ПрочитатьАннотациюИсточникПараметров(СвойстваПоля, Конфигурация)

	Аннотации = РаботаСАннотациями.ПолучитьАннотации(СвойстваПоля, "ИсточникПараметров");

	Для Каждого Аннотация Из Аннотации Цикл

		Параметры = РаботаСАннотациями.ПолучитьЗначенияПараметровАннотации(Аннотация, "Значение");

		Для Каждого Значение Из Параметры Цикл
			Конфигурация.ДобавитьИсточникПараметров(СвойстваПоля.Имя, Значение);
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

Процедура ПрочитатьАннотациюСортировка(СвойстваКонструктора, Конфигурация)

	Если РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "СортировкаПоМетоду") Тогда
		Сортировка = СортировкиОтчетаБенчмарков.Метод;
	ИначеЕсли РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "СортировкаОтБыстрыхКМедленным") Тогда
		Сортировка = СортировкиОтчетаБенчмарков.ОтБыстрыхКМедленным;
	ИначеЕсли РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "СортировкаОтМедленныхКБыстрым") Тогда
		Сортировка = СортировкиОтчетаБенчмарков.ОтМедленныхКБыстрым;
	Иначе
		Возврат;
	КонецЕсли;

	Конфигурация.УстановитьСортировкуОтчета(Сортировка);

КонецПроцедуры

Процедура ПрочитатьАннотациюМониторингПамяти(СвойстваКонструктора, Конфигурация)

	Если РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "МониторингПамяти") Тогда
		Конфигурация.ДобавитьМониторингПамяти();
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьОбработчикиСобытия(ИмяСобытия, РефлекторОбъекта, Конфигурация)

	ТаблицаМетодов = РефлекторОбъекта.ПолучитьТаблицуМетодов(ИмяСобытия);
	Если ТаблицаМетодов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Для Каждого СвойстваМетода Из ТаблицаМетодов Цикл
		Конфигурация.ДобавитьОбработчикСобытия(ИмяСобытия, СвойстваМетода.Имя);
	КонецЦикла;

КонецПроцедуры

Процедура ПрочитатьАннотацииКолонок(СвойстваКонструктора, Конфигурация)
	
	ПрочитатьАннотацииКолонкиБезПараметров(СвойстваКонструктора, Конфигурация);
	ПрочитатьАннотацииКолонкиПроцентиля(СвойстваКонструктора, Конфигурация);

КонецПроцедуры

Процедура ПрочитатьАннотацииКолонкиБезПараметров(СвойстваКонструктора, Конфигурация)
	
	ПереченьКолонок = "Мин, Макс, Медиана, НижнийКвартиль, ВерхнийКвартиль, RatioSD";
	Колонки = СтрРазделить(ПереченьКолонок, ", ", Ложь);

	Для Каждого ИмяКолонки Из Колонки Цикл
		Если РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "Колонка" + ИмяКолонки) Тогда
			Конфигурация.ДобавитьКолонку(КолонкиОтчетаБенчмарков[ИмяКолонки]);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ПрочитатьАннотацииКолонкиПроцентиля(СвойстваКонструктора, Конфигурация)
	
	Аннотации = РаботаСАннотациями.ПолучитьАннотации(СвойстваКонструктора, "КолонкаПроцентиль");

	КоличествоДобавлено = 0;
	Для Каждого Аннотация Из Аннотации Цикл

		Процентили = РаботаСАннотациями.ПолучитьЗначенияПараметровАннотации(Аннотация, "Значение");

		Для Каждого Процентиль Из Процентили Цикл
			Конфигурация.ДобавитьКолонку(КолонкиОтчетаБенчмарков.Процентиль(Процентиль));
			КоличествоДобавлено = КоличествоДобавлено + 1;
		КонецЦикла;

	КонецЦикла;

	Если Аннотации.Количество() > 0 И КоличествоДобавлено = 0 Тогда
		Конфигурация.ДобавитьКолонку(КолонкиОтчетаБенчмарков.Процентиль);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюКаталогАртефактов(СвойстваКонструктора, Конфигурация)

	Если Не РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "КаталогАртефактов") Тогда
		Возврат;
	КонецЕсли;

	КаталогАртефактов = РаботаСАннотациямиБенчмарков.ЗначениеАннотации(
		СвойстваКонструктора,
		"КаталогАртефактов"
	);
	
	Конфигурация.УстановитьКаталогАртефактов(КаталогАртефактов);

КонецПроцедуры

Процедура ПрочитатьАннотациюЭкспортMarkdown(СвойстваКонструктора, Конфигурация)

	Если РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "ЭкспортMarkdown") Тогда
		Конфигурация.ДобавитьЭкспортер(ЭкспортерыРезультатовБенчмарков.Markdown);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюЭкспортJson(СвойстваКонструктора, Конфигурация)

	Если РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "ЭкспортJson") Тогда
		Конфигурация.ДобавитьЭкспортер(ЭкспортерыРезультатовБенчмарков.Json);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюЭкспортHtml(СвойстваКонструктора, Конфигурация)

	Если РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваКонструктора, "ЭкспортHtml") Тогда
		Конфигурация.ДобавитьЭкспортер(ЭкспортерыРезультатовБенчмарков.Html);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюИсполняющаяСреда(СвойстваКонструктора, Конфигурация)

	Аннотации = РаботаСАннотациями.ПолучитьАннотации(СвойстваКонструктора, "ИсполняющаяСреда");

	Для Каждого Аннотация Из Аннотации Цикл
		
		// &ИсполняющаяСреда("stable")
		// &ИсполняющаяСреда("stable, dev")
		Значения = РаботаСАннотациями.ПолучитьЗначенияПараметровАннотации(Аннотация, "Значение");

		Для Каждого Значение Из Значения Цикл
			ВерсииСреды = СтрРазделить(Значение, ", ", Ложь);
			Для Каждого Версия Из ВерсииСреды Цикл
				Конфигурация.ДобавитьВерсиюИсполняющейСреды(Версия);
			КонецЦикла;
		КонецЦикла;

		// &ИсполняющаяСреда(Версия = "stable", Наименование = "Стабильная", ЭтоЭталон = Ложь) // BSLLS:CommentedCode-off
		Версия = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "Версия", , Истина);
		Если Не Версия = Неопределено Тогда
			Наименование = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "Наименование", "", Истина);
			ЭтоЭталон = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "ЭтоЭталон", Ложь, Истина);
			Конфигурация.ДобавитьВерсиюИсполняющейСреды(Версия, Наименование, ЭтоЭталон);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

#КонецОбласти