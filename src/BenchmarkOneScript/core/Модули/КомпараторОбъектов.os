#Область ПрограммныйИнтерфейс

// Определяет, равны ли значения двух объектов по их внутреннему содержанию.
//
// Параметры:
//   Значение1 - Произвольный - Первый объект для сравнения
//   Значение2 - Произвольный - Второй объект для сравнения
//
// Возвращаемое значение:
//   Булево
Функция Равны(Значение1, Значение2) Экспорт
	
	Тип = ТипЗнч(Значение1);

	Если Тип <> ТипЗнч(Значение2) Тогда
		Возврат Ложь;
	ИначеЕсли Тип = Тип("Массив") Тогда
		Возврат МассивыРавны(Значение1, Значение2);
	ИначеЕсли Тип = Тип("Соответствие") Тогда
		Возврат СоответствияРавны(Значение1, Значение2);
	ИначеЕсли Тип = Тип("Структура") Тогда
		Возврат СтруктурыРавны(Значение1, Значение2);
	ИначеЕсли Тип = Тип("ТаблицаЗначений") Тогда 
		Возврат ТаблицыРавны(Значение1, Значение2);
	Иначе
		Возврат Значение1 = Значение2;
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МассивыРавны(Массив1, Массив2)

	Если Массив1.Количество() <> Массив2.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;

	Для Инд = 0 По Массив1.ВГраница() Цикл
		Если Не Равны(Массив1[Инд], Массив2[Инд]) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;

	Возврат Истина;
	
КонецФункции

Функция СоответствияРавны(Соответствие1, Соответствие2)
	
	Если Соответствие1.Количество() <> Соответствие2.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;

	Для Каждого КлючЗначение Из Соответствие1 Цикл
		Ключ = КлючЗначение.Ключ;
		Если Не Равны(Соответствие1[Ключ], Соответствие2[КлючЗначение.Ключ]) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;

	Возврат Истина;
	
КонецФункции

Функция СтруктурыРавны(Структура1, Структура2)
	
	Если Структура1.Количество() <> Структура2.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;

	Для Каждого КлючЗначение Из Структура1 Цикл
		Ключ = КлючЗначение.Ключ;
		Если Не Структура2.Свойство(Ключ) Тогда
			Возврат Ложь;
		КонецЕсли;

		Если Не Равны(Структура1[Ключ], Структура2[Ключ]) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;

	Возврат Истина;
	
КонецФункции

Функция ТаблицыРавны(Таблица1, Таблица2)
	
	Если Таблица1.Количество() <> Таблица2.Количество() 
		Или Таблица1.Колонки.Количество() <> Таблица2.Колонки.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;

	Для Каждого Колонка Из Таблица1.Колонки Цикл
		Если Таблица2.Колонки.Найти(Колонка.Имя) = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;

	Для Инд = 0 По Таблица1.Количество() - 1 Цикл
		СтрокаТаблицы1 = Таблица1[Инд];
		СтрокаТаблицы2 = Таблица2[Инд];
		Для Каждого Колонка Из Таблица1.Колонки Цикл
			Если Не Равны(СтрокаТаблицы1[Колонка.Имя], СтрокаТаблицы2[Колонка.Имя]) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Возврат Истина;
	
КонецФункции

#КонецОбласти