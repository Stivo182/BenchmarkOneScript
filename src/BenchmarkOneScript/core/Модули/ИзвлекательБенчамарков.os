#Использовать annotations
#Использовать asserts

#Область ПрограммныйИнтерфейс

// Извлекает бенчмарки
//
// Параметры:
//   Объект - Произвольный - Ссылка на объект бенчмарк-класса
//
// Возвращаемое значение:
//   Массив из Бенчмарк
Функция Извлечь(Объект) Экспорт

	Набор = Новый Массив();

	РефлекторОбъекта = Новый РефлекторОбъекта(Объект);
	ТаблицаМетодов = РефлекторОбъекта.ПолучитьТаблицуМетодов("Бенчмарк");

	Для Каждого СвойстваМетода Из ТаблицаМетодов Цикл

		Бенчмарк = Новый Бенчмарк(Объект, СвойстваМетода.Имя);

		ПрочитатьАннотациюЭталон(СвойстваМетода, Бенчмарк);
		ПрочитатьАннотациюКатегория(СвойстваМетода, Бенчмарк);
		ПрочитатьАннотациюПараметры(СвойстваМетода, Бенчмарк);

		Набор.Добавить(Бенчмарк);

	КонецЦикла;

	Возврат Набор;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПрочитатьАннотациюЭталон(СвойстваМетода, Бенчмарк)

	Если РаботаСАннотациямиБенчмарков.ЕстьАннотация(СвойстваМетода, "Эталон") Тогда
		Бенчмарк.ИспользоватьКакЭталон();
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюКатегория(СвойстваМетода, Бенчмарк)

	Категория = РаботаСАннотациямиБенчмарков.ЗначениеАннотации(СвойстваМетода, "Категория");
	Если ЗначениеЗаполнено(Категория) Тогда
		Бенчмарк.УстановитьКатегорию(Категория);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАннотациюПараметры(СвойстваМетода, Бенчмарк)

	КоличествоПараметров = СвойстваМетода.Параметры.Количество();

	ТекстОшибкиКоличествоПараметровНеСовпадает = СтрШаблон(
		"Количество переданных параметров должно совпадать количеству параметров метода бенчмарка '%1'",
		Бенчмарк.Метод()
	);

	Аннотации = РаботаСАннотациями.ПолучитьАннотации(СвойстваМетода, "Параметры");
	ИменаПараметров = Бенчмарк.ИменаПараметров();
	КоличествоПараметров = ИменаПараметров.Количество();

	Для Каждого Аннотация Из Аннотации Цикл

		Параметры = РаботаСАннотациями.ПолучитьЗначенияПараметровАннотации(Аннотация, "Значение");

		Ожидаем.Что(КоличествоПараметров, ТекстОшибкиКоличествоПараметровНеСовпадает).Равно(Параметры.Количество());
		
		Бенчмарк.ДобавитьПараметры(Параметры);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти