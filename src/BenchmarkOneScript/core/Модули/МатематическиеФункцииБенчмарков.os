Функция Сумма(Значения) Экспорт
	
	Результат = 0;
	Для Каждого Значение Из Значения Цикл
		Результат = Результат + Значение;	
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

Функция Среднее(Значения) Экспорт
	
	Количество = Значения.Количество();
	
	Если Количество = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат Сумма(Значения) / Количество;
	
КонецФункции

Функция Дисперсия(Значения) Экспорт
	
	Количество = Значения.Количество();
	
	Если Количество = 0 Тогда
		Возврат Неопределено;
	ИначеЕсли Количество = 1 Тогда
		Возврат 0;
	КонецЕсли;

	Среднее = Среднее(Значения);
	
	Результат = 0;
	Для Инд = 0 По Количество - 1 Цикл	
		Результат = Результат + Pow(Значения[Инд] - Среднее, 2);	
	КонецЦикла;	

	Результат = Результат / (Количество - 1);
	
	Возврат Результат;
	
КонецФункции

Функция СтандартноеОтклонение(Значения) Экспорт
	
	Дисперсия = Дисперсия(Значения);
	
	Если Дисперсия = Неопределено Тогда
		Возврат Неопределено;	
	КонецЕсли;
	
	Возврат Sqrt(Дисперсия);
	
КонецФункции

Функция СтандартнаяОшибкаСреднего(СтандартноеОтклонение, Количество) Экспорт

	Если Количество = 0 Тогда
		Возврат 0;
	КонецЕсли;

	Возврат СтандартноеОтклонение / Sqrt(Количество);

КонецФункции

Функция Квартили(УпорядоченныеЗначения) Экспорт
	
	Квартили = Новый Структура();
	Квартили.Вставить("Q0", Квантиль(УпорядоченныеЗначения, 0));
	Квартили.Вставить("Q1", Квантиль(УпорядоченныеЗначения, 0.25));
	Квартили.Вставить("Q2", Квантиль(УпорядоченныеЗначения, 0.5));
	Квартили.Вставить("Q3", Квантиль(УпорядоченныеЗначения, 0.75));
	Квартили.Вставить("Q4", Квантиль(УпорядоченныеЗначения, 1));

	Возврат Квартили;

КонецФункции

Функция Квантиль(УпорядоченныеЗначения, Вероятность) Экспорт

	Количество = УпорядоченныеЗначения.Количество();

	Если Количество = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Позиция = Вероятность * (Количество - 1);

	ИндексПозиции = Цел(Позиция);
	ДробнаяЧасть = Позиция - ИндексПозиции;

	Если ИндексПозиции < 0 Тогда 
		Возврат УпорядоченныеЗначения[0];
	ИначеЕсли ИндексПозиции >= Количество - 1 Тогда
		Возврат УпорядоченныеЗначения[Количество - 1];
	Иначе
		НижнееЗначение = УпорядоченныеЗначения[ИндексПозиции];
		ВерхнееЗначение = УпорядоченныеЗначения[ИндексПозиции + 1];
		Возврат НижнееЗначение + ДробнаяЧасть * (ВерхнееЗначение - НижнееЗначение);
	КонецЕсли;

КонецФункции

Функция ТолькоЧисла(Значения, ВключаяНеопределено = Ложь) Экспорт

	ТипЧисло = Тип("Число");

	Для Каждого Значение Из Значения Цикл
		
		Если Значение = Неопределено И ВключаяНеопределено Тогда
			Продолжить;
		КонецЕсли;

		Если Не ТипЗнч(Значение) = ТипЧисло Тогда
			Возврат Ложь;
		КонецЕсли;

	КонецЦикла;

	Возврат Истина;

КонецФункции