#Область ПрограммныйИнтерфейс

// Комбинирует параметры бенчмарка
//
// Параметры:
//   ДескрипторБенчмарка - ДескрипторБенчмарка
//   Конфигурация - КонфигурацияБенчмарков
//
// Возвращаемое значение:
//   Массив из Массив из ПараметрБенчмарка
Функция Комбинировать(ДескрипторБенчмарка, Конфигурация) Экспорт

	Параметры = Новый Массив();
	ДополнитьПараметры(Параметры, Конфигурация.Параметры());
	ДополнитьПараметры(Параметры, ДескрипторБенчмарка.НаборыПараметров());

	ГруппыПараметров = СгруппироватьПараметры(Параметры);
	КомбинацииПараметров = КомбинироватьГруппы(ГруппыПараметров);

	Возврат КомбинацииПараметров;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СгруппироватьПараметры(Параметры)

	Группы = Новый Массив();
	Дерево = Новый Соответствие();

	Для Каждого Параметр Из Параметры Цикл

		Если ТипЗнч(Параметр) = Тип("Массив") Тогда

			Имя = "_";
			ЭтоПараметрМетода = Параметр[0].ЭтоПараметрМетода;

		Иначе		

			Имя = Параметр.Имя;
			ЭтоПараметрМетода = Параметр.ЭтоПараметрМетода;

		КонецЕсли;

		Строки = Дерево[ЭтоПараметрМетода];
		Если Строки = Неопределено Тогда
			Строки = Новый Соответствие();
			Дерево.Вставить(ЭтоПараметрМетода, Строки);
		КонецЕсли;

		ПараметрыГруппы = Строки[Имя];
		Если ПараметрыГруппы = Неопределено Тогда
			ПараметрыГруппы = Новый Массив();
			Строки.Вставить(Имя, ПараметрыГруппы);
			Группы.Добавить(ПараметрыГруппы);
		КонецЕсли;

		ПараметрыГруппы.Добавить(Параметр);

	КонецЦикла;

	Возврат Группы;

КонецФункции

Функция КомбинироватьГруппы(ГруппыПараметров)
	
	КомбинацииПараметров = Новый Массив();
	
	ЭтоПерваяГруппа = Истина;
	Для Каждого Параметры Из ГруппыПараметров Цикл

		КоличествоКомбинаций = КомбинацииПараметров.Количество();

		ЭтоПервыйПараметр = Истина;

		Для Каждого Параметр Из Параметры Цикл

			Если ЭтоПерваяГруппа Тогда

				Комбинация = Новый Массив();
				ДобавитьПараметрВКомбинацию(Комбинация, Параметр);

				КомбинацииПараметров.Добавить(Комбинация);
				
			Иначе

				Для Инд = 0 По КоличествоКомбинаций - 1 Цикл

					Если ЭтоПервыйПараметр Тогда

						Комбинация = КомбинацииПараметров[Инд];

					Иначе

						Комбинация = Новый Массив();
						Для Каждого ТекущийПараметр Из КомбинацииПараметров[Инд] Цикл
							Комбинация.Добавить(ТекущийПараметр);
						КонецЦикла;

						КомбинацииПараметров.Добавить(Комбинация);

						ДобавитьПараметрВКомбинацию(Комбинация, Параметр);

					КонецЕсли;

					ДобавитьПараметрВКомбинацию(Комбинация, Параметр);

				КонецЦикла;

			КонецЕсли;	

			ЭтоПервыйПараметр = Ложь;

		КонецЦикла;

		ЭтоПерваяГруппа = Ложь;

	КонецЦикла;

	Возврат КомбинацииПараметров;
	
КонецФункции

Процедура ДобавитьПараметрВКомбинацию(Комбинация, Параметр)

	Если ТипЗнч(Параметр) = Тип("Массив") Тогда

		ДобавитьПараметрыВКомбинацию(Комбинация, Параметр);

	Иначе

		Для Инд = 0 По Комбинация.ВГраница() Цикл

			ТекущийПараметр = Комбинация[Инд];
			Если ТекущийПараметр.Имя = Параметр.Имя
				И ТекущийПараметр.ЭтоПараметрМетода = Параметр.ЭтоПараметрМетода Тогда
				Комбинация.Удалить(Инд);
				Прервать;
			КонецЕсли;

		КонецЦикла;

		Комбинация.Добавить(Параметр);

	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьПараметрыВКомбинацию(Комбинация, Параметры)

	Для Каждого Параметр Из Параметры Цикл
		ДобавитьПараметрВКомбинацию(Комбинация, Параметр);
	КонецЦикла;

КонецПроцедуры

Процедура ДополнитьПараметры(Параметры, Источник)

	Для Каждого Параметр Из Источник Цикл
		Параметры.Добавить(Параметр);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти