#Область ПрограммныйИнтерфейс

// Комбинирует параметры бенчмарка
//
// Параметры:
//   Параметры - Массив из ПараметрБенчмарка
//             - Массив из Массив из ПараметрБенчмарка
//             - Массив из ПараметрыМетодаБенчмарка
//
// Возвращаемое значение:
//   Массив из Массив из ПараметрБенчмарка
Функция Комбинировать(Параметры) Экспорт

	ГруппыПараметров = СгруппироватьПараметры(Параметры);
	КомбинацииПараметров = КомбинироватьГруппы(ГруппыПараметров);

	Возврат КомбинацииПараметров;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СгруппироватьПараметры(Параметры)

	Группы = Новый Массив();
	Дерево = Новый Соответствие();

	Для Каждого Параметр Из Параметры Цикл

		ТипЭлемента = ТипЗнч(Параметр);
		Если ТипЭлемента = Тип("ПараметрБенчмарка") Тогда

			Имя = Параметр.Имя();
			ЭтоПараметрМетода = Параметр.ЭтоПараметрМетода();

		ИначеЕсли ТипЭлемента = Тип("Массив") Или ТипЭлемента = Тип("ПараметрыМетодаБенчмарка") Тогда

			Если Параметр.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;

			Имя = "_";
			ЭтоПараметрМетода = ТипЭлемента = Тип("ПараметрыМетодаБенчмарка");

		Иначе		
			ВызватьИсключение СтрШаблон("Не поддерживается тип <%1>", ТипЭлемента);
		КонецЕсли;

		Строки = Дерево[ЭтоПараметрМетода];
		Если Строки = Неопределено Тогда
			Строки = Новый Соответствие();
			Дерево.Вставить(ЭтоПараметрМетода, Строки);
		КонецЕсли;

		ПараметрыГруппы = Строки[Имя];
		Если ПараметрыГруппы = Неопределено Тогда
			ПараметрыГруппы = Новый Массив();
			Строки.Вставить(Имя, ПараметрыГруппы);
			Группы.Добавить(ПараметрыГруппы);
		КонецЕсли;

		ПараметрыГруппы.Добавить(Параметр);

	КонецЦикла;

	Возврат Группы;

КонецФункции

Функция КомбинироватьГруппы(ГруппыПараметров)
	
	КомбинацииПараметров = Новый Массив();
	
	ЭтоПерваяГруппа = Истина;
	Для Каждого ПараметрыГруппы Из ГруппыПараметров Цикл

		КоличествоКомбинаций = КомбинацииПараметров.Количество();

		ЭтоПервыйПараметр = Истина;

		Для Каждого Параметр Из ПараметрыГруппы Цикл

			Если ЭтоПерваяГруппа Тогда

				Комбинация = Новый Массив();
				ДобавитьПараметрВКомбинацию(Комбинация, Параметр);

				КомбинацииПараметров.Добавить(Комбинация);
				
			Иначе

				Для Инд = 0 По КоличествоКомбинаций - 1 Цикл

					Если ЭтоПервыйПараметр Тогда

						Комбинация = КомбинацииПараметров[Инд];

					Иначе

						Комбинация = Новый Массив();
						Для Каждого ТекущийПараметр Из КомбинацииПараметров[Инд] Цикл
							Комбинация.Добавить(ТекущийПараметр);
						КонецЦикла;

						КомбинацииПараметров.Добавить(Комбинация);

					КонецЕсли;

					ДобавитьПараметрВКомбинацию(Комбинация, Параметр);

				КонецЦикла;

			КонецЕсли;	

			ЭтоПервыйПараметр = Ложь;

		КонецЦикла;

		ЭтоПерваяГруппа = Ложь;

	КонецЦикла;

	Возврат КомбинацииПараметров;
	
КонецФункции

Процедура ДобавитьПараметрВКомбинацию(Комбинация, Параметр)

	Если ТипЗнч(Параметр) = Тип("Массив") Тогда

		ДобавитьПараметрыВКомбинацию(Комбинация, Параметр);

	ИначеЕсли ТипЗнч(Параметр) = Тип("ПараметрыМетодаБенчмарка") Тогда

		ДобавитьПараметрыВКомбинацию(Комбинация, Параметр.ВМассив());

	Иначе

		Для Инд = 0 По Комбинация.ВГраница() Цикл

			ТекущийПараметр = Комбинация[Инд];
			Если ТекущийПараметр.Имя() = Параметр.Имя()
				И ТекущийПараметр.ЭтоПараметрМетода() = Параметр.ЭтоПараметрМетода() Тогда
				Комбинация.Удалить(Инд);
				Прервать;
			КонецЕсли;

		КонецЦикла;

		Комбинация.Добавить(Параметр);

	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьПараметрыВКомбинацию(Комбинация, Параметры)

	Для Каждого Параметр Из Параметры Цикл
		ДобавитьПараметрВКомбинацию(Комбинация, Параметр);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти