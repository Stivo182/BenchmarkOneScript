// BSLLS:ExportVariables-off
// BSLLS:MissingVariablesDescription-off

// Секунды
Перем Наносекунда Экспорт;
Перем Микросекунда Экспорт;
Перем Миллисекунда Экспорт;
Перем Секунда Экспорт;

// Байты
Перем Байт Экспорт;
Перем Килобайт Экспорт;
Перем Мегабайт Экспорт;
Перем Гигабайт Экспорт;

// Составное
Перем ОперацийВСекунду Экспорт;

Перем _Список; // Массив

Функция Создать(Обозначение, Базовая = Неопределено, ОтношениеКБазовой = 1) Экспорт
	ЕдиницаИзмерения = Новый ЕдиницаИзмеренияБенчмарка(Обозначение, Базовая, ОтношениеКБазовой);
	_Список.Добавить(ЕдиницаИзмерения);
	Возврат ЕдиницаИзмерения;
КонецФункции

Функция Минимальная(ЕдиницаИзмерения1, ЕдиницаИзмерения2) Экспорт
	Если ЕдиницаИзмерения1.ОтношениеКБазовой > ЕдиницаИзмерения2.ОтношениеКБазовой Тогда
		Возврат ЕдиницаИзмерения2;
	Иначе
		Возврат ЕдиницаИзмерения1;
	КонецЕсли;
КонецФункции

Функция Максимальная(ЕдиницаИзмерения1, ЕдиницаИзмерения2) Экспорт
	Если ЕдиницаИзмерения1.ОтношениеКБазовой < ЕдиницаИзмерения2.ОтношениеКБазовой Тогда
		Возврат ЕдиницаИзмерения2;
	Иначе
		Возврат ЕдиницаИзмерения1;
	КонецЕсли;
КонецФункции

Функция Подобрать(Значение, ЕдиницаИзмерения) Экспорт
	
	ЗначениеБазовое = Конвертировать(Значение, ЕдиницаИзмерения, ЕдиницаИзмерения.Базовая);
	Единицы = НайтиПоБазовой(ЕдиницаИзмерения.Базовая);
	ПодходящаяЕдиница = Неопределено;
	
	Для Каждого Единица Из Единицы Цикл
		Если Единица.ОтношениеКБазовой <= ЗначениеБазовое Тогда
			ПодходящаяЕдиница = ?(ПодходящаяЕдиница = Неопределено, Единица, Максимальная(ПодходящаяЕдиница, Единица));
		КонецЕсли;	
	КонецЦикла;

	Если ПодходящаяЕдиница = Неопределено Тогда
		Возврат ЕдиницаИзмерения.Базовая;
	Иначе
		Возврат ПодходящаяЕдиница;
	КонецЕсли;

КонецФункции

Функция Конвертировать(Значение, ТекущаяЕдиница, ЦелеваяЕдиница) Экспорт
	БазовоеЗначение = Значение * ТекущаяЕдиница.ОтношениеКБазовой;
	Возврат БазовоеЗначение / ЦелеваяЕдиница.ОтношениеКБазовой;
КонецФункции

Функция НайтиПоБазовой(БазоваяЕдиница) Экспорт

	Результат = Новый Массив();
	Для Каждого ЕдиницаИзмерения Из _Список Цикл
		Если ЕдиницаИзмерения.Базовая = БазоваяЕдиница Тогда
			Результат.Добавить(ЕдиницаИзмерения);
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

_Список = Новый Массив();

Наносекунда = Создать("ns");
Микросекунда = Создать("us", Наносекунда, Pow(10, 3));
Миллисекунда = Создать("ms", Наносекунда, Pow(10, 6));
Секунда = Создать("s", Наносекунда, Pow(10, 9));

Байт = Создать("B");
Килобайт = Создать("KB", Байт, Pow(2, 10));
Мегабайт = Создать("MB", Байт, Pow(2, 20));
Гигабайт = Создать("GB", Байт, Pow(2, 30));

ОперацийВСекунду = Создать("op/s");