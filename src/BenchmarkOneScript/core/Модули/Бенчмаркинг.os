#Использовать logos

Перем _Лог; // Лог
Перем _РегуляркаЗапрещенныеСимволыВИмени; // РегулярноеВыражение

#Область ПрограммныйИнтерфейс

// Запускает бенчмарки
//
// Параметры:
//   ИсточникБенчмарков - Тип - Класс, содержащий бенчмарки
//                      - КоллекцияДескрипторовБенчмарков
//                      - ДескрипторБенчмарка
//                      - Произвольный - Экземпляр класса с бенчмарками
//   Конфигурация - КонфигурацияБенчмарков - Конфигурация бенчмарков
// 
// Возвращаемое значение:
//   РезультатЗапускаБенчмарков:
//     * СредаОкружения - СредаОкруженияБенчмарков
//     * Конфигурация - КонфигурацияБенчмарков
//     * ДескрипторыБенчмарков - КоллекцияДескрипторовБенчмарков
//     * Отчет - ОтчетБенчмарков
//     * Запуски - Массив из РезультатЗапускаБенчмаркаДто:
//       ** ДескрипторБенчмарка - ДескрипторБенчмарка
//       ** Параметры - Массив из ПараметрБенчмарка
//       ** Эталон - РезультатЗапускаБенчмаркаДто, Неопределено
//       ** ЭтоЭталон - Булево
//       ** Статистика - СтатистикаБенчмарка
//       ** ИсполняющаяСреда - ИсполняющаяСредаБенчмарковДто:
//          *** Версия - Строка
//          *** Алиас - Строка
//          *** Наименование - Строка
//       ** Замеры - Массив из РезультатИтерацииБенчмаркаДто:
//          *** Этап - см. ЭтапыБенчмарка
//          *** НомерИтерации - Число
//          *** КоличествоОпераций - Число
//          *** Наносекунд - Число - Наносекунд за итерацию
//          *** НаносекундЗаОперацию - Число - Наносекунд за операцию
//          *** ВыделяемаяПамять - Число - Байт выделяемой памяти за итерацию
//          *** ВыделяемаяПамятьЗаОперацию - Число - Байт выделяемой памяти за операцию
Функция Запустить(ИсточникБенчмарков, Конфигурация = Неопределено) Экспорт

	ЗапускательБенчмарков = Новый ЗапускательБенчмарков(ИсточникБенчмарков, Конфигурация);
	Возврат ЗапускательБенчмарков.Запустить();

КонецФункции

// Запускает все бенчмарки, найденные в указанном каталоге
//
// Параметры:
//   Каталог - Строка - Путь к каталогу с бенчмарками
//   ИскатьВПодкаталогах - Булево - Сканировать все вложенные каталоги
//   Конфигурация - КонфигурацияБенчмарков - Конфигурация, которая с приоритетом будет объединена
//                                           с каждой конфигурацией бенчмарка
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Результат - см. Бенчмаркинг.Запустить
//                 - Неопределено
//     * ИмяФайла - Строка - Имя файла
//     * ПолноеИмяФайла - Строка - Путь к файлу сценария
//     * Успешно - Булево - Индикатор корректного выполнения
Функция ЗапуститьИзКаталога(Каталог, ИскатьВПодкаталогах = Ложь, Конфигурация = Неопределено) Экспорт

	ЗапускательБенчмарковИзКаталога = Новый ЗапускательБенчмарковИзКаталога(Каталог, ИскатьВПодкаталогах, Конфигурация);
	Возврат ЗапускательБенчмарковИзКаталога.Запустить();

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Подключает файл бенчмарка как сценарий и возвращает его тип для дальнейшего запуска.
//
// Если ИмяТипа не задано, оно формируется из имени файла без расширения.
// При конфликте имён к нему добавляется уникальный суффикс.
//
// Параметры:
//   ПутьКФайлу - Строка - Полный к файлу бенчмарка (.os)
//   ИмяТипа    - Строка - Имя типа для регистрации сценария. Если не задано,
//                         вычисляется из имени файла автоматически.
//
// Возвращаемое значение:
//   Тип          - Тип подключённого сценария, если он содержит хотя бы один бенчмарк
//   Неопределено - Если файл не удалось подключить или бенчмарки в сценарии не найдены
Функция ПодключитьБенчмарк(ПутьКФайлу, ИмяТипа = "") Экспорт
			
	Если Не ЗначениеЗаполнено(ИмяТипа) Тогда
		ИмяТипа = ИмяТипаИзСтроки(Новый Файл(ПутьКФайлу).ИмяБезРасширения);	

		Если ТипСуществует(ИмяТипа) Тогда
			ИмяТипа = ИмяТипа + "_" + СтрЗаменить(Новый УникальныйИдентификатор(), "-", "");
		КонецЕсли;
	КонецЕсли;

	Попытка
		ПодключитьСценарий(ПутьКФайлу, ИмяТипа);
		Тип = Тип(ИмяТипа);
	Исключение
		_Лог.Ошибка(
			"Не удалось подключить файл бенчмарка <%1> по причине: %2",
			ПутьКФайлу,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке())
		);
		Возврат Неопределено;
	КонецПопытки;

	ДескрипторыБенчмарков = Новый КоллекцияДескрипторовБенчмарков(Тип);
	Если ДескрипторыБенчмарков.Количество() = 0 Тогда
		_Лог.Предупреждение("Сценарий <%1> не содержит бенчмарков", ИмяТипа);
		Возврат Неопределено;
	КонецЕсли;

	_Лог.Отладка("Найдено бенчмарков в сценарии <%1>: %2 шт.", ИмяТипа, ДескрипторыБенчмарков.Количество());

	Возврат Тип;

КонецФункции

Функция ЭтоВоркер() Экспорт
	Возврат ПолучитьПеременнуюСреды("BENCHMARK_WORKER") = "1";
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяТипаИзСтроки(Строка)

	Если _РегуляркаЗапрещенныеСимволыВИмени = Неопределено Тогда
		_РегуляркаЗапрещенныеСимволыВИмени = Новый РегулярноеВыражение("[^a-zA-Zа-яА-Я0-9_]");
	КонецЕсли;

	Возврат _РегуляркаЗапрещенныеСимволыВИмени.Заменить(Строка, "");

КонецФункции

Функция ТипСуществует(ИмяТипа)

	Попытка
		Тип = Тип(ИмяТипа); // BSLLS:UnusedLocalVariable-off
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;

КонецФункции

#КонецОбласти

_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.Бенчмаркинг");