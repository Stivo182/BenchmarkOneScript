#Использовать "../../custom-jason"

Перем _ПримитивныеТипы; // МножествоСоответствие, Неопределено

#Область ПрограммныйИнтерфейс

// Сериализует переданное значение в строку JSON.
//
// Параметры:
//   Значение - Произвольный - Значение, которое требуется сериализовать в JSON.
//
// Возвращаемое значение:
//   Строка - Строка JSON
Функция ВJson(Значение) Экспорт
	
	Сериализатор = Новый СериализаторJsonBench();
	Возврат Сериализатор.Сериализовать(Значение);

КонецФункции

// Десериализует строку JSON в объект указанного типа.
//
// Параметры:
//   Json - Строка - Строка JSON.
//   ТипОбъекта - Тип - Тип объекта, в который необходимо выполнить десериализацию.
//
// Возвращаемое значение:
//   Произвольный - Экземпляр объекта указанного типа.
Функция ИзJson(Json, ТипОбъекта) Экспорт
	
	Десериализатор = Новый ДесериализаторJsonBench();
	Возврат Десериализатор.Десериализовать(Json, ТипОбъекта);

КонецФункции

// Подготавливает значение для безопасного вывода.
//
// Параметры:
//   Значение - Произвольный - Любое значение для обработки
//
// Возвращаемое значение:
//   Произвольный
Функция ПривестиКПримитиву(Значение) Экспорт

	Если ЭтоПримитив(Значение) Тогда
		Возврат Значение;
	КонецЕсли;
		
	Возврат Строка(Значение);

КонецФункции

// Проверяет, относится ли значение к примитивным типам данных.
//
// Параметры:
//   Значение - Произвольный - Проверяемое значение
//
// Возвращаемое значение:
//   Булево
Функция ЭтоПримитив(Значение) Экспорт
	Возврат ПримитивныеТипы().Содержит(ТипЗнч(Значение));
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПримитивныеТипы()

	Если Не _ПримитивныеТипы = Неопределено Тогда
		Возврат _ПримитивныеТипы;
	КонецЕсли;

	_ПримитивныеТипы = Новый МножествоСоответствие();
	_ПримитивныеТипы.Добавить(Тип("Число"));
	_ПримитивныеТипы.Добавить(Тип("Строка"));
	_ПримитивныеТипы.Добавить(Тип("Булево"));
	_ПримитивныеТипы.Добавить(Тип("Дата"));
	_ПримитивныеТипы.Добавить(Тип("Неопределено"));
	_ПримитивныеТипы.Добавить(Тип("Null"));

	Возврат _ПримитивныеТипы;

КонецФункции

#КонецОбласти