Функция Представление(
	Значение, 
	ЕдиницаИзмерения = Неопределено, 
	ФорматнаяСтрока = "", 
	ДополнитьЕдиницейИзмерения = Истина) Экспорт
	
	Если ЗначениеЗаполнено(ФорматнаяСтрока) Тогда
		Представление = Формат(Значение, ФорматнаяСтрока);
	ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
		Представление = ПредставлениеЧисла(Значение, ЕдиницаИзмерения);
	ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
		Представление = ПредставлениеБулево(Значение);
	Иначе
		Представление = Строка(Значение);
	КонецЕсли;

	Если Не ЕдиницаИзмерения = Неопределено И ДополнитьЕдиницейИзмерения Тогда
		Представление = Представление + " " + ЕдиницаИзмерения.Обозначение;
	КонецЕсли;

	Возврат Представление;

КонецФункции

Функция ПредставлениеЧитаемое(
	Знач Значение, 
	ЕдиницаИзмерения = Неопределено, 
	ФорматнаяСтрока = "", 
	ДополнитьЕдиницейИзмерения = Истина) Экспорт

	Если Не ЕдиницаИзмерения = Неопределено Тогда
		ЕдиницаИзмеренияДружелюбная = ЕдиницыИзмеренийБенчмарков.Подобрать(Значение, ЕдиницаИзмерения);
		Значение = ЕдиницыИзмеренийБенчмарков.Конвертировать(Значение, ЕдиницаИзмерения, ЕдиницаИзмеренияДружелюбная);
	Иначе
		ЕдиницаИзмеренияДружелюбная = ЕдиницаИзмерения;
	КонецЕсли;

	Возврат Представление(Значение, ЕдиницаИзмеренияДружелюбная, ФорматнаяСтрока, ДополнитьЕдиницейИзмерения);

КонецФункции

Функция ПредставлениеЧисла(Значение, ЕдиницаИзмерения = Неопределено) Экспорт

	Разрядность = ПодобратьРазрядностьДробнойЧасти(Значение, ЕдиницаИзмерения);

	Представление = Формат(Значение, ФорматнаяСтрокаЧисла(Разрядность));

	Возврат Представление;

КонецФункции

Функция ПодобратьРазрядностьДробнойЧасти(Знач Значение, ЕдиницаИзмерения = Неопределено) Экспорт

	ЭтоВремя = Не ЕдиницаИзмерения = Неопределено
		И (ЕдиницаИзмерения = ЕдиницыИзмеренийБенчмарков.Наносекунда
		Или ЕдиницаИзмерения.Базовая = ЕдиницыИзмеренийБенчмарков.Наносекунда);

	МинимальнаяРазрядность = ?(ЭтоВремя, 1, 0);
	МаксимальнаяРазрядность = 4;

	Если Значение = 0 Тогда
		Возврат МинимальнаяРазрядность;
	КонецЕсли;

	Значение = ?(Значение < 0, -Значение, Значение);
	Разрядность = 3 - Цел(Log10(Значение));
	Разрядность = Макс(Разрядность, МинимальнаяРазрядность);
	Разрядность = Мин(Разрядность, МаксимальнаяРазрядность);

	Значение = Окр(Значение, Разрядность);

	Разрядность = Макс(РазрядностьЧисла(Значение), МинимальнаяРазрядность);

	Возврат Разрядность;

КонецФункции

Функция ФорматнаяСтрокаЧисла(Разрядность = Неопределено) Экспорт
	
	Шаблон = "ЧРД=.; ЧРГ=,; ЧН=%1;";
	
	Если Не Разрядность = Неопределено Тогда
		Шаблон = Шаблон + " ЧДЦ=" + Разрядность;
	КонецЕсли;

	Если Не Разрядность = Неопределено И Разрядность > 0 Тогда
		ПредставлениеНуля = "0." + Лев("0000000000", Разрядность);
	Иначе
		ПредставлениеНуля = "0";
	КонецЕсли;

	Шаблон = СтрШаблон(Шаблон, ПредставлениеНуля);

	Возврат Шаблон;

КонецФункции

Функция ПредставлениеБулево(Значение) Экспорт
	Возврат Формат(Значение, "БЛ=False; БИ=True");
КонецФункции

Функция ФиксированнаяСтрока(
	Знач Строка, 
	Знач ТребуемаяДлина,
	ПоложениеПраво = Ложь,
	ЗаполняемыйСимвол = " ",
	ОборачиватьЗаполняемымСимволом = Ложь) Экспорт
	
	Если ОборачиватьЗаполняемымСимволом Тогда
		Строка = ЗаполняемыйСимвол + Строка + ЗаполняемыйСимвол;
	КонецЕсли;
	
	КоличествоСимволов = ТребуемаяДлина - СтрДлина(Строка);
	Если КоличествоСимволов <= 0 Тогда
		Возврат Строка;
	КонецЕсли;

	ДобавочныеСимволы = Новый Массив();
	Для Инд = 1 По КоличествоСимволов Цикл
		ДобавочныеСимволы.Добавить(ЗаполняемыйСимвол);
	КонецЦикла;
	
	ДобавочныеСимволы = СтрСоединить(ДобавочныеСимволы);

	Если ПоложениеПраво Тогда
		Возврат ДобавочныеСимволы + Строка;
	Иначе
		Возврат Строка + ДобавочныеСимволы;
	КонецЕсли;

КонецФункции

Функция РазрядностьЧисла(Знач Значение)

	Значение = ?(Значение < 0, -Значение, Значение);
	Значение = Значение - Цел(Значение);

	Если Значение = 0 Тогда
		Возврат 0;
	КонецЕсли;

	Возврат СтрДлина(СтрЗаменить(Значение, "0.", ""));

КонецФункции