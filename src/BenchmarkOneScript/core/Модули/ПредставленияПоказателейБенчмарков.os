Функция Представление(
	Значение, 
	ЕдиницаИзмерения = Неопределено, 
	ФорматнаяСтрока = "", 
	ДополнитьЕдиницейИзмерения = Истина) Экспорт
	
	Если ЗначениеЗаполнено(ФорматнаяСтрока) Тогда
		Представление = Формат(Значение, ФорматнаяСтрока);
	ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
		Представление = ПредставлениеЧисла(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
		Представление = ПредставлениеБулево(Значение);
	Иначе
		Представление = Строка(Значение);
	КонецЕсли;

	Если Не ЕдиницаИзмерения = Неопределено И ДополнитьЕдиницейИзмерения Тогда
		Представление = Представление + " " + ЕдиницаИзмерения.Обозначение;
	КонецЕсли;

	Возврат Представление;

КонецФункции

Функция ПредставлениеЧитаемое(
	Знач Значение, 
	ЕдиницаИзмерения = Неопределено, 
	ФорматнаяСтрока = "", 
	ДополнитьЕдиницейИзмерения = Истина) Экспорт

	Если Не ЕдиницаИзмерения = Неопределено Тогда
		ЕдиницаИзмеренияДружелюбная = ЕдиницыИзмеренийБенчмарков.Подобрать(Значение, ЕдиницаИзмерения);
		Значение = ЕдиницыИзмеренийБенчмарков.Конвертировать(Значение, ЕдиницаИзмерения, ЕдиницаИзмеренияДружелюбная);
	Иначе
		ЕдиницаИзмеренияДружелюбная = ЕдиницаИзмерения;
	КонецЕсли;

	Возврат Представление(Значение, ЕдиницаИзмеренияДружелюбная, ФорматнаяСтрока, ДополнитьЕдиницейИзмерения);

КонецФункции

Функция ПредставлениеЧисла(Знач Значение, Разрядность = Неопределено) Экспорт

	МинимальнаяТочность = 1;
	МаксимальнаяТочность = 4;

	Если Значение <> 0 Тогда
		Точность = 3 - Цел(Log10(?(Значение < 0, -Значение, Значение)));
		Точность = Макс(Точность, МинимальнаяТочность);
		Точность = Мин(Точность, МаксимальнаяТочность);

		Значение = Окр(Значение, Точность);
	КонецЕсли;

	Представление = Формат(Значение, ФорматнаяСтрокаЧисла(Разрядность));

	Возврат Представление;

КонецФункции

Функция ФорматнаяСтрокаЧисла(Разрядность = Неопределено) Экспорт
	Возврат "ЧРД=.; ЧРГ=,; ЧН=0;" + ?(Разрядность = Неопределено, "", " ЧДЦ=" + Разрядность);
КонецФункции

Функция ПредставлениеБулево(Значение) Экспорт
	Возврат Формат(Значение, "БЛ=false; БИ=true");
КонецФункции

Функция ФиксированнаяСтрока(
	Знач Строка, 
	Знач ТребуемаяДлина,
	ПоложениеПраво = Ложь,
	ЗаполняемыйСимвол = " ",
	ОборачиватьЗаполняемымСимволом = Ложь) Экспорт
	
	Если ОборачиватьЗаполняемымСимволом Тогда
		Строка = ЗаполняемыйСимвол + Строка + ЗаполняемыйСимвол;
	КонецЕсли;
	
	КоличествоСимволов = ТребуемаяДлина - СтрДлина(Строка);
	Если КоличествоСимволов <= 0 Тогда
		Возврат Строка;
	КонецЕсли;

	ДобавочныеСимволы = Новый Массив();
	Для Инд = 1 По КоличествоСимволов Цикл
		ДобавочныеСимволы.Добавить(ЗаполняемыйСимвол);
	КонецЦикла;
	
	ДобавочныеСимволы = СтрСоединить(ДобавочныеСимволы);

	Если ПоложениеПраво Тогда
		Возврат ДобавочныеСимволы + Строка;
	Иначе
		Возврат Строка + ДобавочныеСимволы;
	КонецЕсли;

КонецФункции