Перем _ОтчетБенчмарков; // ОтчетБенчмарков

// Конвертер отчета бенчмарков в Markdown
//
// Параметры:
//   ОтчетБенчмарков - ОтчетБенчмарков
Процедура ПриСозданииОбъекта(ОтчетБенчмарков)

	_ОтчетБенчмарков = ОтчетБенчмарков;

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Конвертирует отчет бенчмарков в Markdown
//
// Возвращаемое значение:
//   Строка
Функция Конвертировать() Экспорт
	
	Результат = Новый Массив();

	ДобавитьЗаголовкиКолонок(Результат);
	ДобавитьСтроки(Результат);

	Возврат СтрСоединить(Результат, Символы.ПС);

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьЗаголовкиКолонок(Результат)

	ПодстрокиЗаголовки = Новый Массив();
	ПодстрокиРазделитель = Новый Массив();

	Для Каждого Колонка Из _ОтчетБенчмарков.Колонки Цикл
	
		ПоложениеПраво = Колонка.ЭтоЧисло;
		Размер = Колонка.Размер + ДобавочныхСимволов();

		ПодстрокиЗаголовки.Добавить(РазделительКолонок());
		ПодстрокиЗаголовки.Добавить(ПредставленияПоказателейБенчмарков.ФиксированнаяСтрока(Колонка.Заголовок, Размер, ПоложениеПраво, , Истина));
		
		ПодстрокиРазделитель.Добавить(РазделительКолонок());
		ПодстрокиРазделитель.Добавить(ПредставленияПоказателейБенчмарков.ФиксированнаяСтрока(?(ПоложениеПраво, ":", ""), Размер, Истина, "-", Ложь));	

	КонецЦикла;

	ПодстрокиЗаголовки.Добавить(РазделительКолонок());
	ПодстрокиРазделитель.Добавить(РазделительКолонок());

	Результат.Добавить(СтрСоединить(ПодстрокиЗаголовки));
	Результат.Добавить(СтрСоединить(ПодстрокиРазделитель));

КонецПроцедуры

Процедура ДобавитьСтроки(Результат)

	Для Каждого СтрокаСтатистики Из _ОтчетБенчмарков.Таблица Цикл

		Подстроки = Новый Массив();

		Для Каждого Колонка Из _ОтчетБенчмарков.Колонки Цикл
			
			ПоложениеПраво = Колонка.ЭтоЧисло;
			Размер = Колонка.Размер + ДобавочныхСимволов();
			Значение = СтрокаСтатистики[Колонка.Имя];
			Представление = Колонка.ПредставлениеЗначения(Значение, СтрокаСтатистики);

			Подстроки.Добавить(РазделительКолонок());
			Подстроки.Добавить(ПредставленияПоказателейБенчмарков.ФиксированнаяСтрока(Представление, Размер, ПоложениеПраво, , Истина));	

		КонецЦикла;

		Подстроки.Добавить(РазделительКолонок());
		Результат.Добавить(СтрСоединить(Подстроки));

	КонецЦикла;
	
КонецПроцедуры

Функция РазделительКолонок()
	Возврат "|";
КонецФункции

Функция ДобавочныхСимволов()
	Возврат 2;
КонецФункции

#КонецОбласти