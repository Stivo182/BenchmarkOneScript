#Использовать delegate
#Использовать validate
#Использовать reflector
#Использовать "../../dto"

&Сериализуемое("Type")
&Тип("Тип")
&Заполнено
Перем _Тип; // Тип - Класс бенчмарков

&Сериализуемое("Method")
&Тип("Строка")
&Заполнено
Перем _Метод; // Строка - Метод бенчмарка

&Сериализуемое("Baseline")
&Тип("Булево")
Перем _ЭтоЭталон; // Булево

&Сериализуемое("Category")
&Тип("Строка")
Перем _Категория; // Строка

&Сериализуемое("ParameterSets")
&Тип("Массив") &ДляКаждого &Тип("ПараметрыМетодаБенчмарка")
Перем _НаборыПараметров; // Массив из ПараметрыМетодаБенчмарка

&Сериализуемое("ParameterSources")
&Тип("Массив")
Перем _ИсточникиПараметров; // Массив из Строка, Делегат

&Несериализуемое
Перем _ИменаПараметров; // Массив из Строка

#Область ОбработчикиСобытий

// Дескриптор бенчмарка
//
// Параметры:
//   Объект - Произвольный - Тип или экземпляр класса бенчмарков
//   Метод - Строка - Метод бенчмарка
Процедура ПриСозданииОбъекта(Объект = Неопределено, Метод = "")

	_Тип = ?(ТипЗнч(Объект) = Тип("Тип"), Объект, ТипЗнч(Объект));
	_Метод = Метод;
	_НаборыПараметров = Новый Массив();
	_ЭтоЭталон = Ложь;
	_Категория = "";
	_ИсточникиПараметров = Новый Массив();

	Если Не Объект = Неопределено Тогда
		ИзвлечьИменаПараметров(Объект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Класс бенчмарков
//
// Возвращаемое значение:
//   Тип
Функция ТипОбъекта() Экспорт
	Возврат _Тип;
КонецФункции

// Метод бенчмарка
//
// Возвращаемое значение:
//   Строка
Функция Метод() Экспорт
	Возврат _Метод;
КонецФункции

// Наборы параметров, используемые для запуска бенчмарков с различными входными данными.
//
// Возвращаемое значение:
//   Массив из ПараметрыМетодаБенчмарка
Функция НаборыПараметров() Экспорт
	Возврат Новый Массив(Новый ФиксированныйМассив(_НаборыПараметров));
КонецФункции

// Добавляет параметры бенчмарка, позволяя запускать тесты с различными наборами входных данных.
// Принимает значения только примитивного типа. Для составных типов используйте метод ДобавитьИсточникПараметров().
//
// Параметры:
//   Параметры - Массив из Произвольный - Параметры бенчмарка. Длина массива должна соответствовать 
//                                        количеству объявленных параметров метода.
//                                        Значения должны быть примитивного типа.
//             - Произвольный - Значение единственного параметра бенчмарка (примитивного типа).
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ДобавитьПараметры(Параметры) Экспорт
	
	ПараметрыМетода = Новый ПараметрыМетодаБенчмарка();
	КоличествоТребуемыхПараметров = _ИменаПараметров.Количество();

	Если КоличествоТребуемыхПараметров = 0 Тогда		
		ВызватьИсключение СтрШаблон(
			"Метод бенчмарка <%1> не имеет объявленных параметров. Передача параметров невозможна.",
			_Метод
		);
	КонецЕсли;

	Если ТипЗнч(Параметры) = Тип("Массив") Тогда

		Если Параметры.Количество() <> КоличествоТребуемыхПараметров Тогда		
			ВызватьИсключение СтрШаблон(
				"Количество переданных параметров (%1) не соответствует ожидаемому количеству (%2) для метода бенчмарка <%3>.",
				Параметры.Количество(),
				КоличествоТребуемыхПараметров,
				_Метод
			);
		КонецЕсли;

		Для ИндексПараметра = 0 По Параметры.ВГраница() Цикл
			Параметр = Новый ПараметрБенчмарка(_ИменаПараметров[ИндексПараметра], Параметры[ИндексПараметра], Истина);
			ПараметрыМетода.Добавить(Параметр);
		КонецЦикла;

	Иначе

		Если КоличествоТребуемыхПараметров > 1 Тогда
			ВызватьИсключение СтрШаблон(
				"Передача одиночного значения недопустима для метода бенчмарка <%1>, так как он ожидает %2 параметра(ов).",
				_Метод,
				КоличествоТребуемыхПараметров
			);
		КонецЕсли;

		Параметр = Новый ПараметрБенчмарка(_ИменаПараметров[0], Параметры, Истина);
		ПараметрыМетода.Добавить(Параметр);	

	КонецЕсли;

	_НаборыПараметров.Добавить(ПараметрыМетода);

	Возврат ЭтотОбъект;

КонецФункции

// Очищает набор параметров
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ОчиститьПараметры() Экспорт

	_НаборыПараметров.Очистить();

	Возврат ЭтотОбъект;

КонецФункции

// Указывает имя экспортного метода, возвращающего коллекцию параметров для бенчмарка
//
// Если метод бенчмарка принимает более одного параметра, возвращаемая коллекция должна быть одного из следующих типов:
//   * Массив из Произвольный     - Элементы передаются в параметры метода по порядку.
//                                  Если количество элементов не совпадает с количеством параметров, 
//                                  возникает исключение.
//   * Структура или Соответствие - Ключи коллекции сопоставляются с именами параметров метода.
//   * СтрокаТаблицыЗначений      - Колонки таблицы сопоставляются с именами параметров метода.
//
// Параметры:
//   Источник - Строка - Имя экспортного метода. Может быть задано в формате: 
//                       <ИмяТипаИлиМодуля>.<ИмяМетода> или <ИмяМетода> (в сценарии бенчмарка).
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ДобавитьИсточникПараметров(Источник) Экспорт

	Если _ИсточникиПараметров.Найти(Источник) = Неопределено Тогда
		 _ИсточникиПараметров.Добавить(Источник);
	КонецЕсли;

	Возврат ЭтотОбъект;

КонецФункции

// Источники параметров бенчмарка
//
// Возвращаемое значение:
//   Массив из Строка - Имя экспортного метода
Функция ИсточникиПараметров() Экспорт
	Возврат Новый Массив(Новый ФиксированныйМассив(_ИсточникиПараметров));
КонецФункции

// Очищает источники параметров бенчмарка
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ОчиститьИсточникиПараметров() Экспорт
	_ИсточникиПараметров.Очистить();
	Возврат ЭтотОбъект;
КонецФункции

// Бенчмарк используется в качестве эталонного
//
// Возвращаемое значение:
//   Булево
Функция ЭтоЭталон() Экспорт
	Возврат _ЭтоЭталон;
КонецФункции

// Использовать бенчмарк в качестве эталонного
//
// Результаты выполнения эталонного бенчмарка используются как базовая метрика 
// для оценки эффективности других тестов.
//
// Параметры:
//   Флаг - Булево - Флаг для пометки бенчмарка как эталонного
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ИспользоватьКакЭталон(Флаг = Истина) Экспорт

	_ЭтоЭталон = Флаг;

	Возврат ЭтотОбъект;

КонецФункции

// Категория бенчмарка
//
// Возвращаемое значение:
//   Строка
Функция Категория() Экспорт
	Возврат _Категория;
КонецФункции

// Устанавливает категорию для группировки бенчмарков в отчетах
// Позволяет логически объединять тесты по функциональности, типу нагрузки или другим критериям.
//
// Параметры:
//   Категория - Строка
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция УстановитьКатегорию(Категория) Экспорт

	_Категория = Категория;

	Возврат ЭтотОбъект;

КонецФункции

// Имена параметров метода бенчмарка
//
// Возвращаемое значение:
//   Массив из Строка
Функция ИменаПараметров() Экспорт
	Возврат Новый Массив(Новый ФиксированныйМассив(_ИменаПараметров));
КонецФункции

// Делегат
//
// Параметры:
//   Объект - Произвольный - Экземпляр класса бенчмарков
//   Параметры - Массив из Произвольный - Параметры вызываемого метода
//
// Возвращаемое значение:
//   Делегат
Функция Делегат(Объект, Параметры) Экспорт
	Возврат Делегаты.Создать(Объект, _Метод, Параметры);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Сериализация

&ПослеДесериализации
Процедура ПослеДесериализации() Экспорт

	Валидировать();
	ИзвлечьИменаПараметров(Новый(_Тип));

КонецПроцедуры

Процедура Валидировать()
	
	Валидатор = Новый Валидатор;

	Результат = Валидатор.Валидировать(ЭтотОбъект);
	Если Результат.Количество() > 0 Тогда
		ВызватьИсключение Валидатор.ОписаниеОшибокВалидации(Результат);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Процедура ИзвлечьИменаПараметров(Объект)

	РефлекторОбъекта = Новый РефлекторОбъекта(Объект);
	ТаблицаМетодов = РефлекторОбъекта.ПолучитьТаблицуМетодов(, Ложь);

	СвойстваМетода = ТаблицаМетодов.Найти(Метод(), "Имя");

	_ИменаПараметров = СвойстваМетода.Параметры.ВыгрузитьКолонку("Имя");

КонецПроцедуры

#КонецОбласти