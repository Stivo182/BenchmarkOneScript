// BSLLS:UnusedLocalVariable-off
// BSLLS:ExportVariables-off

#Использовать fluent
#Использовать asserts

&Сериализуемое("OriginalValues")
Перем _ЗамерыВремени; // Массив из Число

&Сериализуемое("N")
&Тип("Число")
Перем КоличествоИтераций Экспорт; // Число

&Сериализуемое("Min")
&Тип("Число")
Перем Мин Экспорт; // Число

&Сериализуемое("Q1")
&Тип("Число")
Перем НижнийКвартиль Экспорт; // Число

&Сериализуемое("Median")
&Тип("Число")
Перем Медиана Экспорт; // Число

&Сериализуемое("Mean")
&Тип("Число")
Перем Среднее Экспорт; // Число

&Сериализуемое("Q3")
&Тип("Число")
Перем ВерхнийКвартиль Экспорт; // Число

&Сериализуемое("Max")
&Тип("Число")
Перем Макс Экспорт; // Число

&Сериализуемое("StdDev")
&Тип("Число")
Перем СтандартноеОтклонение Экспорт; // Число

&Сериализуемое("StdErr")
&Тип("Число")
Перем СтандартнаяОшибкаСреднего Экспорт; // Число

&Сериализуемое("Ops")
&Тип("Число")
Перем ОперацийВСекунду Экспорт; // Число

&Сериализуемое("BytesAllocatedPerOperation")
&Тип("Число")
Перем ВыделяемаяПамять Экспорт; // Число

&НеСериализуемое
Перем _УпорядоченныеЗамерыВремени; // Массив из Число

&НеСериализуемое
Перем _ЗамерыПамяти; // Массив из Число

&НеСериализуемое
Перем _ИсходныеПоказатели; // Структура

&НеСериализуемое
Перем _ТекущаяЕдиницаВремени; // ЕдиницаИзмеренияБенчмарка

#Область ОбработчикиСобытий

// Статистика бенчмарка
//
// Параметры:
//   Замеры - Массив из РезультатИтерацииБенчмаркаДто
//          - Структура:
//              * ЗамерыВремени - Массив из Число
//              * ЗамерыПамяти - Массив из Число
Процедура ПриСозданииОбъекта(Замеры = Неопределено)

	_ИсходныеПоказатели = Новый Структура();

	Для Каждого Имя Из ИменаПолейВремени() Цикл
		_ИсходныеПоказатели.Вставить(Имя, 0);
	КонецЦикла;

	Если Не Замеры = Неопределено Тогда
		Прочитать(Замеры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Значение, СтандартнаяОбработка)

    СтандартнаяОбработка = Ложь;

	Результат = Новый Массив();

	// Mean
	СреднееЧитаемое = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
		Среднее,
		_ТекущаяЕдиницаВремени);

	Результат.Добавить(СтрШаблон("Mean = %1, ", СреднееЧитаемое));

	// StdDev 
	СтандартноеОтклонениеЧитаемое = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
		СтандартноеОтклонение,
		_ТекущаяЕдиницаВремени);

	Результат.Добавить(СтрШаблон("StdDev = %1, ", СтандартноеОтклонениеЧитаемое));

	// StdErr
	СтандартнаяОшибкаСреднегоЧитаемое = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
		СтандартнаяОшибкаСреднего,
		_ТекущаяЕдиницаВремени);

	Результат.Добавить(СтрШаблон("StdErr = %1, ", СтандартнаяОшибкаСреднегоЧитаемое));

	// N
	Результат.Добавить(СтрШаблон("N = %1, ", _ЗамерыВремени.Количество()));

	// Op/s
	Результат.Добавить(СтрШаблон("Op/s = %1", ПредставленияПоказателейБенчмарков.Представление(ОперацийВСекунду)));

	// Перенос
	Результат.Добавить(Символы.ПС);

	// Min 
	МинЧитаемое = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
		Мин,
		_ТекущаяЕдиницаВремени);

	Результат.Добавить(СтрШаблон("Min = %1, ", МинЧитаемое));

	// Q1 
	НижнийКвартильЧитаемое = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
		НижнийКвартиль,
		_ТекущаяЕдиницаВремени);

	Результат.Добавить(СтрШаблон("Q1 = %1, ", НижнийКвартильЧитаемое));

	// Median 
	МедианаЧитаемое = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
		Медиана,
		_ТекущаяЕдиницаВремени);

	Результат.Добавить(СтрШаблон("Median = %1, ", МедианаЧитаемое));

	// Q3 
	ВерхнийКвартильЧитаемое = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
		ВерхнийКвартиль,
		_ТекущаяЕдиницаВремени);

	Результат.Добавить(СтрШаблон("Q3 = %1, ", ВерхнийКвартильЧитаемое));

	// Max 
	МаксЧитаемое = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
		Макс,
		_ТекущаяЕдиницаВремени);

	Результат.Добавить(СтрШаблон("Max = %1", МаксЧитаемое));

	// Allocated
	Если ЗначениеЗаполнено(ВыделяемаяПамять) Тогда
		ВыделяемаяПамятьЧитаемое = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
			ВыделяемаяПамять,
			ЕдиницыИзмеренийБенчмарков.Байт);

		Результат.Добавить(Символы.ПС);
		Результат.Добавить(СтрШаблон("Allocated = %1", ВыделяемаяПамятьЧитаемое));
	КонецЕсли;

	Значение = СтрСоединить(Результат, "");

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Квантиль
//
// Параметры:
//   Вероятность - Число
//
// Возвращаемое значение:
//   Число
Функция Квантиль(Вероятность) Экспорт

	Квантиль = МатематическиеФункцииБенчмарков.Квантиль(_УпорядоченныеЗамерыВремени, Вероятность);
	Квантиль = ЕдиницыИзмеренийБенчмарков.Конвертировать(
		Квантиль, 
		ЕдиницыИзмеренийБенчмарков.Наносекунда, 
		_ТекущаяЕдиницаВремени
	);

	Возврат Квантиль;

КонецФункции

// Читает замеры и формирует статистику
//
// Параметры:
//   Замеры - Массив из РезультатИтерацииБенчмаркаДто
//          - Структура:
//              * ЗамерыВремени - Массив из Число
//              * ЗамерыПамяти - Массив из Число
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция Прочитать(Замеры) Экспорт

	Если ТипЗнч(Замеры) = Тип("Массив") Тогда	
		_ЗамерыВремени = ВыгрузитьЗамеры(Замеры, "НаносекундЗаОперацию", ЭтапыБенчмарка.Измерение);
		_ЗамерыПамяти = ВыгрузитьЗамеры(Замеры, "ВыделяемаяПамятьЗаОперацию", ЭтапыБенчмарка.Память);
	ИначеЕсли ТипЗнч(Замеры) = Тип("Структура") Тогда
		_ЗамерыВремени = Замеры.ЗамерыВремени;
		_ЗамерыПамяти = Замеры.ЗамерыПамяти;
	Иначе
		ВызватьИсключение СтрШаблон("Замеры с типом %1 не поддерживаются", ТипЗнч(Замеры));
	КонецЕсли;

	Ожидаем.Что(_ЗамерыВремени, "Набор замеров не должен быть пустым").Заполнено();
	
	_УпорядоченныеЗамерыВремени = ПроцессорыКоллекций.ИзКоллекции(_ЗамерыВремени)
		.Сортировать()
		.ВМассив();
	
	Рассчитать();

	Возврат ЭтотОбъект;

КонецФункции

// Конвертирует показатели времени в секунды
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ВСекунды() Экспорт
	КонвертироватьВремя(ЕдиницыИзмеренийБенчмарков.Секунда);
	Возврат ЭтотОбъект;
КонецФункции

// Конвертирует показатели времени в миллисекунды
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ВМиллисекунды() Экспорт
	КонвертироватьВремя(ЕдиницыИзмеренийБенчмарков.Миллисекунда);
	Возврат ЭтотОбъект;
КонецФункции

// Конвертирует показатели времени в наносекунды
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ВНаносекунды() Экспорт
	КонвертироватьВремя(ЕдиницыИзмеренийБенчмарков.Наносекунда);
	Возврат ЭтотОбъект;
КонецФункции

// Вычисляет отношение соответствующих элементов замеров времени и памяти текущей статистики 
// к элементам переданной статистики и возвращает новый объект СтатистикаБенчмарка с результатами.
//
// Параметры:
//   Статистика - СтатистикаБенчмарка
//
// Возвращаемое значение:
//   СтатистикаБенчмарка
Функция Разделить(Статистика) Экспорт
	
	ЗамерыВремени = МатематическиеФункцииБенчмарков.Разделить(_ЗамерыВремени, Статистика.ЗамерыВремени());
	ЗамерыПамяти = МатематическиеФункцииБенчмарков.Разделить(_ЗамерыПамяти, Статистика.ЗамерыПамяти());

	Замеры = Новый Структура("ЗамерыВремени, ЗамерыПамяти", ЗамерыВремени, ЗамерыПамяти);

	Возврат Новый СтатистикаБенчмарка(Замеры);

КонецФункции

// Замеры времени выполнения
//
// Возвращаемое значение:
//   Массив из Число
Функция ЗамерыВремени() Экспорт
	Возврат Новый Массив(Новый ФиксированныйМассив(_ЗамерыВремени));
КонецФункции

// Замеры памяти выполнения
//
// Возвращаемое значение:
//   Массив из Число
Функция ЗамерыПамяти() Экспорт
	Возврат Новый Массив(Новый ФиксированныйМассив(_ЗамерыПамяти));
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Сериализация

&Сериализуемое("Percentiles")
Функция ПолучитьПроцентили() Экспорт
	
	Процентили = Новый Структура("P0, P25, P50, P67, P80, P85, P90, P95, P100");

	Для Каждого Строка Из Процентили Цикл
		Процент = Число(Сред(Строка.Ключ, 2));
		Процентили[Строка.Ключ] = Квантиль(Процент / 100);
	КонецЦикла;

	Возврат Процентили;
	
КонецФункции

#КонецОбласти

Процедура Рассчитать()
	
	КоличествоИтераций = _ЗамерыВремени.Количество();
	НаносекундВСекунде = ЕдиницыИзмеренийБенчмарков.Секунда.ОтношениеКБазовой;

	_ИсходныеПоказатели.Среднее = МатематическиеФункцииБенчмарков.Среднее(_ЗамерыВремени);
	_ИсходныеПоказатели.СтандартноеОтклонение = МатематическиеФункцииБенчмарков.СтандартноеОтклонение(_ЗамерыВремени);
	_ИсходныеПоказатели.СтандартнаяОшибкаСреднего = МатематическиеФункцииБенчмарков.СтандартнаяОшибкаСреднего(
		_ИсходныеПоказатели.СтандартноеОтклонение,
		КоличествоИтераций
	);
		
	Квартили = МатематическиеФункцииБенчмарков.Квартили(_УпорядоченныеЗамерыВремени);

	_ИсходныеПоказатели.Мин = Квартили.Q0;
	_ИсходныеПоказатели.НижнийКвартиль = Квартили.Q1;
	_ИсходныеПоказатели.Медиана = Квартили.Q2;
	_ИсходныеПоказатели.ВерхнийКвартиль = Квартили.Q3;
	_ИсходныеПоказатели.Макс = Квартили.Q4;
	
	ОперацийВСекунду = ?(_ИсходныеПоказатели.Среднее = 0, 0, НаносекундВСекунде / _ИсходныеПоказатели.Среднее);
	ВыделяемаяПамять = МатематическиеФункцииБенчмарков.Среднее(_ЗамерыПамяти);
	
	КонвертироватьВремя(_ТекущаяЕдиницаВремени);

КонецПроцедуры

Процедура КонвертироватьВремя(Единица)

	Если Единица = Неопределено Тогда
		_ТекущаяЕдиницаВремени = ЕдиницыИзмеренийБенчмарков.Наносекунда;
	Иначе
		_ТекущаяЕдиницаВремени = Единица;
	КонецЕсли;

	Для Каждого Имя Из ИменаПолейВремени() Цикл
		ЭтотОбъект[Имя] = ЕдиницыИзмеренийБенчмарков.Конвертировать(
			_ИсходныеПоказатели[Имя], 
			ЕдиницыИзмеренийБенчмарков.Наносекунда, 
			_ТекущаяЕдиницаВремени
		);
	КонецЦикла;

КонецПроцедуры

Функция ИменаПолейВремени()
	
	Результат = Новый Массив();
	Результат.Добавить("Среднее");
	Результат.Добавить("СтандартноеОтклонение");
	Результат.Добавить("СтандартнаяОшибкаСреднего");
	Результат.Добавить("Медиана");
	Результат.Добавить("НижнийКвартиль");
	Результат.Добавить("ВерхнийКвартиль");
	Результат.Добавить("Мин");
	Результат.Добавить("Макс");

	Возврат Результат;

КонецФункции

Функция ВыгрузитьЗамеры(Замеры, Колонка, Этап)

	Результат = Новый Массив();

	Для Каждого Замер Из Замеры Цикл
		Если Замер.Этап = Этап Тогда
			Результат.Добавить(Замер[Колонка]);
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти