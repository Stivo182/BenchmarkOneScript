#Использовать logos
#Использовать "../../custom-jason"

Перем _Лог; // Лог

#Область Конструктор

// Сериализатор результатов бенчмарков
Процедура ПриСозданииОбъекта()
	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.СериализаторРезультатовБенчмарков");
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Сериализует результаты бенчмарков в JSON-файл
//
// Параметры:
//   РезультатыБенчмарков - РезультатыБенчмарков
//   ПолноеИмяФайла - Строка - Полный путь к файлу, в который будут записаны результаты бенчмарков
Процедура ЗаписатьВJson(РезультатыБенчмарков, ПолноеИмяФайла) Экспорт

	_Лог.Отладка("Начало сериализации результатов в JSON-файл: %1", ПолноеИмяФайла);

	Сериализатор = Новый СериализаторJsonBench();
	Json = Сериализатор.Сериализовать(РезультатыБенчмарков);
	
	ЗаписьТекста = Новый ЗаписьТекста(ПолноеИмяФайла, "UTF-8");
	ЗаписьТекста.Записать(Json);
	ЗаписьТекста.Закрыть();

	_Лог.Отладка("Сериализация в JSON-файл завершена: %1", ПолноеИмяФайла);

КонецПроцедуры

// Десериализует результаты бенчмарков из JSON-файла
//
// Параметры:
//   ПолноеИмяФайла - Строка - Полный путь к JSON-файлу с результатами бенчмарков
//
// Возвращаемое значение:
//   РезультатыБенчмарков
Функция ПрочитатьИзJson(ПолноеИмяФайла) Экспорт
		
	_Лог.Отладка("Начало десериализации из JSON-файла: %1", ПолноеИмяФайла);

	ЧтениеТекста = Новый ЧтениеТекста(ПолноеИмяФайла, "UTF-8");
	Json = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	Десериализатор = Новый ДесериализаторJsonBench();
	РезультатыБенчмарков = Десериализатор.Десериализовать(Json, Тип("РезультатыБенчмарков"));

	_Лог.Отладка("Десериализация из JSON-файла завершена: %1", ПолноеИмяФайла);

	Возврат РезультатыБенчмарков;

КонецФункции

#КонецОбласти