#Использовать logos

Перем _Лог; // Лог

#Область ОбработчикиСобытий

// Сериализатор результатов бенчмарков
Процедура ПриСозданииОбъекта()
	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.СериализаторРезультатовБенчмарков");
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Сериализует результат запуска бенчмарков в JSON-файл
//
// Параметры:
//   РезультатЗапускаБенчмарков - РезультатЗапускаБенчмарков
//   ПолноеИмяФайла - Строка - Полный путь к файлу, в который будут записаны результат запуска бенчмарков
Процедура ЗаписатьВJson(РезультатЗапускаБенчмарков, ПолноеИмяФайла) Экспорт

	_Лог.Отладка("Начало сериализации результатов в JSON-файл: %1", ПолноеИмяФайла);

	ЗаписьТекста = Новый ЗаписьТекста(ПолноеИмяФайла, КодировкаТекста.UTF8);
	ЗаписьТекста.Записать(СериализацияОбъектовБенчмаркинга.ВJson(РезультатЗапускаБенчмарков));
	ЗаписьТекста.Закрыть();

	_Лог.Отладка("Сериализация в JSON-файл завершена: %1", ПолноеИмяФайла);

КонецПроцедуры

// Десериализует результат запуска бенчмарков из JSON-файла
//
// Параметры:
//   ПолноеИмяФайла - Строка - Полный путь к JSON-файлу с результатами бенчмарков
//
// Возвращаемое значение:
//   РезультатЗапускаБенчмарков
Функция ПрочитатьИзJson(ПолноеИмяФайла) Экспорт
		
	_Лог.Отладка("Начало десериализации из JSON-файла: %1", ПолноеИмяФайла);

	ЧтениеТекста = Новый ЧтениеТекста(ПолноеИмяФайла, КодировкаТекста.UTF8);
	Json = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	РезультатЗапускаБенчмарков = СериализацияОбъектовБенчмаркинга.ИзJson(Json, Тип("РезультатЗапускаБенчмарков"));

	_Лог.Отладка("Десериализация из JSON-файла завершена: %1", ПолноеИмяФайла);

	Возврат РезультатЗапускаБенчмарков;

КонецФункции

#КонецОбласти