Перем _Объект; // Произвольный - Ссылка на объект бенчмарк-класса
Перем _Бенчмарки; // Массив из Бенчмарк
Перем _Конфигурация; // КонфигурацияБенчмарков

// Обертка над объектом бенчмарк-класса
//
// Параметры:
//   Источник - Тип, Произвольный - Тип или ссылка на объект бенчмарк-класса
Процедура ПриСозданииОбъекта(Источник)

	Если ТипЗнч(Источник) = Тип("Тип") Тогда
		_Объект = Новый(Источник);
	Иначе
		_Объект = Источник;
	КонецЕсли;

КонецПроцедуры

// Набор бенмарков описанные через аннотации в объекте
//
// Возвращаемое значение:
//   Массив из Бенчмарк
Функция Бенчмарки() Экспорт 

	Если _Бенчмарки = Неопределено Тогда
		_Бенчмарки = ИзвлекательБенчамарков.Извлечь(_Объект);
	КонецЕсли;

	Возврат _Бенчмарки;

КонецФункции

// Получает конфигурацию описанную через аннотации в объекте
//
// Возвращаемое значение:
//   КонфигурацияБенчмарков
Функция Конфигурация() Экспорт

	Если _Конфигурация = Неопределено Тогда
		_Конфигурация = ИзвлекательКонфигурацииБенчмарков.Извлечь(_Объект);
	КонецЕсли;

	Возврат _Конфигурация;

КонецФункции

// Устанавливает значения публичным полям объекта бенчмарк-класса
//
// Параметры:
//   Параметры - Массив из ПараметрБенчмарка
Процедура УстановитьПараметры(Параметры) Экспорт

	Для Каждого Параметр Из Параметры Цикл
		
		Если Не Параметр.ЭтоПараметрМетода Тогда
			_Объект[Параметр.Имя] = Параметр.Значение;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры