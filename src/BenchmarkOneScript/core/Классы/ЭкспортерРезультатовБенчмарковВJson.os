
#Использовать logos

Перем _Лог; // Лог

// Экспортирует результаты бенчмарков в файл JSON
Процедура ПриСозданииОбъекта()
	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.ЭкспортерРезультатовБенчмарковВJson");
КонецПроцедуры

// Экспортирует результаты бенчмарков в файл JSON
//
// Параметры:
//   РезультатыБенчмарков - см. Бенчмаркинг.Запустить
Процедура Записать(РезультатыБенчмарков) Экспорт

	КаталогАртефактов = РезультатыБенчмарков.Конфигурация.КаталогАртефактов();
	ПутьКФайлу = ОбъединитьПути(КаталогАртефактов, РезультатыБенчмарков.ИмяКласса + "-report." + Расширение());

	_Лог.Отладка("Начало формирования файла: %1", ПутьКФайлу);

	Данные = ЭкспортРезультатовБенчмарков.ПодготовитьРезультаты(РезультатыБенчмарков);

	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, Символы.Таб);

	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.ОткрытьФайл(ПутьКФайлу, "UTF-8", , ПараметрыЗаписиJSON);
	ЗаписатьJSON(ЗаписьJSON, Данные);
	ЗаписьJSON.Закрыть();

	_Лог.Отладка("Завершение формирования файла: %1", ПутьКФайлу);

КонецПроцедуры

// Расширение файла
//
// Возвращаемое значение:
//   Строка
Функция Расширение() Экспорт
	Возврат "json";
КонецФункции