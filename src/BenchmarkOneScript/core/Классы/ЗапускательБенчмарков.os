#Использовать fs
#Использовать logos
#Использовать allocs

Перем _ОбъектБенчмарков; // Экземпляр класса с бенчмарками
Перем _Конфигурация; // КонфигурацияБенчмарков
Перем _ДескрипторыБенчмарков; // КоллекцияДескрипторовБенчмарков
Перем _МониторПамяти; // МониторПамяти
Перем _Хронометр; // Хронометр
Перем _Репортер; // КонсольныйРепортерБенчмарков
Перем _Лог; // Лог

// Запускает бенчмарки
//
// Параметры:
//   ИсточникБенчмарков - Тип - Класс, содержащий бенчмарки
//                      - КоллекцияДескрипторовБенчмарков
//                      - ДескрипторБенчмарка
//                      - Произвольный - Экземпляр класса с бенчмарками
//   Конфигурация - КонфигурацияБенчмарков - Конфигурация бенчмарков
Процедура ПриСозданииОбъекта(ИсточникБенчмарков, Конфигурация = Неопределено)

	ОберткаИсточника = Новый ОберткаИсточникаБенчмарков(ИсточникБенчмарков);
	
	_ОбъектБенчмарков = ОберткаИсточника.ОбъектБенчмарков();
	_ДескрипторыБенчмарков = ОберткаИсточника.Дескрипторы();
	_Хронометр = Новый Хронометр();
	_Репортер = Новый КонсольныйРепортерБенчмарков();

	Если Не Конфигурация = Неопределено Тогда
		_Конфигурация = Конфигурация;
	Иначе
		_Конфигурация = Новый КонфигурацияБенчмарков(_ОбъектБенчмарков);
	КонецЕсли;

	Если _Конфигурация.ТребуетсяМониторингПамяти() Тогда
		_МониторПамяти = Новый МониторПамяти();
	КонецЕсли;

	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.ЗапускательБенчмарков");

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Запускает бенчмарки
// 
// Возвращаемое значение:
//   РезультатЗапускаБенчмарков
Функция Запустить() Экспорт

	ЭтоВоркер = Бенчмаркинг.ЭтоВоркер();

	Валидатор = Новый ВалидаторЗапускаБенчмарков(_ДескрипторыБенчмарков, _Конфигурация);
	Если Не Валидатор.ЗапускВозможен() Тогда
		ВызватьИсключение Валидатор.ПолучитьТекстОшибок();
	КонецЕсли;

	Если ЭтоВоркер Тогда
		Результат = ЗапуститьБенчмарки();
	Иначе
		ИзолированныйЗапускатель = Новый ИзолированныйЗапускательБенчмарков(_ДескрипторыБенчмарков, _Конфигурация, _Репортер);
		Результат = ИзолированныйЗапускатель.Запустить();
	КонецЕсли;

	Если Не ЭтоВоркер Тогда
		_Репортер.ВывестиРезультатВыполнения(Результат);
	Иначе
		Результат.Конфигурация = Новый КонфигурацияБенчмарков();
		Результат.ДескрипторыБенчмарков = Новый КоллекцияДескрипторовБенчмарков();
	КонецЕсли;

	ЭкспортироватьРезультаты(Результат);

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗапуститьБенчмарки()

	ПрогретьИнструментыЗамера();

	Если Не Бенчмаркинг.ЭтоВоркер() Тогда
		_Репортер.ВывестиЗаголовок();
		_Репортер.ВывестиКонфигурацию(_Конфигурация);
	КонецЕсли;

	// Инициализация результатов
	РезультатЗапуска = Новый РезультатЗапускаБенчмарков();
	РезультатЗапуска.ДескрипторыБенчмарков = _ДескрипторыБенчмарков;
	РезультатЗапуска.Конфигурация = _Конфигурация;

	// Событие ПередВсеми
	КонтекстСобытия = Новый Структура();
	КонтекстСобытия.Вставить("Конфигурация", _Конфигурация);
	КонтекстСобытия.Вставить("ДескрипторыБенчмарков", _ДескрипторыБенчмарков);

	ВызватьОбработчикСобытия(СобытияБенчмарков.ПередВсеми, КонтекстСобытия);

	// Запуск бенчмарков
	ПараметрыКонфигурации = _Конфигурация.Параметры();
	ПараметрыКонфигурацииИзИсточников = ПрочитатьОбщиеПараметрыИзИсточников();

	Для Каждого ДескрипторБенчмарка Из _ДескрипторыБенчмарков.ВМассив() Цикл

		НаборыПараметров = Новый Массив();
		ДополнитьМассив(НаборыПараметров, ПараметрыКонфигурации);
		ДополнитьМассив(НаборыПараметров, ПараметрыКонфигурацииИзИсточников);
		ДополнитьМассив(НаборыПараметров, ДескрипторБенчмарка.НаборыПараметров());
		ДополнитьМассив(НаборыПараметров, ПрочитатьПараметрыБенчмаркаИзИсточников(ДескрипторБенчмарка));

		КомбинацииПараметров = КомбинаторПараметровБенчмарка.Комбинировать(НаборыПараметров);
		
		Если КомбинацииПараметров.Количество() = 0 Тогда
			КомбинацииПараметров.Добавить(Новый Массив());
		КонецЕсли;

		Для Каждого Параметры Из КомбинацииПараметров Цикл
			РезультатЗапускаБенчмарка = ЗапуститьБенчмарк(ДескрипторБенчмарка, Параметры);	
			РезультатЗапуска.Запуски.Добавить(РезультатЗапускаБенчмарка);
		КонецЦикла;

	КонецЦикла;

	// Подготовка результатов
	РезультатЗапуска.ОпределитьЭталоны();
	РезультатЗапуска.ОбновитьСтатистику();
	
	Построитель = Новый ПостроительОтчетаБенчмарков(
		_ДескрипторыБенчмарков, _Конфигурация, РезультатЗапуска.Запуски);

	РезультатЗапуска.Отчет = Построитель.Сформировать();
	
	// Событие ПослеВсех
	КонтекстСобытия = Новый Структура();
	КонтекстСобытия.Вставить("Конфигурация", РезультатЗапуска.Конфигурация);
	КонтекстСобытия.Вставить("ДескрипторыБенчмарков", РезультатЗапуска.ДескрипторыБенчмарков);
	КонтекстСобытия.Вставить("Запуски", РезультатЗапуска.Запуски);
	КонтекстСобытия.Вставить("Отчет", РезультатЗапуска.Отчет);
	КонтекстСобытия.Вставить("СредаОкружения", РезультатЗапуска.СредаОкружения);

	ВызватьОбработчикСобытия(СобытияБенчмарков.ПослеВсех, КонтекстСобытия);

	Возврат РезультатЗапуска;

КонецФункции

Функция ЗапуститьБенчмарк(ДескрипторБенчмарка, Параметры)

	_Репортер.ВывестиЗаголовокБенчмарка(ДескрипторБенчмарка);
	_Репортер.ВывестиПараметры(Параметры);

	ПараметрыМетода = ПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры);
	УстановитьПараметрыОбъекта(Параметры);

	// Инициализация результата
	РезультатЗапускаБенчмарка = Новый РезультатЗапускаБенчмаркаДто();
	РезультатЗапускаБенчмарка.ДескрипторБенчмарка = ДескрипторБенчмарка;
	РезультатЗапускаБенчмарка.Параметры = Параметры;
	РезультатЗапускаБенчмарка.Статистика = Новый СтатистикаБенчмарка();

	Если Бенчмаркинг.ЭтоВоркер() Тогда
		РезультатЗапускаБенчмарка.ИсполняющаяСреда.Версия = Новый СистемнаяИнформация().Версия;
	КонецЕсли;
	
	// Событие ПередКаждым
	КонтекстСобытия = Новый Структура();
	КонтекстСобытия.Вставить("ДескрипторБенчмарка", ДескрипторБенчмарка);
	КонтекстСобытия.Вставить("ПараметрыМетода", ПараметрыМетода);

	ВызватьОбработчикСобытия(СобытияБенчмарков.ПередКаждым, КонтекстСобытия);

	// Запуск итераций
	Делегат = ДескрипторБенчмарка.Делегат(_ОбъектБенчмарков, ПараметрыМетода);

	Если _Конфигурация.Стратегия() = СтратегииЗапускаБенчмарка.ХолодныйЗапуск Тогда
		КоличествоВызововЗаИтерацию = 1;
	Иначе
		КоличествоВызововЗаИтерацию = РассчитатьКоличествоВызововЗаИтерацию(Делегат);
		ЗапуститьЭтапПрогрева(Делегат, КоличествоВызововЗаИтерацию);
	КонецЕсли;

	ЗапуститьЭтапИзмерения(Делегат, КоличествоВызововЗаИтерацию, РезультатЗапускаБенчмарка.Замеры);

	Если _Конфигурация.ТребуетсяМониторингПамяти() Тогда
		ЗапуститьМониторингПамяти(Делегат, КоличествоВызововЗаИтерацию, РезультатЗапускаБенчмарка.Замеры);
	КонецЕсли;

	// Сбор статистики
	РезультатЗапускаБенчмарка.Статистика.Прочитать(РезультатЗапускаБенчмарка.Замеры);
	_Репортер.ВывестиСтатистику(РезультатЗапускаБенчмарка.Статистика);

	// Событие ПослеКаждого
	КонтекстСобытия = Новый Структура();
	КонтекстСобытия.Вставить("ДескрипторБенчмарка", ДескрипторБенчмарка);
	КонтекстСобытия.Вставить("Параметры", Параметры);
	КонтекстСобытия.Вставить("Замеры", РезультатЗапускаБенчмарка.Замеры);
	КонтекстСобытия.Вставить("Статистика", РезультатЗапускаБенчмарка.Статистика);

	ВызватьОбработчикСобытия(СобытияБенчмарков.ПослеКаждого, КонтекстСобытия);

	Возврат РезультатЗапускаБенчмарка;
	
КонецФункции

Функция РассчитатьКоличествоВызововЗаИтерацию(Делегат)

	КоличествоВызововЗаИтерацию = _Конфигурация.КоличествоВызововЗаИтерацию();
	Если Не КоличествоВызововЗаИтерацию = 0 Тогда
		Возврат КоличествоВызововЗаИтерацию;
	КонецЕсли;
	
	МинимальноеКоличествоВызовов = _Конфигурация.МинимальноеКоличествоВызововЗаИтерацию();
	ЦелевоеВремяИтерации = ЕдиницыИзмеренийБенчмарков.Конвертировать(
		_Конфигурация.МинимальноеВремяИтерации(), 
		ЕдиницыИзмеренийБенчмарков.Миллисекунда,
		ЕдиницыИзмеренийБенчмарков.Наносекунда
	);
	
	КоличествоВызовов = МинимальноеКоличествоВызовов;	
	НомерИтерации = 0;
	ВремяПредыдущейОперации = 0;
	ДопустимаяПогрешность = 0.05;
	МаксимальноеКоличествоПроверокНаПогрешность = 5;

	Пока Истина Цикл

		НомерИтерации = НомерИтерации + 1;
		РезультатИтерации = ВыполнитьИтерацию(Делегат, ЭтапыБенчмарка.Оценка, НомерИтерации, КоличествоВызовов);
		
		ВремяИтерации = РезультатИтерации.Наносекунд;
		ВремяОперации = РезультатИтерации.НаносекундЗаОперацию;

		Погрешность = (ВремяПредыдущейОперации - ВремяОперации) / (ВремяОперации + ВремяПредыдущейОперации);
		Погрешность = ?(Погрешность < 0, -Погрешность, Погрешность);

		Если ВремяИтерации < ЦелевоеВремяИтерации Тогда
			КоличествоВызовов = КоличествоВызовов * 2;
			ВремяПредыдущейОперации = ВремяОперации;
			Продолжить;
		КонецЕсли;

		Если Погрешность > ДопустимаяПогрешность И НомерИтерации < МаксимальноеКоличествоПроверокНаПогрешность Тогда
			ВремяПредыдущейОперации = ВремяОперации;
			Продолжить;
		КонецЕсли;

		Прервать;

	КонецЦикла;

	Возврат КоличествоВызовов;

КонецФункции

Процедура ЗапуститьЭтапПрогрева(Делегат, КоличествоВызововЗаИтерацию)

	КоличествоПрогревочныхИтераций = _Конфигурация.КоличествоПрогревочныхИтераций();

	Если КоличествоПрогревочныхИтераций = 0 Тогда
		Возврат;
	КонецЕсли;

	Для НомерИтерации = 1 По КоличествоПрогревочныхИтераций Цикл

		ВыполнитьИтерацию(Делегат, ЭтапыБенчмарка.Прогрев, НомерИтерации, КоличествоВызововЗаИтерацию);	

	КонецЦикла;
	
КонецПроцедуры

Процедура ЗапуститьЭтапИзмерения(Делегат, КоличествоВызововЗаИтерацию, Замеры)

	Для НомерИтерации = 1 По _Конфигурация.КоличествоИтераций() Цикл

		РезультатИтерации = ВыполнитьИтерацию(Делегат, ЭтапыБенчмарка.Измерение, НомерИтерации, КоличествоВызововЗаИтерацию);
		Замеры.Добавить(РезультатИтерации);

	КонецЦикла;

КонецПроцедуры

Процедура ЗапуститьМониторингПамяти(Делегат, КоличествоВызововЗаИтерацию, Замеры)

	Для НомерИтерации = 1 По _Конфигурация.КоличествоИтераций() Цикл

		РезультатИтерации = ВыполнитьИтерацию(Делегат, ЭтапыБенчмарка.Память, НомерИтерации, КоличествоВызововЗаИтерацию);
		Замеры.Добавить(РезультатИтерации);

	КонецЦикла;

КонецПроцедуры

Процедура ПрогретьИнструментыЗамера()

	ТребуетсяМониторингПамяти = _Конфигурация.ТребуетсяМониторингПамяти();
	КоличествоИтераций = 5;

	Для НомерИтерации = 1 По КоличествоИтераций Цикл

		_Хронометр.Старт();
		_Хронометр.Стоп();

		Если ТребуетсяМониторингПамяти Тогда
			_МониторПамяти.Начать();
			_МониторПамяти.Завершить();
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Функция ВыполнитьИтерацию(Делегат, Этап, НомерИтерации, КоличествоВызовов)

	Наносекунд = 0;
	ВыделяемаяПамять = 0;
	
	Если Этап = ЭтапыБенчмарка.Память Тогда	
		ВыделяемаяПамять = ЗамеритьПамять(Делегат, КоличествоВызовов);
	Иначе
		Наносекунд = ЗамеритьВремя(Делегат, КоличествоВызовов);
	КонецЕсли;

	РезультатИтерации = Новый РезультатИтерацииБенчмаркаДто();
	РезультатИтерации.Этап = Этап;
	РезультатИтерации.НомерИтерации = НомерИтерации;
	РезультатИтерации.КоличествоОпераций = КоличествоВызовов;
	РезультатИтерации.Наносекунд = Наносекунд;
	РезультатИтерации.НаносекундЗаОперацию = Наносекунд / КоличествоВызовов;
	РезультатИтерации.ВыделяемаяПамять = ВыделяемаяПамять;
	РезультатИтерации.ВыделяемаяПамятьЗаОперацию = ВыделяемаяПамять / КоличествоВызовов;

	Если НомерИтерации = 1 Тогда
		_Репортер.ВывестиСтроку(" ");
	КонецЕсли;

	_Репортер.ВывестиСтроку(ПредставлениеРезультатаИтерации(РезультатИтерации), "Серый");

	Возврат РезультатИтерации;

КонецФункции

Функция ЗамеритьВремя(Делегат, КоличествоВызовов)

	Рефлектор = Новый Рефлектор();
	Объект = Делегат.Объект();
	ИмяМетода = Делегат.ИмяМетода();
	Параметры = Делегат.Параметры();
	ОсталосьВызовов = КоличествоВызовов;

	_Лог.Отладка("Начало замера времени <%1>", ИмяМетода);
	_Хронометр.Старт(); // Хронометр должен быть "прогрет"

	Пока ОсталосьВызовов > 0 Цикл
		Рефлектор.ВызватьМетод(Объект, ИмяМетода, Параметры);
		ОсталосьВызовов = ОсталосьВызовов - 1;
	КонецЦикла;

	_Хронометр.Стоп();
	_Лог.Отладка("Окончание замера времени <%1>", ИмяМетода);

	Возврат _Хронометр.Наносекунд;

КонецФункции

Функция ЗамеритьПамять(Делегат, КоличествоВызовов)

	Рефлектор = Новый Рефлектор();
	Объект = Делегат.Объект();
	ИмяМетода = Делегат.ИмяМетода();
	Параметры = Делегат.Параметры();
	ОсталосьВызовов = КоличествоВызовов;
	
	_Лог.Отладка("Начало замера памяти <%1>", ИмяМетода);
	_МониторПамяти.Начать(); // Объект должен быть "прогрет"

	Пока ОсталосьВызовов > 0 Цикл
		Рефлектор.ВызватьМетод(Объект, ИмяМетода, Параметры);
		ОсталосьВызовов = ОсталосьВызовов - 1;
	КонецЦикла;

	ВыделеноБайт = _МониторПамяти.Завершить();
	_Лог.Отладка("Завершение замера памяти <%1>", ИмяМетода);

	Возврат ВыделеноБайт;

КонецФункции

Процедура ВызватьОбработчикСобытия(ИмяСобытия, Контекст = Неопределено) Экспорт
	
	Обработчики = _Конфигурация.ОбработчикиСобытия(ИмяСобытия);	
	
	Для Каждого Обработчик Из Обработчики Цикл

		_Лог.Отладка("Начало вызова обработчика <%1> события <%2>", Обработчик.ИмяМетода(), ИмяСобытия);

		Делегат = Обработчик.Делегат(_ОбъектБенчмарков);
		Делегат.Выполнить(Контекст);

		_Лог.Отладка("Завершение вызова обработчика <%1> события <%2>", Обработчик.ИмяМетода(), ИмяСобытия);

	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры)

	ИменаПараметров = ДескрипторБенчмарка.ИменаПараметров();

	Если ИменаПараметров.Количество() = 0 Тогда
		Возврат Новый Массив();
	КонецЕсли;
	
	ПараметрыМетода = Новый Массив(ИменаПараметров.Количество());
	
	Для Каждого Параметр Из Параметры Цикл
		Если Параметр.ЭтоПараметрМетода() Тогда
			ПараметрыМетода[ИменаПараметров.Найти(Параметр.Имя())] = Параметр.Значение();
		КонецЕсли;	
	КонецЦикла;

	Возврат ПараметрыМетода;

КонецФункции

// Устанавливает значения публичным полям объекта
//
// Параметры:
//   Параметры - Массив из ПараметрБенчмарка
Процедура УстановитьПараметрыОбъекта(Параметры)

	ШаблонОшибки = "Не удалось задать значение поля <%1> для объекта <%2>";

	Для Каждого Параметр Из Параметры Цикл		
		Если Не Параметр.ЭтоПараметрМетода() Тогда
			Попытка
				_ОбъектБенчмарков[Параметр.Имя()] = Параметр.Значение();
			Исключение
				ВызватьИсключение СтрШаблон(ШаблонОшибки, Параметр.Имя(), ТипЗнч(_ОбъектБенчмарков));
			КонецПопытки;
		КонецЕсли;		
	КонецЦикла;

КонецПроцедуры

Функция ПрочитатьОбщиеПараметрыИзИсточников()

	Результат = Новый Массив();

	Для Каждого ИсточникПараметров Из _Конфигурация.ИсточникиПараметров() Цикл
		Читатель = Новый ЧитательПараметровБенчмарков(ИсточникПараметров.Источник, _ОбъектБенчмарков);
		ДополнитьМассив(Результат, Читатель.ПрочитатьКакОбщиеПараметры(ИсточникПараметров.ИмяПоля));
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

Функция ПрочитатьПараметрыБенчмаркаИзИсточников(ДескрипторБенчмарка)

	Результат = Новый Массив();

	Для Каждого ИсточникПараметров Из ДескрипторБенчмарка.ИсточникиПараметров() Цикл
		Читатель = Новый ЧитательПараметровБенчмарков(ИсточникПараметров, _ОбъектБенчмарков);
		ДополнитьМассив(Результат, Читатель.ПрочитатьКакПараметрыБенчмарка(ДескрипторБенчмарка));
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

Функция ПредставлениеРезультатаИтерации(РезультатИтерации)

	ДлинаЭтапа = Макс(
		СтрДлина(ЭтапыБенчмарка.Оценка),
		СтрДлина(ЭтапыБенчмарка.Прогрев),
		СтрДлина(ЭтапыБенчмарка.Измерение),
		СтрДлина(ЭтапыБенчмарка.Память)
	);
	
	ДлинаЭтапа = ДлинаЭтапа + 1;

	Представление = СтрШаблон("%1 %2: %3 op, ",
		Лев(РезультатИтерации.Этап + "              ", ДлинаЭтапа),
		Лев("" + РезультатИтерации.НомерИтерации + " ", 2),
		Формат(РезультатИтерации.КоличествоОпераций, "ЧГ=")
	);

	Если РезультатИтерации.Этап = ЭтапыБенчмарка.Память Тогда

		Возврат Представление + СтрШаблон(
			"%1, %2/op",
			ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
				РезультатИтерации.ВыделяемаяПамять, 
				ЕдиницыИзмеренийБенчмарков.Байт
			),		
			ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
				РезультатИтерации.ВыделяемаяПамятьЗаОперацию, 
				ЕдиницыИзмеренийБенчмарков.Байт
			)
		);

	Иначе

		Возврат Представление + СтрШаблон(
			"%1, %2/op",
			ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
				РезультатИтерации.Наносекунд,
				ЕдиницыИзмеренийБенчмарков.Наносекунда
			),		
			ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
				РезультатИтерации.НаносекундЗаОперацию,
				ЕдиницыИзмеренийБенчмарков.Наносекунда
			)
		);

	КонецЕсли;

КонецФункции

Процедура ЭкспортироватьРезультаты(Результаты)
	
	КаталогАртефактов = _Конфигурация.КаталогАртефактов();
	ИмяФайла = СтрШаблон("%1-report", Строка(ТипЗнч(_ОбъектБенчмарков)));
	МаскаФайлов = СтрШаблон("%1.*", ИмяФайла);
	Экспортеры = _Конфигурация.Экспортеры();

	Если Не ЗначениеЗаполнено(КаталогАртефактов) Или Экспортеры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ФС.ОбеспечитьКаталог(КаталогАртефактов);
	ФС.УдалитьФайлы(КаталогАртефактов, МаскаФайлов);

	Для Каждого Экспортер Из _Конфигурация.Экспортеры() Цикл

		ИмяФайлаСРасширением = СтрШаблон("%1.%2", ИмяФайла, Экспортер.Расширение());
		ПолноеИмяФайла = ОбъединитьПути(КаталогАртефактов, ИмяФайлаСРасширением);

		Экспортер.Записать(Результаты, ПолноеИмяФайла);

	КонецЦикла;

	Если Не Бенчмаркинг.ЭтоВоркер() Тогда

		Артефакты = НайтиФайлы(КаталогАртефактов, МаскаФайлов);
		Если Артефакты.Количество() Тогда
			_Репортер.ВывестиСтроку(" ");
			_Репортер.ВывестиСтроку("// Артефакты", "Малиновый");
		КонецЕсли;

		Для Каждого Артефакт Из Артефакты Цикл
			_Репортер.ВывестиСтроку(Артефакт.ПолноеИмя, "ТемноЖелтый");
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

Процедура ДополнитьМассив(Приемник, Источник)

	Для Каждого Значение Из Источник Цикл
		Приемник.Добавить(Значение);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти