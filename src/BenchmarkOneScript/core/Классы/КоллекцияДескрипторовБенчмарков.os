#Использовать fluent

&Несериализуемое
Перем _ДескрипторыБенчмарков; // Массив из ДескрипторБенчмарка

// Коллекция дескрипторов бенчмарков
//
// Параметры:
//   ИсточникБенчмарков - Произвольный - Тип или экземпляр класса бенчмарков, 
//                                       откуда будут прочитаны дескрипторы
Процедура ПриСозданииОбъекта(ИсточникБенчмарков = Неопределено) Экспорт

	_ДескрипторыБенчмарков = Новый Массив();

	Если Не ИсточникБенчмарков = Неопределено Тогда
		Прочитать(ИсточникБенчмарков);
	КонецЕсли;

КонецПроцедуры

// Добавляет дескриптор бенчмарка
//
// Параметры:
//   ДескрипторБенчмарков - ДескрипторБенчмарков
Процедура Добавить(ДескрипторБенчмарков) Экспорт
	
	Если _ДескрипторыБенчмарков.Найти(ДескрипторБенчмарков) = Неопределено Тогда
		_ДескрипторыБенчмарков.Добавить(ДескрипторБенчмарков);
	КонецЕсли;

КонецПроцедуры

// Дескриптор бенчмарка по индексу
//
// Параметры:
//   Индекс - Число - Индекс начиная от 0
//
// Возвращаемое значение:
//   ДескрипторБенчмарка
Функция Получить(Индекс) Экспорт
	Возврат _ДескрипторыБенчмарков[Индекс];
КонецФункции

// Количество дескрипторов бенчмарков
//
// Возвращаемое значение:
//   Число
Функция Количество() Экспорт
	Возврат _ДескрипторыБенчмарков.Количество();
КонецФункции

// Очищает коллекцию
Процедура Очистить() Экспорт
	_ДескрипторыБенчмарков.Очистить();
КонецПроцедуры

// Дескриптор бенчмарка по имени метода
//
// Параметры:
//   Имя - Строка - Имя метода бенчмарка
//
// Возвращаемое значение:
//   ДескрипторБенчмарка, Неопределено
Функция НайтиПоИмени(Имя) Экспорт

	Возврат ПроцессорыКоллекций
		.ИзКоллекции(_ДескрипторыБенчмарков)
		.Фильтровать("Элемент -> Элемент.Метод() = """ + Имя + """")
		.ПолучитьПервый();

КонецФункции

// Дескриптор бенчмарка по имени метода
//
// Параметры:
//   Тип - Тип - Класс бенчмарков
//   Имя - Строка - Имя метода бенчмарка
//
// Возвращаемое значение:
//   ДескрипторБенчмарка, Неопределено
Функция НайтиПоТипуИИмени(Тип, Имя) Экспорт

	Контекст = Новый Структура("Тип, Имя", Тип, Имя);

	Возврат ПроцессорыКоллекций
		.ИзКоллекции(_ДескрипторыБенчмарков)
		.Фильтровать("Элемент -> Элемент.Метод() = Имя И Элемент.ТипОбъекта() = Тип", Контекст)
		.ПолучитьПервый();

КонецФункции

// Первый в наборе дескриптор бенчмарка
//
// Возвращаемое значение:
//   ДескрипторБенчмарка
Функция ПолучитьПервый() Экспорт

	Если _ДескрипторыБенчмарков.Количество() > 0 Тогда
		Возврат _ДескрипторыБенчмарков[0];
	КонецЕсли;

КонецФункции

// Создает новую коллекцию из эталонных бенчмарков
//
// Возвращаемое значение:
//   КоллекцияДескрипторовБенчмарков
Функция ПолучитьЭталонные() Экспорт

	Коллекция = Скопировать();
	Коллекция.Фильтровать("Бенчмарк -> Бенчмарк.ЭтоЭталон() = Истина");

	Возврат Коллекция;

КонецФункции

// Создает копию коллекции дескрипторов бенчмарков
//
// Возвращаемое значение:
//   КоллекцияДескрипторовБенчмарков
Функция Скопировать() Экспорт

	Коллекция = Новый КоллекцияДескрипторовБенчмарков();

	Для Каждого Бенчмарк Из _ДескрипторыБенчмарков Цикл
		Коллекция.Добавить(Бенчмарк);
	КонецЦикла;

	Возврат Коллекция;

КонецФункции

// Фильтровать коллекцию по условию
//
// Параметры:
//   ФункцияФильтрации - Строка - Лямбда выражение функция с одним параметром в который будет передан элемент,
//                                и которая возвращает Булево, Истина если элемент проходит фильтр, 
//                                Ложь в противном случае
//   ДополнительныеПараметры - Структура - Контекст добавляемый в лямбда выражение
Процедура Фильтровать(ФункцияФильтрации, ДополнительныеПараметры = Неопределено) Экспорт

	_ДескрипторыБенчмарков = ПроцессорыКоллекций
		.ИзКоллекции(_ДескрипторыБенчмарков)
		.Фильтровать(ФункцияФильтрации, ДополнительныеПараметры)
		.ВМассив();

КонецПроцедуры

// Читает дескрипторы бенчмарков из класса
//
// Параметры:
//   ИсточникБенчмарков - Произвольный - Тип или экземпляр класса бенчмарков
Процедура Прочитать(ИсточникБенчмарков) Экспорт
	Очистить();
	ИзвлекательДескрипторовБенчмарков.Извлечь(ИсточникБенчмарков, ЭтотОбъект);
КонецПроцедуры

// Массив дескрипторов бенчмарков
//
// Возвращаемое значение:
//   Массив из ДескрипторБенчмарка
Функция ВМассив() Экспорт
	Возврат Новый Массив(Новый ФиксированныйМассив(_ДескрипторыБенчмарков));
КонецФункции

#Область Сериализация

&ПередСериализацией
Процедура ПередСериализацией(Сериализатор, Результат, СтандартнаяОбработка) Экспорт

	СтандартнаяОбработка = Ложь;

	Результат = Сериализатор.ПреобразоватьОбъектСериализации(_ДескрипторыБенчмарков);

КонецПроцедуры

&ПослеДесериализации
Процедура ПослеДесериализации(Десериализатор, ПрочитанныеСвойства) Экспорт
	
	Если ТипЗнч(ПрочитанныеСвойства) = Тип("Массив") Тогда
		_ДескрипторыБенчмарков = Десериализатор.ПреобразоватьОбъектДесериализации(
				ПрочитанныеСвойства,
				Тип("Массив"),
				Тип("ДескрипторБенчмарка"));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти