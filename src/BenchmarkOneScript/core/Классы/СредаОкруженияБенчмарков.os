// BSLLS:UnusedLocalVariable-off
// BSLLS:ExportVariables-off

#Использовать cpuinfo
#Использовать packageinfo

&Сериализуемое("BenchmarkOneScriptVersion")
&Тип("Строка")
Перем ВерсияБиблиотеки Экспорт; // Строка

&Сериализуемое("OsVersion")
&Тип("Строка")
Перем ВерсияОС Экспорт; // Строка

&Сериализуемое("RuntimeVersion")
&Тип("Строка")
Перем ВерсияИсполняющейСреды Экспорт; // Строка

&Несериализуемое
Перем ИнформацияОПроцессоре Экспорт; // см. ИнформацияОПроцессоре

&Сериализуемое("ProcessorName")
&Тип("Строка")
Перем ИмяПроцессора; // Строка

&Сериализуемое("PhysicalProcessorCount")
&Тип("Число")
Перем КоличествоПроцессоров; // Число

&Сериализуемое("PhysicalCoreCount")
&Тип("Число")
Перем КоличествоЯдер; // Число

&Сериализуемое("LogicalCoreCount")
&Тип("Число")
Перем КоличествоЛогическихПроцессоров; // Число

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта()
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	
	ВерсияБиблиотеки = ОпределитьВерсию();
	ВерсияОС = СистемнаяИнформация.ВерсияОС;
	ВерсияИсполняющейСреды = СистемнаяИнформация.Версия;
	ИнформацияОПроцессоре = Новый ИнформацияОПроцессоре();

КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Значение, СтандартнаяОбработка)

    СтандартнаяОбработка = Ложь;

 	Процессор = ИнформацияОПроцессоре.ПолноеОписание();

	Если ПустаяСтрока(ВерсияИсполняющейСреды) Тогда
		Значение = СтрШаблон("BenchmarkOneScript v%1, %2
			|%3", ВерсияБиблиотеки, ВерсияОС, Процессор
		);
	Иначе
		Значение = СтрШаблон("BenchmarkOneScript v%1, OneScript v%2, %3
			|%4", ВерсияБиблиотеки, ВерсияИсполняющейСреды, ВерсияОС, Процессор
		);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Сериализация

&ПередСериализацией
Процедура ПередСериализацией() Экспорт
	
	ИмяПроцессора = ИнформацияОПроцессоре.ИмяПроцессора;
	КоличествоПроцессоров = ИнформацияОПроцессоре.КоличествоПроцессоров;
	КоличествоЯдер = ИнформацияОПроцессоре.КоличествоЯдер;
	КоличествоЛогическихПроцессоров = ИнформацияОПроцессоре.КоличествоЛогическихПроцессоров;

КонецПроцедуры

&ПослеДесериализации
Процедура ПослеДесериализации() Экспорт

	ИнформацияОПроцессоре = Новый ИнформацияОПроцессоре(Ложь);
	ИнформацияОПроцессоре.ИмяПроцессора = ИмяПроцессора;
	ИнформацияОПроцессоре.КоличествоПроцессоров = КоличествоПроцессоров;
	ИнформацияОПроцессоре.КоличествоЯдер = КоличествоЯдер;
	ИнформацияОПроцессоре.КоличествоЛогическихПроцессоров = КоличествоЛогическихПроцессоров;

КонецПроцедуры

#КонецОбласти

Функция ОпределитьВерсию()

	ПутьКФайлу = ОбъединитьПути(ТекущийСценарий().Каталог, "../../../../packagedef");	
	ИнформацияОПакете = Новый ИнформацияОПакете(ПутьКФайлу);
	
	Возврат ИнформацияОПакете.Версия();
	
КонецФункции

#КонецОбласти