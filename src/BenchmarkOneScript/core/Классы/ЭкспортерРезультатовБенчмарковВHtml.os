
#Использовать logos

Перем _Лог; // Лог

Процедура ПриСозданииОбъекта()
	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.ЭкспортерРезультатовБенчмарковВHtml");
КонецПроцедуры

Процедура Записать(РезультатыБенчмарков) Экспорт

	КаталогАртефактов = РезультатыБенчмарков.Конфигурация.КаталогАртефактов();
	ПутьКФайлу = ОбъединитьПути(КаталогАртефактов, РезультатыБенчмарков.ИмяКласса + "-report.html");

	_Лог.Отладка("Начало формирования файла: %1", ПутьКФайлу);
	
	Части = Новый Массив();
	Части.Добавить("<!DOCTYPE html>");
	Части.Добавить("<html lang='ru'>");
	Части.Добавить("<head>");
	Части.Добавить("<meta charset='utf-8' />");
	Части.Добавить(Css());
	Части.Добавить("</head>");

	Части.Добавить("<body>");
	Части.Добавить(СтрШаблон("<pre><code>%1</code></pre>", РезультатыБенчмарков.СредаОкружения.ВСтроку()));

	Части.Добавить("<table>");
	Части.Добавить("<thead>");
	Части.Добавить("<tr>");

	Для Каждого Колонка Из РезультатыБенчмарков.Отчет.Колонки Цикл
		Части.Добавить("<th>");
		Части.Добавить(Колонка.Заголовок);
		Части.Добавить("</th>");
	КонецЦикла;

	Части.Добавить("</tr>");
	Части.Добавить("</thead>");

	Части.Добавить("<tbody>");

	Для Каждого СтрокаОтчета Из РезультатыБенчмарков.Отчет.Таблица Цикл

		Части.Добавить("<tr>");

		Для Каждого Колонка Из РезультатыБенчмарков.Отчет.Колонки Цикл
			Части.Добавить("<td>");
			Части.Добавить(Колонка.ПредставлениеЗначения(СтрокаОтчета[Колонка.Имя], СтрокаОтчета));
			Части.Добавить("</td>");
		КонецЦикла;

		Части.Добавить("</tr>");

	КонецЦикла;

	Части.Добавить("</tbody>");
	Части.Добавить("</table>");

	Части.Добавить("</body>");
	Части.Добавить("</html>");

	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлу, КодировкаТекста.UTF8);
	ЗаписьТекста.Записать(СтрСоединить(Части, Символы.ПС));
	ЗаписьТекста.Закрыть();

	_Лог.Отладка("Завершение формирования файла: %1", ПутьКФайлу);

КонецПроцедуры

Функция Css()
	Возврат "
	|<style type=""text/css"">
	|	table { border-collapse: collapse; display: block; width: 100%; overflow: auto; }
	|	td, th { padding: 6px 13px; border: 1px solid #ddd; text-align: right; }
	|	tr { background-color: #fff; border-top: 1px solid #ccc; }
	|	tr:nth-child(even) { background: #f8f8f8; }
	|</style>";
КонецФункции