#Использовать fs
#Использовать tempfiles
#Использовать logos

Перем _Конфигурация; // КонфигурацияБенчмарков
Перем _ДескрипторыБенчмарков; // КоллекцияДескрипторовБенчмарков
Перем _Репортер; // КонсольныйРепортерБенчмарков
Перем _ЛокаторИсполняющейСреды; // ЛокаторИсполняющейСреды
Перем _МенеджерВременныхФайлов; // МенеджерВременныхФайлов
Перем _Лог; // Лог
Перем _ЖурналСообщенийПроцесса; // Массив из Строка

#Область ОбработчикиСобытий

// Запускает бенчмарки
//
// Параметры:
//   ДескрипторыБенчмарков - КоллекцияДескрипторовБенчмарков
//   Конфигурация - КонфигурацияБенчмарков
//   Репортер - КонсольныйРепортерБенчмарков
Процедура ПриСозданииОбъекта(ДескрипторыБенчмарков, Конфигурация, Репортер)

	_ДескрипторыБенчмарков = ДескрипторыБенчмарков;
	_Конфигурация = Конфигурация;
	_Репортер = Репортер;
	_ЛокаторИсполняющейСреды = Новый ЛокаторИсполняющейСреды();
	_МенеджерВременныхФайлов = Новый МенеджерВременныхФайлов();
	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.ИзолированныйЗапускательБенчмарков");
	_ЖурналСообщенийПроцесса = Новый Массив();

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Запускает изолированно бенчмарки
// 
// Возвращаемое значение:
//   РезультатЗапускаБенчмарков
Функция Запустить() Экспорт
	
	_ЖурналСообщенийПроцесса.Очистить();
	
	ПередаваемаяКонфигурация = _Конфигурация
		.Скопировать()
		.УстановитьКаталогАртефактов(_МенеджерВременныхФайлов.НовоеИмяФайла())
		.УдалитьЭкспортеры()
		.ДобавитьЭкспортер(ЭкспортерыРезультатовБенчмарков.Json);

	РезультатЗапуска = Новый РезультатЗапускаБенчмарков();
	РезультатЗапуска.ДескрипторыБенчмарков = _ДескрипторыБенчмарков;
	РезультатЗапуска.Конфигурация = _Конфигурация;

	ВерсииИсполняющейСреды = _Конфигурация.ВерсииИсполняющейСреды();
	ВыполнятьВТекущейВерсии = ВерсииИсполняющейСреды.Количество() = 0;

	Если ВыполнятьВТекущейВерсии Тогда
		ВерсииИсполняющейСреды.Добавить();
	КонецЕсли;

	Для Каждого ИсполняющаяСреда Из ВерсииИсполняющейСреды Цикл

		Попытка
			ТекущиеРезультатыЗапуска = ЗапуститьВИсполняющейСреде(ПередаваемаяКонфигурация, ИсполняющаяСреда);			
		Исключение
			УдалитьАртефакты();
			ВызватьИсключение;
		КонецПопытки;

		Если ТекущиеРезультатыЗапуска = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РезультатЗапуска.СредаОкружения = ТекущиеРезультатыЗапуска.СредаОкружения;

		Для Каждого РезультатЗапускаБенчмарка Из ТекущиеРезультатыЗапуска.Запуски Цикл
			
			Если ВыполнятьВТекущейВерсии Тогда
				РезультатЗапускаБенчмарка.ИсполняющаяСреда.Версия = "";
			Иначе	
				РезультатЗапускаБенчмарка.ИсполняющаяСреда.Алиас = ИсполняющаяСреда.Версия;	
				РезультатЗапускаБенчмарка.ИсполняющаяСреда.Наименование = ИсполняющаяСреда.Наименование;
			КонецЕсли;

			РезультатЗапуска.Запуски.Добавить(РезультатЗапускаБенчмарка);

		КонецЦикла;

	КонецЦикла;

	Если ВерсииИсполняющейСреды.Количество() > 1 Тогда
		РезультатЗапуска.СредаОкружения.ВерсияИсполняющейСреды = "";
	КонецЕсли;

	РезультатЗапуска.ОпределитьЭталоны();

	Построитель = Новый ПостроительОтчетаБенчмарков(_ДескрипторыБенчмарков, _Конфигурация, РезультатЗапуска.Запуски);

	РезультатЗапуска.Отчет = Построитель.Сформировать();

	УдалитьАртефакты();

	Возврат РезультатЗапуска;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗапуститьВИсполняющейСреде(Конфигурация, ИсполняющаяСреда)
			
	КаталогАртефактов = Конфигурация.КаталогАртефактов();
	ФС.ОбеспечитьПустойКаталог(КаталогАртефактов);

	Если ЗначениеЗаполнено(ИсполняющаяСреда.Версия) Тогда
		ФайлИлиВерсия = ИсполняющаяСреда.Версия;
		Версия = ИсполняющаяСреда.Версия;
	Иначе
		ФайлИлиВерсия = _ЛокаторИсполняющейСреды.НайтиИсполняемыйФайлВКаталоге(КаталогПрограммы());
		Версия = Новый СистемнаяИнформация().Версия;
	КонецЕсли;

	_Лог.Отладка("Запуск процесса в версии <%1> исполняющей среды", Версия);

	_Репортер.ВывестиЗаголовок(Версия);
	_Репортер.ВывестиКонфигурацию(_Конфигурация);

	Процесс = ПодготовитьПроцесс(Конфигурация, ФайлИлиВерсия);
	Процесс.Запустить();

	ОжидатьЗавершениеПроцесса(Процесс);
	ОбработатьВыполнениеПроцесса(Процесс, Версия);

	РезультатЗапуска = ПрочитатьРезультатЗапускаБенчмарковИзJSON(КаталогАртефактов);

	_Лог.Отладка("Завершение процесса в версии <%1> исполняющей среды", Версия);

	Возврат РезультатЗапуска;

КонецФункции

Функция ПодготовитьПроцесс(Конфигурация, Версия)
	
	РабочийКаталог         = _МенеджерВременныхФайлов.СоздатьКаталог();	
	ПутьИсполняемогоФайла  = ОпределитьИсполняемыйФайл(Версия);
	ПутьСтартовогоСценария = ПодготовитьСтартовыйСценарий(Конфигурация, РабочийКаталог);

	ПодготовитьКонфигурационныйФайл(РабочийКаталог);
	
	СтрокаКоманды = Новый Массив();
	СтрокаКоманды.Добавить(ОбернутьВКавычкиДляWindows(ПутьИсполняемогоФайла));
	СтрокаКоманды.Добавить(ОбернутьВКавычки(ПутьСтартовогоСценария));
	СтрокаКоманды = СтрСоединить(СтрокаКоманды, " ");

	_Лог.Отладка("Строка запуска <%1>", СтрокаКоманды);
			
	Попытка
		Процесс = СоздатьПроцесс(СтрокаКоманды, РабочийКаталог, Истина);
	Исключение
		ТекстОшибки = ИнформацияОбОшибке().ПодробноеОписаниеОшибки();
		ВызватьИсключение СтрШаблон("Не удалось запустить процесс бенчмаркинга для версии <%1>.
			|Ошибка: %2", 
			Версия,
			ТекстОшибки);
	КонецПопытки;

	Возврат Процесс;

КонецФункции

Процедура ОжидатьЗавершениеПроцесса(Процесс)
	
	ТаймаутСекунд = 600;

	ВремяНачала = ТекущаяУниверсальнаяДата();
	Пока Не ПроверитьЗавершениеПроцесса(Процесс) Цикл
		ПрошлоСекунд = (ТекущаяУниверсальнаяДата() - ВремяНачала);
		Если ПрошлоСекунд >= ТаймаутСекунд Тогда
			Процесс.Завершить();
			ВызватьИсключение СтрШаблон("Превышено время ожидания (%1 сек.) завершения дочернего процесса", ТаймаутСекунд);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция ПроверитьЗавершениеПроцесса(Процесс)

	ПериодОпросаВМиллисекундах = 100;
		
	Приостановить(ПериодОпросаВМиллисекундах);

	Если Процесс.ПотокВывода.ЕстьДанные Тогда
		Текст = Процесс.ПотокВывода.Прочитать();
		_Репортер.ВывестиСтроку(Текст);
		_ЖурналСообщенийПроцесса.Добавить(Текст);
	КонецЕсли;

	Если Процесс.ПотокОшибок.ЕстьДанные Тогда
		Текст = Процесс.ПотокОшибок.Прочитать();
		_Репортер.ВывестиСтроку(Текст);
		_ЖурналСообщенийПроцесса.Добавить(Текст);
	КонецЕсли;

	Возврат Процесс.Завершен;

КонецФункции

Процедура ОбработатьВыполнениеПроцесса(Процесс, Версия)
	
	Если Процесс.КодВозврата = 0 Тогда
		Возврат;
	КонецЕсли;

	ТекстОшибки = НайтиПоследнююОшибку();
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ТекстОшибки = ": " + ТекстОшибки;
	КонецЕсли;

	ВызватьИсключение СтрШаблон("Завершение процесса с ошибкой в версии <%1> исполняющей среды%2", Версия, ТекстОшибки);

КонецПроцедуры

#Область КонфигурационныйФайл

Процедура ПодготовитьКонфигурационныйФайл(КаталогСохранения)

	ПутьФайла = ОбъединитьПути(КаталогСохранения, "oscript.cfg");
	
	ПрочитанныеНастройки = НайтиИПрочитатьКонфигурационныеФайлы();
	ДополнитьСистемныеНастройкиКаталогамиБиблиотек(ПрочитанныеНастройки);

	МассивСтрок = Новый Массив();
	Для Каждого Строка Из ПрочитанныеНастройки Цикл
		МассивСтрок.Добавить(СтрШаблон("%1=%2", Строка.Ключ, Строка.Значение));
	КонецЦикла;

	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ТекстКонфигурации = СтрСоединить(МассивСтрок, Символы.ПС);

	ЗаписьТекста = Новый ЗаписьТекста(ПутьФайла);
	ЗаписьТекста.Записать(ТекстКонфигурации);
	ЗаписьТекста.Закрыть();

	_Лог.Отладка(
		"Текст конфигурационного файла: 
		|%1", 
		ТекстКонфигурации
	);

КонецПроцедуры

Процедура ДополнитьСистемныеНастройкиКаталогамиБиблиотек(Настройки)

	ИмяДополняемойНастройки = "lib.additional";
	ИменаИсточников = СтрРазделить("lib.additional,lib.system", ",");

	Для Каждого ИмяНастройки Из ИменаИсточников Цикл

		КаталогиБиблиотек = ПолучитьЗначениеСистемнойНастройки(ИмяНастройки);
		Если Не ЗначениеЗаполнено(КаталогиБиблиотек) Тогда
			Продолжить;
		КонецЕсли;

		// Предполагается, что настройка задана через переменную среды, 
		// поэтому пути разрешаются относительно текущего каталога.
		КаталогиБиблиотек = РазрешитьПутиКаталоговБиблиотекИзНастройки(КаталогиБиблиотек, ТекущийКаталог());

		ИсходноеЗначение = Настройки[ИмяДополняемойНастройки];
		Если ЗначениеЗаполнено(ИсходноеЗначение) Тогда
			НовоеЗначение = ИсходноеЗначение + ";" + КаталогиБиблиотек;
		Иначе
			НовоеЗначение = КаталогиБиблиотек;
		КонецЕсли;

		Настройки[ИмяДополняемойНастройки] = НовоеЗначение;

	КонецЦикла;

КонецПроцедуры

Функция НайтиИПрочитатьКонфигурационныеФайлы()

	ИмяФайла = "oscript.cfg";

	КаталогиПоиска = Новый Массив();
	КаталогиПоиска.Добавить(Новый Файл(ПутьКФайлуБенчмарка()).Путь);
	КаталогиПоиска.Добавить(СтартовыйСценарий().Каталог);
	КаталогиПоиска.Добавить(ТекущийКаталог());

	ПрочитанныеНастройки = Новый Соответствие();

	Для Каждого Каталог Из КаталогиПоиска Цикл

		ПутьКФайлу = ОбъединитьПути(Каталог, ИмяФайла);
		Если Не ФС.ФайлСуществует(ПутьКФайлу) Тогда
			Продолжить;
		КонецЕсли;

		ПрочитатьКонфигурационныйФайл(ПутьКФайлу, ПрочитанныеНастройки);

	КонецЦикла;

	Возврат ПрочитанныеНастройки;
	
КонецФункции

Процедура ПрочитатьКонфигурационныйФайл(ПутьКФайлу, ПрочитанныеНастройки)
	
	КаталогРазрешенияПутей = Новый Файл(ПутьКФайлу).Путь;

	ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлу);
	СодержимоеФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	РегулярноеВыражение = Новый РегулярноеВыражение("^(?!\s*#)\s*([^=\r\n]+?)\s*=\s*(.*)$");
	Совпадения = РегулярноеВыражение.НайтиСовпадения(СодержимоеФайла);

	Для Каждого Совпадение Из Совпадения Цикл

		Ключ = НРег(Совпадение.Группы[1].Значение);
		Значение = Совпадение.Группы[2].Значение;

		Если Ключ = "lib.system" Или Ключ = "lib.additional" Тогда
			Значение = РазрешитьПутиКаталоговБиблиотекИзНастройки(Значение, КаталогРазрешенияПутей);
		КонецЕсли;

		ПрочитанныеНастройки.Вставить(Ключ, Значение);

	КонецЦикла;

КонецПроцедуры

Функция РазрешитьПутиКаталоговБиблиотекИзНастройки(Значение, КаталогРазрешенияПутей)
	Пути = СтрРазделить(Значение, ";");
	Возврат СтрСоединить(РазрешитьПутиФС(Пути, КаталогРазрешенияПутей), ";");
КонецФункции

#КонецОбласти

Функция ЗаписатьНастройкиКонфигурацииВФайл(Конфигурация)

	ПутьКФайлу = _МенеджерВременныхФайлов.СоздатьФайл("json");

	Сериализатор = Новый СериализаторНастроекБенчмарков();
	Сериализатор.ЗаписатьВJson(_ДескрипторыБенчмарков, Конфигурация, ПутьКФайлу);

	Возврат ПутьКФайлу;
	
КонецФункции

Функция ПодготовитьСтартовыйСценарий(Конфигурация, РабочийКаталог)

	ПутьКСценарию         = ОбъединитьПути(РабочийКаталог, "main.os");
	ПутьКБиблиотеке       = ФС.НормализоватьПуть(ОбъединитьПути(ТекущийСценарий().Каталог, "../../../.."));
	ПутьКФайлуБенчмарка   = ПутьКФайлуБенчмарка();
	ПутьФайлаКонфигурации = ЗаписатьНастройкиКонфигурацииВФайл(Конфигурация);
	ИмяТипаБенчмарка      = Строка(_ДескрипторыБенчмарков.ПолучитьПервый().ТипОбъекта());

	ТекстСценария = "
	|#Использовать """ + ПутьКБиблиотеке + """
	|
	|УстановитьПеременнуюСреды(""BENCHMARK_WORKER"", 1);
	|
	|Тип = Бенчмаркинг.ПодключитьБенчмарк(""" + ПутьКФайлуБенчмарка + """, """ + ИмяТипаБенчмарка + """);
	|
	|Сериализатор = Новый СериализаторНастроекБенчмарков();
	|Настройки = Сериализатор.ПрочитатьИзJson(""" + ПутьФайлаКонфигурации + """);
	|
	|Бенчмаркинг.Запустить(Настройки.ДескрипторыБенчмарков, Настройки.Конфигурация);";

	ЗаписьТекста = Новый ЗаписьТекста(ПутьКСценарию);
	ЗаписьТекста.Записать(ТекстСценария);
	ЗаписьТекста.Закрыть();

	_Лог.Отладка(
		"Текст стартового сценария: 
		|%1", 
		ТекстСценария
	);

	Возврат ПутьКСценарию;

КонецФункции

Функция ПутьКФайлуБенчмарка()
	ТипОбъектаБенчмарка = _ДескрипторыБенчмарков.ПолучитьПервый().ТипОбъекта();
	Возврат Новый РасширениеТипа(ТипОбъектаБенчмарка).Источник;
КонецФункции

Функция ОпределитьИсполняемыйФайл(Версия)

	Если ФС.ФайлСуществует(Версия) Тогда
		Возврат Версия;
	КонецЕсли;

	КаталогУстановки = _ЛокаторИсполняющейСреды.ОпределитьКаталогУстановки();
	Если КаталогУстановки = Неопределено Тогда
		ВызватьИсключение 
			"Не найден каталог с установленными версиями OneScript.
			|Убедитесь, что OVM (OneScript Version Manager) установлен и переменная OVM_INSTALL_PATH настроена корректно.";
	КонецЕсли;

	_Лог.Отладка(СтрШаблон("Каталог с установленными версиями OneScript: %1", КаталогУстановки));
	
	ПутьФайла = _ЛокаторИсполняющейСреды.НайтиИсполняемыйФайл(Версия);
	Если ПутьФайла = Неопределено Тогда
		ВызватьИсключение СтрШаблон(
			"Не найдена установленная версия OneScript (%1). Установите её командой: ovm install %1", Версия);
	КонецЕсли;

	_Лог.Отладка(СтрШаблон("Найден исполняемый файл OneScript: %1", ПутьФайла));
				
	Возврат ПутьФайла;

КонецФункции

Функция ПрочитатьРезультатЗапускаБенчмарковИзJSON(Каталог)

	ИмяТипа = Строка(_ДескрипторыБенчмарков.ПолучитьПервый().ТипОбъекта());
	ПутьКОтчету = ОбъединитьПути(Каталог, СтрШаблон("%1-report.json", ИмяТипа));

	Если Не ФС.ФайлСуществует(ПутьКОтчету) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Сериализатор = Новый СериализаторРезультатовБенчмарков();
	РезультатЗапуска = Сериализатор.ПрочитатьИзJson(ПутьКОтчету);

	Возврат РезультатЗапуска;

КонецФункции

Функция НайтиПоследнююОшибку()
	
	КоличествоПроверяемыхСообщений = 10;
	КускиТекста = Новый Массив();
	КоличествоСообщений = _ЖурналСообщенийПроцесса.Количество();

	Для ИндексСообщения = 1 По Мин(КоличествоПроверяемыхСообщений, КоличествоСообщений) Цикл

		Текст = _ЖурналСообщенийПроцесса[КоличествоСообщений - ИндексСообщения];
		Строки = СтрРазделить(Текст, Символы.ПС);
		КоличествоСтрок = Строки.Количество();

		Для ИндексСтроки = 1 По КоличествоСтрок Цикл
			Строка = Строки[КоличествоСтрок - ИндексСтроки];
			КускиТекста.Добавить(Строка);

			Если СтрНачинаетсяС(Строка, "{Модуль ") Или СтрНачинаетсяС(Строка, "{Module ") Тогда
				Возврат ИзвлечьКраткоеОписаниеОшибки(СтрСоединить(КускиТекста, Символы.ПС));
			КонецЕсли;
		КонецЦикла;

	КонецЦикла;

	Возврат "";

КонецФункции

Функция ИзвлечьКраткоеОписаниеОшибки(ТекстОшибки)

	Инд = СтрНайти(ТекстОшибки, "Ошибка в строке");

	Если Инд = 0 Тогда
		Инд = СтрНайти(ТекстОшибки, "Error in line");
	КонецЕсли;

	Если Инд = 0 Тогда 
		Возврат ТекстОшибки;
	КонецЕсли;

	Инд = СтрНайти(ТекстОшибки, "/", , Инд);

	Если Инд = 0 Тогда 
		Возврат ТекстОшибки;
	КонецЕсли;

	Возврат Сред(ТекстОшибки, Инд + 2);

КонецФункции

Процедура УдалитьАртефакты()
	_МенеджерВременныхФайлов.Удалить();
КонецПроцедуры

Функция ОбернутьВКавычки(Строка)
	Кавычка = ?(ЭтоWindows(), """", "'");
	Возврат СтрШаблон("%2%1%2", Строка, Кавычка);
КонецФункции

Функция ОбернутьВКавычкиДляWindows(Строка)
	Если ЭтоWindows() Тогда
		Возврат ОбернутьВКавычки(Строка);
	Иначе
		Возврат Строка;
	КонецЕсли;
КонецФункции

// Определяет, является ли текущая ОС Windows
//
// Возвращаемое значение:
//   Булево
Функция ЭтоWindows()

	СистемнаяИнформация = Новый СистемнаяИнформация();
	ТекущаяПлатформа = СистемнаяИнформация.ТипПлатформы;

	Возврат ТекущаяПлатформа = ТипПлатформы.Windows_x86_64
		Или ТекущаяПлатформа = ТипПлатформы.Windows_x86;

КонецФункции

Функция РазрешитьПутиФС(Пути, КаталогРазрешенияПутей)

	Результат = Новый Массив();

	Для Каждого Путь Из Пути Цикл
		Результат.Добавить(РаботаСПутямиФайловойСистемы.ПолучитьАбсолютныйПуть(Путь, КаталогРазрешенияПутей));
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти