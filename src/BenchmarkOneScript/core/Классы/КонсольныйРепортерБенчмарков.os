#Использовать coloratos

#Область ПрограммныйИнтерфейс

// Выводит результат запуска бенчмарков
//
// Параметры:
//   Результат - РезультатЗапускаБенчмарков
Процедура ВывестиРезультатВыполнения(Результат) Экспорт

	// Среда окружения
	ТекстСреды = Строка(Результат.СредаОкружения);
	ТекстСреды = СтрЗаменить(ТекстСреды, Символы.ПС, " \" + Символы.ПС);

	ВывестиСтроку(" ");
	ВывестиСтроку("// Сводка", "Малиновый");
	ВывестиСтроку(ТекстСреды, "ТемноЖелтый");

	// Отчет в Markdown
	ВывестиСтроку(" ");
	ВывестиСтроку(Результат.Отчет.КакMarkdown(), "Бирюза");

	// Легенда
	ВывестиСтроку(" ");
	ВывестиСтроку("// Легенда", "Малиновый");
	ВывестиСтроку(Результат.Отчет.Легенда(), "Синий");

КонецПроцедуры

// Выводит заголовок запуска бенчмарков
//
// Параметры:
//   Версия - Строка - Версия исполняющей среды OneScript.
//                     Если не указана, определяется автоматически.
Процедура ВывестиЗаголовок(Версия = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Версия) Тогда
		Версия = Новый СистемнаяИнформация().Версия;
	КонецЕсли;
	
	ВывестиСтроку(" ");
	ВывестиСтроку(СтрШаблон("*** Запуск бенчмарков в исполняющей среде OneScript <%1> ***", Версия), "Синий");

КонецПроцедуры

// Выводит информацию о конфигурации запуска бенчмарков
//
// Параметры:
//   Конфигурация - КонфигурацияБенчмарков
Процедура ВывестиКонфигурацию(Конфигурация) Экспорт

	ВывестиСтроку(" ");
	ВывестиСтроку("// Конфигурация", "Малиновый");
	ВывестиСтроку(Строка(Конфигурация), "ТемноЖелтый");

КонецПроцедуры

// Выводит агрегированную статистику запуска бенчмарков
//
// Параметры:
//   Статистика - СтатистикаБенчмарка
Процедура ВывестиСтатистику(Статистика) Экспорт

	ВывестиСтроку(" ");
	ВывестиСтроку("// Статистика", "Малиновый");
	ВывестиСтроку(Строка(Статистика), "Бирюза");

КонецПроцедуры

// Выводит заголовок текущего бенчмарка
//
// Параметры:
//   ДескрипторБенчмарка - ДескрипторБенчмарка
Процедура ВывестиЗаголовокБенчмарка(ДескрипторБенчмарка) Экспорт
	
	Текст = СтрШаблон("// Бенчмарк: %1.%2", ДескрипторБенчмарка.ТипОбъекта(), ДескрипторБенчмарка.Метод());
	
	ВывестиСтроку(" ");
	ВывестиСтроку(Текст, "Малиновый");

КонецПроцедуры

// Выводит список параметров
//
// Параметры:
//   Параметры - Массив из ПараметрБенчмарка - Коллекция параметров бенчмарка
Процедура ВывестиПараметры(Параметры) Экспорт
		
	Цвет = "ТемноЖелтый";
	
	Если Параметры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Вывести("Параметры: ", Цвет);

	ЭтоПервыйПараметр = Истина;
	Для Каждого Параметр Из Параметры Цикл	

		Если Не ЭтоПервыйПараметр Тогда
			Вывести(", ", Цвет);
		КонецЕсли;
		
		Вывести(СтрШаблон("%1 = %2", Параметр.Имя(), Строка(Параметр.Представление())), Цвет);
		
		ЭтоПервыйПараметр = Ложь;

	КонецЦикла;
	
	ВывестиСтроку("");	
	
КонецПроцедуры

// Выводит строку в консоль с переводом строки
//
// Параметры:
//   Текст - Строка - Выводимый текст
//   Цвет  - Строка - Имя цвета для оформления вывода
Процедура ВывестиСтроку(Текст, Цвет = Неопределено) Экспорт

	Если Бенчмаркинг.ЭтоВоркер() И Не Цвет = Неопределено Тогда
		ЦветнойВывод.ВывестиСтроку(ТекстСЦветом(Текст, Цвет));
	Иначе
		ЦветнойВывод.ВывестиСтроку(ПодготовитьТекст(Текст), Цвет);
	КонецЕсли;

КонецПроцедуры

// Выводит текст в консоль без добавления перевода строки
//
// Параметры:
//   Текст - Строка - Выводимый текст
//   Цвет  - Строка - Имя цвета для оформления вывода
Процедура Вывести(Текст, Цвет = Неопределено) Экспорт

	Если Бенчмаркинг.ЭтоВоркер() И Не Цвет = Неопределено Тогда
		ЦветнойВывод.Вывести(ТекстСЦветом(Текст, Цвет));
	Иначе
		ЦветнойВывод.Вывести(ПодготовитьТекст(Текст), Цвет);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьТекст(Знач Текст)
	Возврат СтрЗаменить(Текст, "|!#color=", "|#color=");
КонецФункции

Функция ТекстСЦветом(Текст, Цвет)

	Шаблон = "(%1|!#color=%2)";
	МассивСтрок = СтрРазделить(Текст, Символы.ПС);

	Для Инд = 0 По МассивСтрок.ВГраница() Цикл
		МассивСтрок[Инд] = СтрШаблон(Шаблон, МассивСтрок[Инд], Цвет);
	КонецЦикла;

	Возврат СтрСоединить(МассивСтрок, Символы.ПС);

КонецФункции

#КонецОбласти