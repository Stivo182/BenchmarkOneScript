
&Сериализуемое("Event")
Перем _Событие; // Строка - см. СобытияБенчмарков

&Сериализуемое("Handler")
Перем _КвалифицированноеИмяМетода; // Строка

&Несериализуемое
Перем _Объект; // Произвольный

&Несериализуемое
Перем _Делегат; // Действие

#Область ОбработчикиСобытий

// Конструктор обработчика события. 
// Инициализирует связь между событием бенчмарка и методом-обработчиком.
//
// Параметры:
//   КвалифицированноеИмяМетода - Строка - Имя метода в формате:
//     - <ИмяМетода> - Имя метода класса бенчмарков
//     - <ИмяМодуля>.<ИмяМетода> - Метод модуля
//     - <ИмяТипа>.<ИмяМетода> - Метод экземпляра указанного типа
//   Событие - Строка - см. СобытияБенчмарков 
Процедура ПриСозданииОбъекта(КвалифицированноеИмяМетода = "", Событие = "")
	_КвалифицированноеИмяМетода = КвалифицированноеИмяМетода;
	_Событие = Событие;
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Возвращает тип события бенчмарка, для которого предназначен обработчик.
//
// Возвращаемое значение:
//   Строка - см. СобытияБенчмарков
Функция Событие() Экспорт
	Возврат _Событие;
КонецФункции

// Возвращает квалифицированное имя метода.
//
// Возвращаемое значение:
//   Строка - см. СобытияБенчмарков
Функция ИмяМетода() Экспорт
	Возврат _КвалифицированноеИмяМетода;
КонецФункции

// Возвращает делегат для вызова метода-обработчика. 
// Кэширует созданный делегат для последнего переданного объекта.
//
// Параметры:
//   Объект - Произвольный - Объект для создания делегата.
//
// Возвращаемое значение:
//   Действие
Функция Делегат(Объект = Неопределено) Экспорт

	Если _Делегат <> Неопределено И _Объект = Объект Тогда
		Возврат _Делегат;
	КонецЕсли;

	ЛенивыйДелегат = Новый ЛенивыйДелегат(_КвалифицированноеИмяМетода, Объект);

	_Объект = Объект;
	_Делегат = ЛенивыйДелегат.Получить();

	Возврат _Делегат;

КонецФункции

#КонецОбласти