Перем _Источник; // Строка
Перем _ОбъектБенчмарков; // Произвольный

Процедура ПриСозданииОбъекта(Источник, ОбъектБенчмарков)

	_Источник = Источник;
	_ОбъектБенчмарков = ОбъектБенчмарков;

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Получает значения из источника и формирует общие параметры бенчмарков.
//
// Параметры:
//   ИмяПоля - Строка - Имя поля класса бенчмарков.
//
// Возвращаемое значение:
//   Массив из ПараметрБенчмарка
Функция ПрочитатьКакОбщиеПараметры(ИмяПоля) Экспорт

	Результат = Новый Массив();

	Значения = ПрочитатьЗначения();
	Для Каждого Значение Из Значения Цикл
		Результат.Добавить(Новый ПараметрБенчмарка(ИмяПоля, Значение, Ложь));
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Получает значения из источника и формирует наборы параметров бенчмарка.
//
// Параметры:
//   ДескрипторБенчмарка - ДескрипторБенчмарка - Дескриптор бенчмарка.
//
// Возвращаемое значение:
//   Массив из ПараметрыМетодаБенчмарка
Функция ПрочитатьКакПараметрыБенчмарка(ДескрипторБенчмарка) Экспорт

	Результат = Новый Массив();

	НаборыПараметров = ПрочитатьЗначения();
	Для Каждого Параметры Из НаборыПараметров Цикл
		Результат.Добавить(ПреобразоватьВПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры));
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрочитатьЗначения()

	ЛенивыйДелегат = Новый ЛенивыйДелегат(_Источник, _ОбъектБенчмарков);
	Значение = ЛенивыйДелегат.Получить().Выполнить();
	
	Тип = ТипЗнч(Значение);
	Если Не (Тип = Тип("Массив") Или Тип = Тип("ТаблицаЗначений")) Тогда
		Результат = Новый Массив();
		Результат.Добавить(Значение);
		Возврат Результат;
	Иначе
		Возврат Значение;
	КонецЕсли;
	
КонецФункции

Функция ПреобразоватьВПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры)

	ИменаПараметров = ДескрипторБенчмарка.ИменаПараметров();
	КоличествоПараметровМетода = ИменаПараметров.Количество();

	Если КоличествоПараметровМетода = 0 Тогда
		Возврат Новый Массив();
	КонецЕсли;

	Если КоличествоПараметровМетода = 1 Тогда
		ПараметрБенчмарка = Новый ПараметрБенчмарка(ИменаПараметров[0], Параметры, Истина);
		ПараметрыМетодаБенчмарка = Новый ПараметрыМетодаБенчмарка();
		ПараметрыМетодаБенчмарка.Добавить(ПараметрБенчмарка);
		Возврат ПараметрыМетодаБенчмарка;
	КонецЕсли;

	ТипКоллекции = ТипЗнч(Параметры);

	Если ТипКоллекции = Тип("Массив") Тогда
		Возврат ПреобразоватьМассивВПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры);
	ИначеЕсли ТипКоллекции = Тип("Структура") Или ТипКоллекции = Тип("Соответствие") Тогда
		Возврат ПреобразоватьСтруктуруВПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры);
	ИначеЕсли ТипКоллекции = Тип("СтрокаТаблицыЗначений") Тогда
		Возврат ПреобразоватьСтрокуТаблицыЗначенийВПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры);
	Иначе
		ВызватьИсключение СтрШаблон(
			"Не поддерживается тип <%1> для передачи параметров в метод бенчмарка <%2>",
			ТипКоллекции,
			ДескрипторБенчмарка.Метод()
		);
	КонецЕсли;

КонецФункции

Функция ПреобразоватьМассивВПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры)
	
	ПараметрыМетодаБенчмарка = Новый ПараметрыМетодаБенчмарка();
	ИменаПараметров = ДескрипторБенчмарка.ИменаПараметров();
	КоличествоТребуемыхПараметров = ИменаПараметров.Количество();

	Если Параметры.Количество() <> КоличествоТребуемыхПараметров Тогда		
		ВызватьИсключение СтрШаблон(
			"Количество переданных параметров (%1) не соответствует ожидаемому количеству (%2) для метода бенчмарка <%3>.",
			Параметры.Количество(),
			КоличествоТребуемыхПараметров,
			ДескрипторБенчмарка.Метод()
		);
	КонецЕсли;

	Для ИндексПараметра = 0 По Параметры.ВГраница() Цикл
		ПараметрБенчмарка = Новый ПараметрБенчмарка(ИменаПараметров[ИндексПараметра], Параметры[ИндексПараметра], Истина);
		ПараметрыМетодаБенчмарка.Добавить(ПараметрБенчмарка);
	КонецЦикла;

	Возврат ПараметрыМетодаБенчмарка;

КонецФункции

Функция ПреобразоватьСтруктуруВПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры)
		
	ПараметрыМетодаБенчмарка = Новый ПараметрыМетодаБенчмарка();
	ТипКоллекции = ТипЗнч(Параметры);

	Для Каждого ИмяПараметра Из ДескрипторБенчмарка.ИменаПараметров() Цикл

		Если ТипКоллекции = Тип("Структура") Тогда
			ОтсутствуетПараметр = Не Параметры.Свойство(ИмяПараметра);
		Иначе
			ОтсутствуетПараметр = Параметры[ИмяПараметра] = Неопределено;
		КонецЕсли;

		Если ОтсутствуетПараметр Тогда
			ВызватьИсключение СтрШаблон(
				"Отсутствует параметр <%1> метода бенчмарка <%2> в переданной коллекции <%3>",
				ИмяПараметра,
				ДескрипторБенчмарка.Метод(),
				ТипКоллекции
			);
		КонецЕсли;

		ПараметрБенчмарка = Новый ПараметрБенчмарка(ИмяПараметра, Параметры[ИмяПараметра], Истина);
		ПараметрыМетодаБенчмарка.Добавить(ПараметрБенчмарка);

	КонецЦикла;

	Возврат ПараметрыМетодаБенчмарка;

КонецФункции

Функция ПреобразоватьСтрокуТаблицыЗначенийВПараметрыМетодаБенчмарка(ДескрипторБенчмарка, Параметры)
		
	ПараметрыМетодаБенчмарка = Новый ПараметрыМетодаБенчмарка();
	Колонки = Параметры.Владелец().Колонки;

	Для Каждого ИмяПараметра Из ДескрипторБенчмарка.ИменаПараметров() Цикл

		Если Колонки.Найти(ИмяПараметра) = Неопределено Тогда
			ВызватьИсключение СтрШаблон(
				"Отсутствует параметр <%1> метода бенчмарка <%2> в переданной таблице",
				ИмяПараметра,
				ДескрипторБенчмарка.Метод()
			);
		КонецЕсли;

		ПараметрБенчмарка = Новый ПараметрБенчмарка(ИмяПараметра, Параметры[ИмяПараметра], Истина);
		ПараметрыМетодаБенчмарка.Добавить(ПараметрБенчмарка);

	КонецЦикла;

	Возврат ПараметрыМетодаБенчмарка;

КонецФункции

#КонецОбласти