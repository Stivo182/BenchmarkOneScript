
#Использовать xml-parser
#Использовать logos

Перем _Лог; // Лог

// Экспортирует результаты бенчмарков в файл XML
Процедура ПриСозданииОбъекта()
	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.ЭкспортерРезультатовБенчмарковВXml");
КонецПроцедуры

// Экспортирует результаты бенчмарков в файл XML
//
// Параметры:
//   РезультатыБенчмарков - см. Бенчмаркинг.Запустить
Процедура Записать(РезультатыБенчмарков) Экспорт

	КаталогАртефактов = РезультатыБенчмарков.Конфигурация.КаталогАртефактов();
	ПутьКФайлу = ОбъединитьПути(КаталогАртефактов, РезультатыБенчмарков.ИмяКласса + "-report." + Расширение());

	_Лог.Отладка("Начало формирования файла: %1", ПутьКФайлу);

	Данные = ЭкспортРезультатовБенчмарков.ПодготовитьРезультаты(РезультатыБенчмарков);
	ПодготовитьМассивы(Данные);
	Данные = Новый Структура("Отчет", Данные);

	ПроцессорXML = Новый СериализацияДанныхXML(Ложь);
	ПроцессорXML.ЗаписатьВФайл(Данные, ПутьКФайлу, Истина);

	_Лог.Отладка("Завершение формирования файла: %1", ПутьКФайлу);

КонецПроцедуры

// Расширение файла
//
// Возвращаемое значение:
//   Строка
Функция Расширение() Экспорт
	Возврат "xml";
КонецФункции

Процедура ПодготовитьМассивы(Данные)
		
	Для Каждого Бенчмарк Из Данные.Бенчмарки Цикл
		Бенчмарк.Статистика.Значения = Новый Структура("Значение", Бенчмарк.Статистика.Значения);
	КонецЦикла;

	Данные.Бенчмарки = Новый Структура("Бенчмарк", Данные.Бенчмарки);

КонецПроцедуры