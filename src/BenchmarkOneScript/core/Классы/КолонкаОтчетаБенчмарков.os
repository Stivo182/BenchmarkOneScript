// BSLLS:ExportVariables-off

Перем Имя Экспорт; // Строка
Перем Заголовок Экспорт; // Строка
Перем Описание Экспорт; // Строка
Перем ЕдиницаИзмерения Экспорт; // см. ЕдиницыИзмеренийБенчмарков
Перем ЕдиницаИзмеренияПредставления Экспорт; // см. ЕдиницыИзмеренийБенчмарков
Перем СтильКоэффициента Экспорт; // см. СтилиКоэффициентаБенчмарка
Перем ФорматнаяСтрока Экспорт; // Строка
Перем Размер Экспорт; // Число
Перем ЭтоЧисло Экспорт; // Булево
Перем ЗначениеДелегат Экспорт; // Делегат
Перем ПредставлениеЗначенияДелегат Экспорт; // Делегат

// Колонка отчета бенчмарков
//
// Параметры:
//   пИмя - Строка - Имя колонки
Процедура ПриСозданииОбъекта(пИмя)

	Имя = пИмя;
	Заголовок = "";
	Описание = "";
	ФорматнаяСтрока = "";
	Размер = 0;
	ЭтоЧисло = Ложь;

КонецПроцедуры

// Значение по результатам замеров
//
// Параметры:
//   СтрокаРезультата - СтрокаТаблицыЗначений - см. ЗапускательБенчмарков.НоваяТаблицаРезультатовЗапусков
//   Колонка - КолонкаОтчетаБенчмарков
//
// Возвращаемое значение:
//   Произвольный
Функция Значение(СтрокаРезультата, Колонка) Экспорт
	
	МассивПараметров = Новый Массив();
	МассивПараметров.Добавить(СтрокаРезультата);
	МассивПараметров.Добавить(Колонка);

	Возврат ЗначениеДелегат.Исполнить(МассивПараметров);
	
КонецФункции

// Представление значения
//
// Параметры:
//   Значение - Произвольный
//   СтрокаОтчета - СтрокаТаблицыЗначений
//
// Возвращаемое значение:
//   Строка
Функция ПредставлениеЗначения(Знач Значение, СтрокаОтчета) Экспорт

	Если Не ПредставлениеЗначенияДелегат = Неопределено Тогда

		МассивПараметров = Новый Массив();
		МассивПараметров.Добавить(Значение);
		МассивПараметров.Добавить(СтрокаОтчета);
	
		Возврат ПредставлениеЗначенияДелегат.Исполнить(МассивПараметров);

	КонецЕсли;

	Если СтильКоэффициента = СтилиКоэффициентаБенчмарка.Процент Тогда

		ФорматнаяСтрокаПроцента = ПредставленияПоказателейБенчмарков.ФорматнаяСтрокаЧисла(2);

		Если СтрокаОтчета.Эталон Тогда
			Представление = "-";
		Иначе
			Представление = ?(Значение >= 1,
				"+" + Формат((Значение - 1) * 100, ФорматнаяСтрокаПроцента) + "%", 
				"-" + Формат((1 - Значение) * 100, ФорматнаяСтрокаПроцента) + "%"
			);
		КонецЕсли;

		Возврат Представление;

	Иначе
		
		ЕдиницаИзмеренияЗначения = ЕдиницаИзмерения;
		Если Не ЕдиницаИзмеренияПредставления = Неопределено Тогда
			Значение = ЕдиницыИзмеренийБенчмарков.Конвертировать(Значение, ЕдиницаИзмерения, ЕдиницаИзмеренияПредставления);
			ЕдиницаИзмеренияЗначения = ЕдиницаИзмеренияПредставления;
		КонецЕсли;

		ДополнитьЕдиницойИзмерения = Не ЕдиницаИзмеренияЗначения = Неопределено 
			И Не ВРег(Заголовок) = ВРег(ЕдиницаИзмеренияЗначения.Обозначение);

		Возврат ПредставленияПоказателейБенчмарков.Представление(
			Значение,
			ЕдиницаИзмеренияЗначения,
			ФорматнаяСтрока,
			ДополнитьЕдиницойИзмерения
		);

	КонецЕсли;

КонецФункции