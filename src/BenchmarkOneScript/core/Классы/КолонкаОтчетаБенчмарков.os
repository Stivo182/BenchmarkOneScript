// BSLLS:ExportVariables-off

Перем Имя Экспорт; // Строка
Перем Заголовок Экспорт; // Строка
Перем Описание Экспорт; // Строка
Перем ЕдиницаИзмерения Экспорт; // см. ЕдиницыИзмеренийБенчмарков
Перем ЕдиницаИзмеренияПредставления Экспорт; // см. ЕдиницыИзмеренийБенчмарков
Перем СтильКоэффициента Экспорт; // см. СтилиКоэффициентаБенчмарка
Перем ФорматнаяСтрока Экспорт; // Строка
Перем Размер Экспорт; // Число
Перем ЭтоЧисло Экспорт; // Булево
Перем ДелегатЗначение Экспорт; // Действие
Перем ДелегатПредставлениеЗначения Экспорт; // Действие

// Лямбда-выражение для скрытия колонки при выполнении условия.
//   Пример: 
//      Элемент -> Элемент >= 0 И Элемент < 0.02
//   Логика: 
//     Колонка скрывается, если ВСЕ её элементы находятся в интервале [0, 0.02).
Перем ФункцияСкрытия Экспорт; // Строка

// Колонка отчета бенчмарков
//
// Параметры:
//   пИмя - Строка - Имя колонки
Процедура ПриСозданииОбъекта(пИмя)

	Имя = пИмя;
	Заголовок = "";
	Описание = "";
	ФорматнаяСтрока = "";
	Размер = 0;
	ЭтоЧисло = Ложь;
	ФункцияСкрытия = "";

КонецПроцедуры

// Значение по результатам замеров
//
// Параметры:
//   РезультатЗапускаБенчмарка - РезультатЗапускаБенчмаркаДто
//   Колонка - КолонкаОтчетаБенчмарков
//
// Возвращаемое значение:
//   Произвольный
Функция Значение(РезультатЗапускаБенчмарка, Колонка) Экспорт
	Возврат ДелегатЗначение.Выполнить(РезультатЗапускаБенчмарка, Колонка);
КонецФункции

// Представление значения
//
// Параметры:
//   Значение - Произвольный
//   СтрокаОтчета - СтрокаТаблицыЗначений
//
// Возвращаемое значение:
//   Строка
Функция ПредставлениеЗначения(Знач Значение, СтрокаОтчета) Экспорт

	Если Не ДелегатПредставлениеЗначения = Неопределено Тогда
		Возврат ДелегатПредставлениеЗначения.Выполнить(Значение, СтрокаОтчета);
	КонецЕсли;

	Если СтильКоэффициента = СтилиКоэффициентаБенчмарка.Процент Тогда

		ФорматнаяСтрокаПроцента = ПредставленияПоказателейБенчмарков.ФорматнаяСтрокаЧисла(2);

		Если СтрокаОтчета.Эталон Тогда
			Представление = "-";
		Иначе
			Представление = ?(Значение >= 1,
				"+" + Формат((Значение - 1) * 100, ФорматнаяСтрокаПроцента) + "%", 
				"-" + Формат((1 - Значение) * 100, ФорматнаяСтрокаПроцента) + "%"
			);
		КонецЕсли;

		Возврат Представление;

	Иначе
		
		ЕдиницаИзмеренияЗначения = ЕдиницаИзмерения;
		Если Не ЕдиницаИзмеренияПредставления = Неопределено Тогда
			Значение = ЕдиницыИзмеренийБенчмарков.Конвертировать(Значение, ЕдиницаИзмерения, ЕдиницаИзмеренияПредставления);
			ЕдиницаИзмеренияЗначения = ЕдиницаИзмеренияПредставления;
		КонецЕсли;

		ДополнитьЕдиницейИзмерения = Не ЕдиницаИзмеренияЗначения = Неопределено 
			И Не ВРег(Заголовок) = ВРег(ЕдиницаИзмеренияЗначения.Обозначение);

		Возврат ПредставленияПоказателейБенчмарков.Представление(
			Значение,
			ЕдиницаИзмеренияЗначения,
			ФорматнаяСтрока,
			ДополнитьЕдиницейИзмерения
		);

	КонецЕсли;

КонецФункции