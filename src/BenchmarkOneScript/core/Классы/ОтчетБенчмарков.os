// BSLLS:ExportVariables-off

Перем Таблица Экспорт; // ТаблицаЗначений
Перем Колонки Экспорт; // Массив из КолонкаОтчетаБенчмарков

Процедура ПриСозданииОбъекта()
	
	Таблица = Новый ТаблицаЗначений();
	Колонки = Новый Массив();

КонецПроцедуры

// Конвертирует отчет в формат Markdown
//
// Возвращаемое значение:
//   Строка
Функция КакMarkdown() Экспорт
	
	КонвертерВMarkdown = Новый КонвертерОтчетаБенчмарковВMarkdown(ЭтотОбъект);
	Возврат КонвертерВMarkdown.Конвертировать();

КонецФункции

// Легенда отчета
//
// Возвращаемое значение:
//   Строка
Функция Легенда() Экспорт

	Состав = Новый СписокЗначений();

	// Колонки
	Для Каждого Колонка Из Колонки Цикл
		Если ЗначениеЗаполнено(Колонка.Описание) Тогда
			Состав.Добавить(Колонка.Заголовок, Колонка.Описание);
		КонецЕсли;
	КонецЦикла;
	
	// Единицы измерения
	Для Каждого Колонка Из Колонки Цикл

		Если ЗначениеЗаполнено(Колонка.ЕдиницаИзмеренияПредставления) Тогда
			Единица = Колонка.ЕдиницаИзмеренияПредставления;
		ИначеЕсли ЗначениеЗаполнено(Колонка.ЕдиницаИзмерения) Тогда
			Единица = Колонка.ЕдиницаИзмерения;
		Иначе
			Продолжить;
		КонецЕсли;

		Если Не Единица.ВыводитьВЛегенде Тогда
			Продолжить;
		КонецЕсли;

		Заголовок = "1 " + Единица.Обозначение;
		Если Состав.НайтиПоЗначению(Заголовок) = Неопределено Тогда
			Состав.Добавить(Заголовок, "1 " + Единица.Наименование);
		КонецЕсли;

	КонецЦикла;

	// Длина обозначения
	МаксДлина = 0;
	Для Каждого Строка Из Состав Цикл
		МаксДлина = Макс(СтрДлина(Строка.Значение), МаксДлина);
	КонецЦикла;

	// Сборка
	Результат = Новый Массив();
	Для Каждого Строка Из Состав Цикл
		Обозначение = ПредставленияПоказателейБенчмарков.ФиксированнаяСтрока(Строка.Значение, МаксДлина);
		Результат.Добавить(Обозначение + " : " + Строка.Представление);
	КонецЦикла;

	Возврат СтрСоединить(Результат, Символы.ПС);

КонецФункции