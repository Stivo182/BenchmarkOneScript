#Использовать fs
#Использовать 1commands

Перем _КаталогУстановки; // Строка, Неопределено

#Область ПрограммныйИнтерфейс

// Выполняет поиск исполняемого файла OneScript для указанной версии или алиаса.
//
// Параметры:
//   Версия - Строка - Требуемая версия или алиас OneScript (например, "2.0.0" или "stable")
//
// Возвращаемое значение:
//   Строка       - Полный путь к исполняемому файлу, если найден
//   Неопределено - Если каталог установки не найден или исполняемый файл отсутствует
Функция НайтиИсполняемыйФайл(Версия) Экспорт

	Если Не _КаталогУстановки = Неопределено Тогда
		КаталогУстановки = _КаталогУстановки;
	Иначе
		КаталогУстановки = ОпределитьКаталогУстановки();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(КаталогУстановки) Тогда
		Возврат Неопределено;
	КонецЕсли;

	КаталогBin = ОбъединитьПути(КаталогУстановки, Версия, "bin");

	Возврат НайтиИсполняемыйФайлВКаталоге(КаталогBin);

КонецФункции

// Определяет каталог, в котором установлены версии OneScript.
//
// Возвращаемое значение:
//   Строка       - Путь к каталогу установки версий OneScript
//   Неопределено - Если каталог установки не удалось определить ни одним из способов
Функция ОпределитьКаталогУстановки() Экспорт

	_КаталогУстановки = Неопределено;

	// Из переменной среды OVM_INSTALL_PATH
	КаталогУстановки = ПолучитьПеременнуюСреды("OVM_INSTALL_PATH");
	Если ЗначениеЗаполнено(КаталогУстановки) И ФС.КаталогСуществует(КаталогУстановки) Тогда
		_КаталогУстановки = КаталогУстановки;
		Возврат КаталогУстановки;
	КонецЕсли;

	// Из переменной среды OSCRIPTBIN
	КаталогИсполняющейСреды = ПолучитьПеременнуюСреды("OSCRIPTBIN");
	Если ЗначениеЗаполнено(КаталогИсполняющейСреды) И ФС.КаталогСуществует(КаталогИсполняющейСреды) Тогда
		КаталогУстановки = ФС.НормализоватьПуть(ОбъединитьПути(КаталогИсполняющейСреды, "../.."));
		ИмяКаталога = Новый Файл(КаталогУстановки).Имя;
		Если ИмяКаталога = "ovm" Тогда
			_КаталогУстановки = КаталогУстановки;
			Возврат КаталогУстановки;
		КонецЕсли;
	КонецЕсли;	

	// Через ovm which
	КаталогУстановки = ОпределитьКаталогУстановкиЧерезWhich();
	Если ЗначениеЗаполнено(КаталогУстановки) Тогда
		_КаталогУстановки = КаталогУстановки;
		Возврат КаталогУстановки;
	КонецЕсли;

КонецФункции

// Находит исполняемый файл в каталоге.
//
// Параметры:
//   Каталог - Строка - Каталог поиска
//
// Возвращаемое значение:
//   Строка       - Полный путь к исполняемому файлу, если найден
//   Неопределено - Если каталог установки не найден или исполняемый файл отсутствует
Функция НайтиИсполняемыйФайлВКаталоге(Каталог) Экспорт
	
	ВариантыИмени = Новый Массив();
	ВариантыИмени.Добавить("oscript.exe");
	ВариантыИмени.Добавить("oscript");

	Для Каждого ИмяФайла Из ВариантыИмени Цикл

		ПутьКФайлу = ОбъединитьПути(Каталог, ИмяФайла);

		Если ФС.ФайлСуществует(ПутьКФайлу) Тогда
			Возврат ПутьКФайлу;
		КонецЕсли;

	КонецЦикла;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОпределитьКаталогУстановкиЧерезWhich()
		
	Команда = Новый Команда();
	Команда.УстановитьКоманду("ovm");
	Команда.ДобавитьПараметр("which");
	Команда.ДобавитьПараметр("current");
	КодВозврата = Команда.Исполнить();

	Если Не КодВозврата = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	ПутьКФайлу = СокрЛП(Команда.ПолучитьВывод());
	КаталогФайла = Новый Файл(ПутьКФайлу).Путь;

	КаталогУстановки = ФС.НормализоватьПуть(ОбъединитьПути(КаталогФайла, "../.."));

	Возврат КаталогУстановки;

КонецФункции

#КонецОбласти