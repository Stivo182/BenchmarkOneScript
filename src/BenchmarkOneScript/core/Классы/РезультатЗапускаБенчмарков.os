// BSLLS:ExportVariables-off

&Сериализуемое("HostEnvironmentInfo")
&Тип("СредаОкруженияБенчмарков")
Перем СредаОкружения Экспорт; // СредаОкруженияБенчмарков

&Сериализуемое("Configuration")
&Тип("КонфигурацияБенчмарков")
Перем Конфигурация Экспорт; // КонфигурацияБенчмарков

&Сериализуемое("BenchmarkDescriptors")
&Тип("КоллекцияДескрипторовБенчмарков")
Перем ДескрипторыБенчмарков Экспорт; // КоллекцияДескрипторовБенчмарков

&Сериализуемое("Runs")
&Тип("Массив") &ДляКаждого &Тип("РезультатЗапускаБенчмаркаДто")
Перем Запуски Экспорт; // Массив из РезультатЗапускаБенчмаркаДто

&Несериализуемое
Перем Отчет Экспорт; // ОтчетБенчмарков

Процедура ПриСозданииОбъекта()
	
	ДескрипторыБенчмарков = Новый КоллекцияДескрипторовБенчмарков();
	Конфигурация = Новый КонфигурацияБенчмарков();
	Отчет = Новый ОтчетБенчмарков();
	СредаОкружения = Новый СредаОкруженияБенчмарков();
	Запуски = Новый Массив();

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Связывает каждую строку замеров с эталонной строкой.
//
// Алгоритм поиска эталонов (в порядке приоритета):
//  1. При наличии эталонной версии исполняющей среды:
//     - Ищет строку бенчмарка с идентичным именем и набором параметров в соответствующей эталонной среде
//  2. При отсутствии эталонной среды, но при наличии эталонного бенчмарка:
//     - Ищет строку бенчмарка в пределах текущей категории бенчмарков, наборов параметров и исполняющей среды
Процедура ОпределитьЭталоны() Экспорт

	ЭталоннаяВерсия = Конфигурация.ЭталоннаяВерсияИсполняющейСреды();

	Для Каждого РезультатЗапускаБенчмарка Из Запуски Цикл

		РезультатЗапускаБенчмарка.Эталон = НайтиЭталонДляСтроки(РезультатЗапускаБенчмарка, ЭталоннаяВерсия);
	
		Если Не ЗначениеЗаполнено(ЭталоннаяВерсия) Тогда
			РезультатЗапускаБенчмарка.ЭтоЭталон = РезультатЗапускаБенчмарка.ДескрипторБенчмарка.ЭтоЭталон();
		Иначе
			РезультатЗапускаБенчмарка.ЭтоЭталон = 
				ЭталоннаяВерсия = РезультатЗапускаБенчмарка.ИсполняющаяСреда.Версия 
				Или ЭталоннаяВерсия = РезультатЗапускаБенчмарка.ИсполняющаяСреда.Алиас;
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

// Пересчитывает статистические показатели для всех замеров в таблице
Процедура ОбновитьСтатистику() Экспорт
	Для Каждого РезультатЗапускаБенчмарка Из Запуски Цикл
		РезультатЗапускаБенчмарка.Статистика.Прочитать(РезультатЗапускаБенчмарка.Замеры);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НайтиЭталонДляСтроки(ВходнаяСтрока, ЭталоннаяВерсияИсполняющейСреды)

	Для Каждого РезультатЗапуска Из Запуски Цикл

		ЭтоЭталон = РезультатЗапуска.ДескрипторБенчмарка.ЭтоЭталон();
		ЭтоРазныеСтроки = Не РезультатЗапуска = ВходнаяСтрока;
		ИмяМетодаСовпадает = РезультатЗапуска.ДескрипторБенчмарка.Метод() = ВходнаяСтрока.ДескрипторБенчмарка.Метод();
		КатегорияСовпадает = РезультатЗапуска.ДескрипторБенчмарка.Категория() = ВходнаяСтрока.ДескрипторБенчмарка.Категория();
		ПараметрыСовпадают = ПараметрыСовпадают(РезультатЗапуска, ВходнаяСтрока);
		ВерсияИсполняющейСредыСовпадает = РезультатЗапуска.ИсполняющаяСреда.Версия = ВходнаяСтрока.ИсполняющаяСреда.Версия;
		ЭталоннаяВерсияИсполняющейСредыУказана = ЗначениеЗаполнено(ЭталоннаяВерсияИсполняющейСреды);
		ЭталоннаяВерсияИсполняющейСредыСовпадает = ЭталоннаяВерсияИсполняющейСреды = РезультатЗапуска.ИсполняющаяСреда.Версия 
			Или ЭталоннаяВерсияИсполняющейСреды = РезультатЗапуска.ИсполняющаяСреда.Алиас;

		ПоискПоЭталоннойСреде = ЭталоннаяВерсияИсполняющейСредыУказана 
			И ЭталоннаяВерсияИсполняющейСредыСовпадает 
			И ИмяМетодаСовпадает;

		ПоискПоЭталонномуБенчмарку = Не ЭталоннаяВерсияИсполняющейСредыУказана 
			И ЭтоЭталон 
			И КатегорияСовпадает 
			И ВерсияИсполняющейСредыСовпадает;

		НайденЭталон = ЭтоРазныеСтроки 
			И ПараметрыСовпадают
			И (ПоискПоЭталоннойСреде Или ПоискПоЭталонномуБенчмарку);

		Если НайденЭталон Тогда
			Возврат РезультатЗапуска;
		КонецЕсли;

	КонецЦикла;

КонецФункции

Функция ПараметрыСовпадают(СтрокаПервая, СтрокаВторая)
		
	Если СтрокаПервая.Параметры.Количество() <> СтрокаВторая.Параметры.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;

	ПараметрыСовпадают = Истина;
	Для Каждого ПараметрПервой Из СтрокаПервая.Параметры Цикл

		ПараметрНайден = Ложь;
		Для Каждого ПараметрВторой Из СтрокаВторая.Параметры Цикл
			Если ПараметрПервой.Имя() = ПараметрВторой.Имя()
				И ПараметрПервой.Значение() = ПараметрВторой.Значение() 
				И ПараметрПервой.Представление() = ПараметрВторой.Представление() Тогда
				ПараметрНайден = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если Не ПараметрНайден Тогда
			ПараметрыСовпадают = Ложь;
			Прервать;
		КонецЕсли;

	КонецЦикла;

	Возврат ПараметрыСовпадают;

КонецФункции

#Область Сериализация

&ПослеДесериализации
Процедура ПослеДесериализации() Экспорт

	ПереопределитьДескрипторыВЗапусках();
	ОпределитьЭталоны();
	ОбновитьСтатистику();

	ПостроительОтчета = Новый ПостроительОтчетаБенчмарков(ДескрипторыБенчмарков, Конфигурация, Запуски);
	Отчет = ПостроительОтчета.Сформировать();

КонецПроцедуры

Процедура ПереопределитьДескрипторыВЗапусках()
	
	Для Каждого Запуск Из Запуски Цикл
		Дескриптор = Запуск.ДескрипторБенчмарка;
		НайденныйДескриптор = ДескрипторыБенчмарков.НайтиПоТипуИИмени(Дескриптор.ТипОбъекта(), Дескриптор.Метод());
		Если Не НайденныйДескриптор = Неопределено Тогда
			Запуск.ДескрипторБенчмарка = НайденныйДескриптор;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры
	
#КонецОбласти

#КонецОбласти