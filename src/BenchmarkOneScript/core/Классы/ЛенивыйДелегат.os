Перем _КвалифицированноеИмяМетода; // Строка
Перем _Объект;                     // Произвольный
Перем _Действие;                   // Дейтсвие, Неопределено

#Область ОбработчикиСобытий

// Конструктор делегата. Инициализирует параметры для последующего создания делегата.
//
// Параметры:
//   КвалифицированноеИмяМетода - Строка - Имя метода в формате:
//     * <ИмяМетода> - Имя метода объекта. Требует указания параметра Объект
//     * <ИмяМодуля>.<ИмяМетода> - Метод модуля
//     * <ИмяТипа>.<ИмяМетода> - Метод экземпляра указанного типа
//   Объект - Произвольный - Объект, содержащий метод. Обязателен для формата <ИмяМетода>.
//
Процедура ПриСозданииОбъекта(КвалифицированноеИмяМетода, Объект = Неопределено)
	_КвалифицированноеИмяМетода = КвалифицированноеИмяМетода;
	_Объект = Объект;
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Создает и возвращает объект Действие для указанного метода.
// Анализирует квалифицированное имя метода и определяет способ создания делегата.
//
// Возвращаемое значение:
//   Действие - Делегат для вызова метода
Функция Получить() Экспорт

	Если _Действие <> Неопределено Тогда
		Возврат _Действие;
	КонецЕсли;

	Части = СтрРазделить(_КвалифицированноеИмяМетода, ".");
	КоличествоЧастей = Части.Количество();

	Если КоличествоЧастей = 1 Тогда

		Если Не ЗначениеЗаполнено(_Объект) Тогда
			ВызватьИсключение СтрШаблон(
				"Для создания делегата <%1> не указан объект", _КвалифицированноеИмяМетода);
		КонецЕсли;

		_Действие = Новый Действие(_Объект, _КвалифицированноеИмяМетода);

	ИначеЕсли КоличествоЧастей = 2 Тогда

		Имя = Части[0];
		Метод = Части[1];

		Объект = ПолучитьМодуль(Имя);
		Если Объект = Неопределено Тогда
			Объект = Новый(Имя);
		КонецЕсли;

		_Действие = Новый Действие(Объект, Метод);
	
	Иначе

		ВызватьИсключение СтрШаблон("Квалифицированное имя метода <%1> задано некорректно", _КвалифицированноеИмяМетода);

	КонецЕсли;

	Возврат _Действие;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьМодуль(ИмяМодуля)

	Попытка
		Модуль = Вычислить(ИмяМодуля);
	Исключение
		Возврат Неопределено
	КонецПопытки;

	Если ТипЗнч(Модуль) <> Тип("Сценарий") Тогда
		ВызватьИсключение СтрШаблон("Модуль <%1> не является сценарием", ИмяМодуля);
	КонецЕсли;

	Возврат Модуль;

КонецФункции

#КонецОбласти