#Использовать delegate

Перем _Объект; // Произвольный - Ссылка на объект бенчмарк-класса
Перем _Метод; // Строка - Метод бенчмарка
Перем _ЭтоЭталон; // Булево
Перем _Категория; // Строка
Перем _НаборыПараметров; // Массив из Массив из ПараметрБенчмарка
Перем _ИменаПараметров; // Массив из Строка

// Бенчмарк
//
// Параметры:
//   Объект - Произвольный - Ссылка на объект бенчмарк-класса
//   Метод - Строка - Метод бенчмарка
Процедура ПриСозданииОбъекта(Объект, Метод)

	_Объект = Объект;
	_Метод = Метод;
	_НаборыПараметров = Новый Массив();
	_ЭтоЭталон = Ложь;
	_Категория = "";

	ЗаполнитьИменаПараметров();
	
КонецПроцедуры

// Ссылка на объект бенчмарк-класса
//
// Возвращаемое значение:
//   Произвольный
Функция Объект() Экспорт
	Возврат _Объект;
КонецФункции

// Метод бенчмарка
//
// Возвращаемое значение:
//   Строка
Функция Метод() Экспорт
	Возврат _Метод;
КонецФункции

// Наборы параметров
//
// Возвращаемое значение:
//   Массив из Массив из ПараметрБенчмарка
Функция НаборыПараметров() Экспорт
	Возврат Новый Массив(Новый ФиксированныйМассив(_НаборыПараметров));
КонецФункции

// Добавляет параметры в набор
//
// Параметры:
//   Параметры - Произвольный, Массив из Произвольный
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ДобавитьПараметры(Параметры) Экспорт
	
	МассивПараметров = Новый Массив();

	Если ТипЗнч(Параметры) = Тип("Массив") Тогда

		Для ИндексПараметра = 0 По Параметры.ВГраница() Цикл
			Параметр = Новый ПараметрБенчмарка(_ИменаПараметров[ИндексПараметра], Параметры[ИндексПараметра], Истина);
			МассивПараметров.Добавить(Параметр);		
		КонецЦикла;

	Иначе

		Параметр = Новый ПараметрБенчмарка(_ИменаПараметров[0], Параметры, Истина);
		МассивПараметров.Добавить(Параметр);	

	КонецЕсли;

	_НаборыПараметров.Добавить(МассивПараметров);

	Возврат ЭтотОбъект;

КонецФункции

// Очищает набор параметров
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ОчиститьПараметры() Экспорт

	_НаборыПараметров.Очистить();

	Возврат ЭтотОбъект;

КонецФункции

// Бенчмарк является эталоном
//
// Возвращаемое значение:
//   Булево
Функция ЭтоЭталон() Экспорт
	Возврат _ЭтоЭталон;
КонецФункции

// Использовать бенчмарк как эталон
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция ИспользоватьКакЭталон() Экспорт

	_ЭтоЭталон = Истина;

	Возврат ЭтотОбъект;

КонецФункции

// Категория бенчмарка
//
// Возвращаемое значение:
//   Строка
Функция Категория() Экспорт
	Возврат _Категория;
КонецФункции

// Устанавливает категорию бенчмарка
//
// Параметры:
//   Категория - Строка
//
// Возвращаемое значение:
//   ЭтотОбъект
Функция УстановитьКатегорию(Категория) Экспорт

	_Категория = Категория;

	Возврат ЭтотОбъект;

КонецФункции

// Имена параметров метода бенчмарка
//
// Возвращаемое значение:
//   Массив из Строка
Функция ИменаПараметров() Экспорт
	Возврат Новый Массив(Новый ФиксированныйМассив(_ИменаПараметров));
КонецФункции

// Делегат
//
// Параметры:
//   Параметры - Массив из Произвольный - Параметры вызываемого метода
//
// Возвращаемое значение:
//   Делегат
Функция Делегат(Параметры) Экспорт
	Возврат Делегаты.Создать(_Объект, _Метод, Параметры);
КонецФункции

Процедура ЗаполнитьИменаПараметров()

	РефлекторОбъекта = Новый РефлекторОбъекта(_Объект);
	ТаблицаМетодов = РефлекторОбъекта.ПолучитьТаблицуМетодов(, Ложь);

	СвойстваМетода = ТаблицаМетодов.Найти(Метод(), "Имя");

	_ИменаПараметров = СвойстваМетода.Параметры.ВыгрузитьКолонку("Имя");

КонецПроцедуры