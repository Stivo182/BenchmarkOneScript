Перем _Объект; // Произвольный - Объект бенчмарка
Перем _ДескрипторыБенчмарков; // Массив из Бенчмарк
Перем _Конфигурация; // КонфигурацияБенчмарков

// Обертка над объектом бенчмарка
//
// Параметры:
//   Источник - Тип, Произвольный - Тип или объект бенчмарка
Процедура ПриСозданииОбъекта(Источник)

	Если ТипЗнч(Источник) = Тип("Тип") Тогда
		_Объект = Новый(Источник);
	Иначе
		_Объект = Источник;
	КонецЕсли;

КонецПроцедуры

// Набор дескрипторов бенмарков из объекта
//
// Возвращаемое значение:
//   Массив из ДескрипторБенчмарка
Функция ДескрипторыБенчмарков() Экспорт 

	Если _ДескрипторыБенчмарков = Неопределено Тогда
		_ДескрипторыБенчмарков = ИзвлекательБенчамарков.Извлечь(_Объект);
	КонецЕсли;

	Возврат _ДескрипторыБенчмарков;

КонецФункции

// Получает конфигурацию описанную через аннотации в объекте
//
// Возвращаемое значение:
//   КонфигурацияБенчмарков
Функция Конфигурация() Экспорт

	Если _Конфигурация = Неопределено Тогда
		_Конфигурация = ИзвлекательКонфигурацииБенчмарков.Извлечь(_Объект);
	КонецЕсли;

	Возврат _Конфигурация;

КонецФункции

// Устанавливает значения публичным полям объекта
//
// Параметры:
//   Параметры - Массив из ПараметрБенчмарка
Процедура УстановитьПараметры(Параметры) Экспорт

	Для Каждого Параметр Из Параметры Цикл
		
		Если Не Параметр.ЭтоПараметрМетода Тогда
			_Объект[Параметр.Имя] = Параметр.Значение;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры