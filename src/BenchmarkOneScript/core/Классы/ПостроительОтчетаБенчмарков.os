#Использовать fluent

Перем _ДескрипторыБенчмарков; // Массив из ДескрипторБенчмарка
Перем _Конфигурация; // КонфигурацияБенчмарков
Перем _РезультатыЗапусков; // ТаблицаЗначений - см. ЗапускательБенчмарков.НоваяТаблицаРезультатовЗапусков
Перем _Отчет; // ОтчетБенчмарков
Перем _КолонкиКонфигурации; // Массив из Строка

// Построитель отчета по результатам запуска бенчмарков
//
// Параметры:
//   ДескрипторыБенчмарков - КоллекцияДескрипторовБенчмарков
//   Конфигурация - КонфигурацияБенчмарков
//   РезультатыЗапусков - ТаблицаЗначений - см. ЗапускательБенчмарков.НоваяТаблицаРезультатовЗапусков
Процедура ПриСозданииОбъекта(ДескрипторыБенчмарков, Конфигурация, РезультатыЗапусков)
	
	_ДескрипторыБенчмарков = ДескрипторыБенчмарков.ВМассив();
	_Конфигурация = Конфигурация;
	_РезультатыЗапусков = РезультатыЗапусков;
	_КолонкиКонфигурации = _Конфигурация.Колонки();

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Формирует отчет по результатам запуска бенчмарков
//
// Возвращаемое значение:
//   ОтчетБенчмарков
Функция Сформировать() Экспорт

	_Отчет = Новый ОтчетБенчмарков();
	
	ДобавитьКолонки();
	ЗаполнитьДанными();
	ОпределитьЧисловыеКолонки();
	ОпределитьЕдиницыИзмеренияПредставления();
	ОпределитьФорматныеСтроки();
	ОпределитьРазмерыКолонок();
	Сортировать();
	СкрытьКолонки();

	Возврат _Отчет;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьКолонки()
	
	ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьМетод());
	ДобавитьКолонкуКатегория();
	ДобавитьКолонкиПараметров();
	ДобавитьКолонкуЭталон();
	ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьСреднее());
	ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьСтандартнаяОшибка());
	ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьСтандартноеОтклонение());
	ДобавитьКолонкуКоэффициентПроизводительности();
	ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьRatioSD());
	ДобавитьКолонкуМин();
	ДобавитьКолонкуНижнийКвартиль();
	ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьМедиана());
	ДобавитьКолонкуВерхнийКвартиль();
	ДобавитьКолонкуМакс();
	ДобавитьКолонкиПроцентилей();
	ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьОперацийВСекунду());
	ДобавитьКолонкуВыделяемаяПамять();

КонецПроцедуры

Процедура ДобавитьКолонкиПараметров()

	Для Каждого Параметр Из _Конфигурация.Параметры() Цикл
		ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьПараметр(Параметр.Имя));
	КонецЦикла;

	Для Каждого ДескрипторБенчмарка Из _ДескрипторыБенчмарков Цикл
		Для Каждого Имя Из ДескрипторБенчмарка.ИменаПараметров() Цикл
			ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьПараметр(Имя));
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьКолонкуЭталон()

	Если ИспользуетсяЭталон() Тогда
		ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьЭталон());
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКолонкуКатегория()

	ЕстьКатегория = Ложь;

	Для Каждого ДескрипторБенчмарка Из _ДескрипторыБенчмарков Цикл
		Если ЗначениеЗаполнено(ДескрипторБенчмарка.Категория()) Тогда
			ЕстьКатегория = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ЕстьКатегория Тогда
		ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьКатегория());
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКолонкуКоэффициентПроизводительности()

	Если ИспользуетсяЭталон() Тогда
		ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьКоэффициентПроизводительности());
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКолонкуВыделяемаяПамять()

	Если _Конфигурация.ТребуетсяМониторингПамяти() Тогда
		ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьВыделяемаяПамять());
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКолонкуМин()

	Если ЕстьКолонкаКонфигурации(КолонкиОтчетаБенчмарков.Мин) Тогда
		ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьМин());
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьКолонкуМакс()
	
	Если ЕстьКолонкаКонфигурации(КолонкиОтчетаБенчмарков.Макс) Тогда
		ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьМакс());
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьКолонкуНижнийКвартиль()
	
	Если ЕстьКолонкаКонфигурации(КолонкиОтчетаБенчмарков.НижнийКвартиль) Тогда
		ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьНижнийКвартиль());
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьКолонкуВерхнийКвартиль()
	
	Если ЕстьКолонкаКонфигурации(КолонкиОтчетаБенчмарков.ВерхнийКвартиль) Тогда
		ДобавитьКолонку(КолонкиОтчетаБенчмарков.СоздатьВерхнийКвартиль());
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьКолонкиПроцентилей()

	Для Каждого Колонка Из КолонкаОтчетаБенчмарковПроцентиль.СоздатьПоИмени(_КолонкиКонфигурации) Цикл
		ДобавитьКолонку(Колонка);
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьКолонку(Колонка)

	Если Не _Отчет.Таблица.Колонки.Найти(Колонка.Имя) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	_Отчет.Таблица.Колонки.Добавить(Колонка.Имя);
	_Отчет.Колонки.Добавить(Колонка);

КонецПроцедуры

Процедура СкрытьКолонки()
	
	СкрываемыеКолонки = Новый Массив();

	Для Каждого Колонка Из _Отчет.Колонки Цикл
		Если ЕстьКолонкаКонфигурации(Колонка.Имя) Или Не ЗначениеЗаполнено(Колонка.ФункцияСкрытия) Тогда
			Продолжить;
		КонецЕсли;

		ПроцессорКоллекций = ПроцессорыКоллекций.ИзКоллекции(_Отчет.Таблица.ВыгрузитьКолонку(Колонка.Имя));

		Если Колонка.ЭтоЧисло Тогда
			ПроцессорКоллекций.Фильтровать("Элемент -> ТипЗнч(Элемент) = Тип(""Число"")");
		КонецЕсли;

		Скрывать = ПроцессорКоллекций.ВсеСоответствуют(Колонка.ФункцияСкрытия);

		Если Скрывать Тогда
			СкрываемыеКолонки.Добавить(Колонка);
		КонецЕсли;
	КонецЦикла;

	Для Каждого Колонка Из СкрываемыеКолонки Цикл
		_Отчет.Колонки.Удалить(_Отчет.Колонки.Найти(Колонка));
		_Отчет.Таблица.Колонки.Удалить(Колонка.Имя);
	КонецЦикла;

КонецПроцедуры

Функция ЕстьКолонкаКонфигурации(ИмяКолонки)
	Возврат Не _КолонкиКонфигурации.Найти(ИмяКолонки) = Неопределено;
КонецФункции

Процедура ЗаполнитьДанными()

	Для Каждого СтрокаРезультат Из _РезультатыЗапусков Цикл

		СтрокаОтчета = _Отчет.Таблица.Добавить();

		Для Каждого Колонка Из _Отчет.Колонки Цикл
			СтрокаОтчета[Колонка.Имя] = Колонка.Значение(СтрокаРезультат, Колонка);
		КонецЦикла;

	КонецЦИкла;

КонецПроцедуры

Процедура ОпределитьЧисловыеКолонки()

	Для Каждого Колонка Из _Отчет.Колонки Цикл

		Если Колонка.ЭтоЧисло Тогда
			Продолжить;
		КонецЕсли;

		ЗначенияКолонки = _Отчет.Таблица.ВыгрузитьКолонку(Колонка.Имя);
		Если МатематическиеФункцииБенчмарков.ТолькоЧисла(ЗначенияКолонки) Тогда
			Колонка.ЭтоЧисло = Истина;
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Процедура ОпределитьЕдиницыИзмеренияПредставления()

	Для Каждого Колонка Из _Отчет.Колонки Цикл
		
		Если Колонка.ЕдиницаИзмерения = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Минимальная = Неопределено;
		Для Каждого Строка Из _Отчет.Таблица Цикл
	
			Значение = Строка[Колонка.Имя];

			ЕдиницаИзмерения = ЕдиницыИзмеренийБенчмарков.Подобрать(Значение, Колонка.ЕдиницаИзмерения);

			Минимальная = ?(
				Минимальная = Неопределено, 
				ЕдиницаИзмерения, 
				ЕдиницыИзмеренийБенчмарков.Минимальная(Минимальная, ЕдиницаИзмерения)
			);
		КонецЦикла;

		Колонка.ЕдиницаИзмеренияПредставления = ?(Минимальная = Неопределено, Колонка.ЕдиницаИзмерения, Минимальная);

	КонецЦикла;	
	
КонецПроцедуры

Процедура ОпределитьФорматныеСтроки()

	Для Каждого Колонка Из _Отчет.Колонки Цикл
		
		Если Не Колонка.ЭтоЧисло Или ЗначениеЗаполнено(Колонка.ФорматнаяСтрока) Тогда
			Продолжить;
		КонецЕсли;

		МинЗначение = Неопределено;
		Для Каждого Строка Из _Отчет.Таблица Цикл
			Значение = Строка[Колонка.Имя];
			Если ТипЗнч(Значение) = Тип("Число") Тогда
				МинЗначение = ?(МинЗначение = Неопределено, Значение, Мин(МинЗначение, Значение));
			КонецЕсли;
		КонецЦикла;

		Если МинЗначение = Неопределено Тогда
			Возврат;
		КонецЕсли;

		Если Не Колонка.ЕдиницаИзмерения = Неопределено И Не Колонка.ЕдиницаИзмеренияПредставления = Неопределено Тогда
			МинЗначение = ЕдиницыИзмеренийБенчмарков.Конвертировать(
				МинЗначение,
				Колонка.ЕдиницаИзмерения,
				Колонка.ЕдиницаИзмеренияПредставления
			);
		КонецЕсли;

		Разрядность = ПредставленияПоказателейБенчмарков.ПодобратьРазрядностьДробнойЧасти(
			МинЗначение,
			Колонка.ЕдиницаИзмерения);

		Колонка.ФорматнаяСтрока = ПредставленияПоказателейБенчмарков.ФорматнаяСтрокаЧисла(Разрядность);

	КонецЦикла;	
	
КонецПроцедуры

Процедура ОпределитьРазмерыКолонок()

	Для Каждого Колонка Из _Отчет.Колонки Цикл
		Колонка.Размер = СтрДлина(Колонка.Заголовок);
	КонецЦикла;

	Для Каждого Строка Из _Отчет.Таблица Цикл
		Для Каждого Колонка Из _Отчет.Колонки Цикл
			Значение = Строка[Колонка.Имя];
			Представление = СтрДлина(Колонка.ПредставлениеЗначения(Значение, Строка));
			Колонка.Размер = Макс(Колонка.Размер, Представление);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура Сортировать()
	
	Сортировка = _Конфигурация.СортировкаОтчета();

	КолонкиСортировки = Новый Массив();

	Если Сортировка = СортировкиОтчетаБенчмарков.ОтБыстрыхКМедленным Тогда

		ДобавитьСортировку(КолонкиСортировки, КолонкиОтчетаБенчмарков.Среднее, "Возр");

	ИначеЕсли Сортировка = СортировкиОтчетаБенчмарков.ОтМедленныхКБыстрым Тогда

		ДобавитьСортировку(КолонкиСортировки, КолонкиОтчетаБенчмарков.Среднее, "Убыв");

	ИначеЕсли Сортировка = СортировкиОтчетаБенчмарков.Метод Тогда

		ДобавитьСортировку(КолонкиСортировки, КолонкиОтчетаБенчмарков.Метод);

	Иначе

		ДобавитьСортировку(КолонкиСортировки, КолонкиОтчетаБенчмарков.Категория);
		ДобавитьСортировкуПоПараметрам(КолонкиСортировки);
		ДобавитьСортировку(КолонкиСортировки, КолонкиОтчетаБенчмарков.Метод);

	КонецЕсли;

	_Отчет.Таблица.Сортировать(СтрСоединить(КолонкиСортировки, ", "));

КонецПроцедуры

Процедура ДобавитьСортировкуПоПараметрам(Сортировка)
	
	Для Каждого ИмяПараметра Из _Конфигурация.ИменаПараметров() Цикл
		ДобавитьСортировку(Сортировка, КолонкиОтчетаБенчмарков.Параметр(ИмяПараметра));
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьСортировку(Сортировка, ИмяКолонки, Направление = "")
	
	Если Не _Отчет.Таблица.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
		Сортировка.Добавить(ИмяКолонки + " " + Направление);
	КонецЕсли;

КонецПроцедуры

Функция ИспользуетсяЭталон()
	
	Для Каждого ДескрипторБенчмарка Из _ДескрипторыБенчмарков Цикл
		Если ДескрипторБенчмарка.ЭтоЭталон() Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции

#КонецОбласти