#Использовать logos
#Использовать "../../dto"
#Использовать "../../custom-jason"

Перем _Лог; // Лог

// Сериализатор настроек бенчмарков
Процедура ПриСозданииОбъекта()
	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.СериализаторНастроекБенчмарков");
КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Сериализует настройки бенчмарков в JSON-файл
//
// Параметры:
//   Дескрипторы  - КоллекцияДескрипторовБенчмарков - Коллекция дескрипторов бенчмарков
//   Конфигурация - КонфигурацияБенчмарков - Конфигурация бенчмарков
//   ИмяФайла     - Строка - Имя файла, в который будет записана конфигурация
Процедура ЗаписатьВJson(Дескрипторы, Конфигурация, ИмяФайла) Экспорт

	_Лог.Отладка("Начало формирования файла: %1", ИмяФайла);

	Сериализатор = Новый СериализаторJsonBench();

	Настройки = Новый НастройкиБенчмарковДто();
	Настройки.ДескрипторыБенчмарков = Дескрипторы;
	Настройки.Конфигурация = Конфигурация;

	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла, "UTF-8");
	ЗаписьТекста.Записать(Сериализатор.Сериализовать(Настройки));
	ЗаписьТекста.Закрыть();

	_Лог.Отладка("Завершение формирования файла: %1", ИмяФайла);

КонецПроцедуры

// Десериализует настройки бенчмарков из JSON-файла
//
// Параметры:
//   ИмяФайла - Строка - Имя файла, из которого будет прочитана конфигурация.
//
// Возвращаемое значение:
//   НастройкиБенчмарковДто
Функция ПрочитатьИзJson(ИмяФайла) Экспорт
		
	_Лог.Отладка("Начало чтения файла: %1", ИмяФайла);

	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, "UTF-8");
	Строка = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	Десериализатор = Новый ДесериализаторJsonBench();
	Настройки = Десериализатор.Десериализовать(Строка, Тип("НастройкиБенчмарковДто"));
		
	_Лог.Отладка("Завершение чтения файла: %1", ИмяФайла);

	Возврат Настройки;

КонецФункции

#КонецОбласти