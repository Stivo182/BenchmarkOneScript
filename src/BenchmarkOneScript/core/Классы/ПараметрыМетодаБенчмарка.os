#Использовать asserts

&Несериализуемое
Перем _Параметры; // Массив из ПараметрБенчмарка

Процедура ПриСозданииОбъекта()
	_Параметры = Новый Массив();
КонецПроцедуры

// Добавляет параметр бенчмарка в коллекцию
//
// Параметры:
//   Параметр - ПараметрБенчмарка
Процедура Добавить(Параметр) Экспорт

	Ожидаем.Что(Параметр).ИмеетТип("ПараметрБенчмарка");
	Ожидаем.Что(Параметр.ЭтоПараметрМетода()).ЭтоИстина();

	_Параметры.Добавить(Параметр);

КонецПроцедуры

// Возвращает количество параметров
//
// Возвращаемое значение:
//   Число
Функция Количество() Экспорт
	Возврат _Параметры.Количество();
КонецФункции

// Возвращает параметр метода по индексу
//
// Параметры:
//   Индекс - Число
//
// Возвращаемое значение:
//   ПараметрБенчмарка
Функция Получить(Индекс) Экспорт
	Возврат _Параметры[Индекс];
КонецФункции

// Возвращает массив параметров
//
// Возвращаемое значение:
//   Массив из ПараметрБенчмарка
Функция ВМассив() Экспорт
	Возврат Новый Массив(Новый ФиксированныйМассив(_Параметры));
КонецФункции

#Область Сериализация

&ПередСериализацией
Процедура ПередСериализацией(Сериализатор, Результат, СтандартнаяОбработка) Экспорт

	СтандартнаяОбработка = Ложь;

	Результат = Сериализатор.ПреобразоватьОбъектСериализации(_Параметры);

КонецПроцедуры

&ПослеДесериализации
Процедура ПослеДесериализации(Десериализатор, ПрочитанныеСвойства) Экспорт
	
	Если ТипЗнч(ПрочитанныеСвойства) = Тип("Массив") Тогда
		_Параметры = Десериализатор.ПреобразоватьОбъектДесериализации(
				ПрочитанныеСвойства,
				Тип("Массив"),
				Тип("ПараметрБенчмарка"));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти