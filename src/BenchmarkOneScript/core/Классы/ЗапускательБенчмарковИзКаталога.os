#Использовать logos

Перем _Лог; // Лог
Перем _Каталог; // Строка
Перем _ИскатьВПодкаталогах; // Булево
Перем _Конфигурация; // КонфигурацияБенчмарков

// Запускает все бенчмарки, найденные в указанном каталоге
//
// Параметры:
//   Каталог - Строка - Путь к каталогу с бенчмарками
//   ИскатьВПодкаталогах - Булево - Сканировать все вложенные каталоги
//   Конфигурация - КонфигурацияБенчмарков - Конфигурация, которая с приоритетом будет объединена
//                                           с каждой конфигурацией бенчмарка
Процедура ПриСозданииОбъекта(Каталог, ИскатьВПодкаталогах = Ложь, Конфигурация = Неопределено)
	_Каталог = Каталог;
	_ИскатьВПодкаталогах = ИскатьВПодкаталогах;
	_Конфигурация = Конфигурация;
	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.ЗапускательБенчмарковИзКаталога");
КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Запускает все бенчмарки, найденные в указанном каталоге
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Результат - см. Бенчмаркинг.Запустить, Неопределено
//     * ИмяФайла - Строка - Имя файла
//     * ПолноеИмяФайла - Строка - Путь к файлу сценария
//     * Успешно - Булево - Индикатор корректного выполнения
Функция Запустить() Экспорт

	Результат = ТаблицаРезультата();

	_Лог.Отладка("Запуск бенчмарков из каталога <%1>", _Каталог);

	Файлы = НайтиФайлы(_Каталог, "*.os", _ИскатьВПодкаталогах);
	Если Файлы.Количество() = 0 Тогда
		_Лог.Предупреждение("Отсутствуют файлы скриптов (.os) в каталоге <%1>", _Каталог);
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого Файл Из Файлы Цикл
		
		Тип = Бенчмаркинг.ПодключитьКласс(Файл.ПолноеИмя);
		Если Тип = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		СтрокаРезультата = Результат.Добавить();
		СтрокаРезультата.ПолноеИмяФайла = Файл.ПолноеИмя;
		СтрокаРезультата.ИмяФайла = Файл.ИмяБезРасширения;
		СтрокаРезультата.Результат = Неопределено;
		СтрокаРезультата.Успешно = Истина;

		Конфигурация = Новый КонфигурацияБенчмарков(Тип);
		Если Не _Конфигурация = Неопределено Тогда
			Конфигурация.Объединить(_Конфигурация);
		КонецЕсли;

		Попытка
			СтрокаРезультата.Результат = Бенчмаркинг.Запустить(Тип, Конфигурация);
		Исключение
			СтрокаРезультата.Успешно = Ложь;
			_Лог.Ошибка("Возникла ошибка при запуске бенчмарков <%1>: %2", 
				СтрокаРезультата.ИмяФайла,
				ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТаблицаРезультата()

	ТипСтрока = Новый ОписаниеТипов("Строка");

	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ПолноеИмяФайла", ТипСтрока);
	Таблица.Колонки.Добавить("ИмяФайла", ТипСтрока);
	Таблица.Колонки.Добавить("Результат");
	Таблица.Колонки.Добавить("Успешно", Новый ОписаниеТипов("Булево"));

	Возврат Таблица;
	
КонецФункции

#КонецОбласти