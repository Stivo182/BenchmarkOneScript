#Использовать logos

Перем _Лог; // Лог
Перем _Каталог; // Строка
Перем _ИскатьВПодкаталогах; // Булево

// Запускает бенчмарки из каталога
//
// Параметры:
//   Каталог - Строка - Путь к каталогу
//   ИскатьВПодкаталогах - Булево - Искать в подкаталогах
Процедура ПриСозданииОбъекта(Каталог, ИскатьВПодкаталогах = Ложь)
	_Каталог = Каталог;
	_ИскатьВПодкаталогах = ИскатьВПодкаталогах;
	_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.ЗапускательБенчмарковИзКаталога");
КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Запускает бенчмарки из каталога
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Результат - см. Бенчмаркинг.Запустить, Неопределено
//     * ИмяКласса - Строка - Имя класса
//     * ИмяФайла - Строка - Имя файла
//     * ПутьКФайлу - Строка - Путь к файлу сценария
//     * Успешно - Булево - Запуск бенчмарков выполнен успешно
Функция Запустить() Экспорт

	Результат = ТаблицаРезультата();

	_Лог.Отладка("Запуск бенчмарков из каталога <%1>", _Каталог);

	Файлы = НайтиФайлы(_Каталог, "*.os", _ИскатьВПодкаталогах);
	Если Файлы.Количество() = 0 Тогда
		_Лог.Предупреждение("Отсутствуют файлы скриптов (.os) в каталоге <%1>", _Каталог);
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого Файл Из Файлы Цикл
		
		Объект = Бенчмаркинг.ПодключитьКласс(Файл.ПолноеИмя);
		Если Объект = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		СтрокаРезультата = Результат.Добавить();
		СтрокаРезультата.ИмяКласса = Строка(Тип(Объект));
		СтрокаРезультата.ПутьКФайлу = Файл.ПолноеИмя;
		СтрокаРезультата.ИмяФайла = Файл.ИмяБезРасширения;
		СтрокаРезультата.Результат = Неопределено;
		СтрокаРезультата.Успешно = Истина;

		Попытка
			СтрокаРезультата.Результат = Бенчмаркинг.Запустить(Объект);
		Исключение
			СтрокаРезультата.Успешно = Ложь;
			_Лог.Ошибка("Возникла ошибка при запуске бенчмарков класса <%1>: %2", 
				СтрокаРезультата.ИмяКласса,
				ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТаблицаРезультата()

	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ИмяКласса");
	Таблица.Колонки.Добавить("ПутьКФайлу");
	Таблица.Колонки.Добавить("ИмяФайла");
	Таблица.Колонки.Добавить("Результат");
	Таблица.Колонки.Добавить("Успешно");

	Возврат Таблица;
	
КонецФункции

#КонецОбласти