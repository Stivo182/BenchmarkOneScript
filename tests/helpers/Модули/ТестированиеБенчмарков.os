#Использовать "../../.."
#Использовать fs
#Использовать 1commands

Перем _Лог; // Лог

Функция КонфигурацияПоУмолчанию() Экспорт

	Конфигурация = Новый КонфигурацияБенчмарков();
	НастроитьКонфигурациюПодТесты(Конфигурация);

	Возврат Конфигурация;

КонецФункции

Процедура НастроитьКонфигурациюПодТесты(Конфигурация) Экспорт

	МинимальноеВремяИтерации = 10;

	Конфигурация
		.УстановитьКоличествоВызововЗаИтерацию(1)
		.УстановитьКоличествоПрогревочныхИтераций(1)
		.УстановитьКоличествоИтераций(1)
		.УстановитьМинимальноеВремяИтерации(МинимальноеВремяИтерации);

КонецПроцедуры

Процедура ПодготовитьСреду(ВерсииИсполняющейСреды) Экспорт

	ПроверитьУстановитьВерсииИсполняющейСреды(ВерсииИсполняющейСреды);

КонецПроцедуры

Процедура ПроверитьУстановитьВерсииИсполняющейСреды(Версии)
	
	ЛокаторИсполняющейСреды = Новый ЛокаторИсполняющейСреды();

	КаталогУстановки = ЛокаторИсполняющейСреды.ОпределитьКаталогУстановки();
	Если КаталогУстановки = Неопределено Тогда
		ВызватьИсключение "Не найден каталог с установленными версиями OneScript.
		|Убедитесь, что OVM (OneScript Version Manager) установлен и переменная OVM_INSTALL_PATH настроена корректно.";
	КонецЕсли;

	_Лог.Отладка(СтрШаблон("Каталог с установленными версиями OneScript: %1", КаталогУстановки));
	_Лог.Отладка(СтрШаблон("Проверка установленных версий: %1", Версии));

	Для Каждого Версия Из СтрРазделить(Версии, ", ") Цикл
		
		ИмяФайла = ЛокаторИсполняющейСреды.НайтиИсполняемыйФайл(Версия);
		Если Не ИмяФайла = Неопределено Тогда
			_Лог.Отладка(СтрШаблон("Обнаружена исполняющая среда OneScript версии %1: %2", Версия, ИмяФайла));
			Продолжить;
		КонецЕсли;

		Команда = Новый Команда();
		Команда.УстановитьКоманду("ovm");
		Команда.ДобавитьПараметр("install");
		Команда.ДобавитьПараметр(Версия);

		КодВозврата = Команда.Исполнить();
		Если КодВозврата <> 0 Тогда
			ТекстОшибки = СтрШаблон("Не удалось установить OneScript версии %1 через OVM (OneScript Version Manager)", Версия);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;

		ИмяФайла = ЛокаторИсполняющейСреды.НайтиИсполняемыйФайл(Версия);
		Если ИмяФайла = Неопределено Тогда
			ВызватьИсключение СтрШаблон("После установки OneScript версии %1 не удалось найти исполняемый файл", Версия);
		КонецЕсли;

		_Лог.Отладка(СтрШаблон("Установлена исполняющая среда OneScript версии %1: %2", Версия, ИмяФайла));

	КонецЦикла;

КонецПроцедуры

Функция ЭтоOneScript1() Экспорт
	Возврат Лев(Новый СистемнаяИнформация().Версия, 2) = "1.";
КонецФункции

_Лог = Логирование.ПолучитьЛог("oscript.lib.benchmark.ТестированиеБенчмарков");