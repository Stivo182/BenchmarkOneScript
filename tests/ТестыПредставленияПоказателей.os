// BSLLS:DuplicateStringLiteral-off
// BSLLS:MagicNumber-off

#Использовать asserts
#Использовать "helpers"

&Тест
Процедура Тест_ПредставлениеБайт() Экспорт

	Кратность = 1024;

	ТестовыйНабор = Новый Соответствие();
	ТестовыйНабор[0] = "0 B";
	ТестовыйНабор[1] = "1 B";
	ТестовыйНабор[512] = "512 B";
	ТестовыйНабор[Кратность] = "1 KB";
	ТестовыйНабор[Pow(Кратность, 2) / 2] = "512 KB";
	ТестовыйНабор[Pow(Кратность, 2)] = "1 MB";
	ТестовыйНабор[Pow(Кратность, 3) / 2] = "512 MB";
	ТестовыйНабор[Pow(Кратность, 3)] = "1 GB";
	ТестовыйНабор[Pow(Кратность, 4)] = "1,024 GB";

	Для Каждого Строка Из ТестовыйНабор Цикл

		Байт = Строка.Ключ;
		Представление = Строка.Значение;

		Результат = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
			Байт,
			ЕдиницыИзмеренийБенчмарков.Байт
		);

		Ожидаем.Что(Результат).Равно(Представление);

	КонецЦикла;
	
КонецПроцедуры

&Тест
Процедура Тест_ПредставлениеСекунд() Экспорт

	ТестовыйНабор = Новый Соответствие();
	ТестовыйНабор[0] = "0.0 ns";
	ТестовыйНабор[10] = "10.0 ns";
	ТестовыйНабор[100] = "100.0 ns";
	ТестовыйНабор[1000] = "1.0 us";
	ТестовыйНабор[10000] = "10.0 us";
	ТестовыйНабор[100000] = "100.0 us";
	ТестовыйНабор[1000000] = "1.0 ms";
	ТестовыйНабор[10000000] = "10.0 ms";
	ТестовыйНабор[100000000] = "100.0 ms";
	ТестовыйНабор[1000000000] = "1.0 s";
	ТестовыйНабор[10000000000] = "10.0 s";
	ТестовыйНабор[60000000000] = "60.0 s";
	ТестовыйНабор[600000000000] = "600.0 s";

	Для Каждого Строка Из ТестовыйНабор Цикл

		Наносекунд = Строка.Ключ;
		Представление = Строка.Значение;

		Результат = ПредставленияПоказателейБенчмарков.ПредставлениеЧитаемое(
			Наносекунд,
			ЕдиницыИзмеренийБенчмарков.Наносекунда
		);

		Ожидаем.Что(Результат).Равно(Представление);

	КонецЦикла;
	
КонецПроцедуры

&Тест
Процедура Тест_ПредставлениеЧислаСекунды() Экспорт

	ЕдиницаИзмерения = ЕдиницыИзмеренийБенчмарков.Наносекунда;

	ТестовыйНабор = Новый Соответствие();
	ТестовыйНабор[0] = "0.0";
	ТестовыйНабор[1] = "1.0";

	ТестовыйНабор[0.1] = "0.1";
	ТестовыйНабор[0.12] = "0.12";
	ТестовыйНабор[0.123] = "0.123";
	ТестовыйНабор[0.1234] = "0.123";

	ТестовыйНабор[0.01] = "0.01";
	ТестовыйНабор[0.012] = "0.012";
	ТестовыйНабор[0.0123] = "0.0123";
	ТестовыйНабор[0.01234] = "0.0123";

	ТестовыйНабор[0.001] = "0.001";
	ТестовыйНабор[0.0012] = "0.0012";
	ТестовыйНабор[0.00123] = "0.0012";

	ТестовыйНабор[1.1] = "1.1";
	ТестовыйНабор[1.12] = "1.12";
	ТестовыйНабор[1.123] = "1.123";
	ТестовыйНабор[1.1234] = "1.123";

	ТестовыйНабор[10.1] = "10.1";
	ТестовыйНабор[10.12] = "10.12";
	ТестовыйНабор[10.123] = "10.12";

	ТестовыйНабор[100.1] = "100.1";
	ТестовыйНабор[100.12] = "100.1";

	ТестовыйНабор[1000.1] = "1,000.1";
	ТестовыйНабор[1000.12] = "1,000.1";

	Для Каждого Строка Из ТестовыйНабор Цикл

		Число = Строка.Ключ;
		Представление = Строка.Значение;

		Результат = ПредставленияПоказателейБенчмарков.ПредставлениеЧисла(Число, ЕдиницаИзмерения);
		РезультатСМинусом = ПредставленияПоказателейБенчмарков.ПредставлениеЧисла(-Число, ЕдиницаИзмерения);

		Ожидаем.Что(Результат).Равно(Представление);

		Если Число <> 0 Тогда
			Ожидаем.Что(РезультатСМинусом).Равно("-" + Представление);	
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры