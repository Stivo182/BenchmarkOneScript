// BSLLS:LineLength-off
// BSLLS:DuplicateStringLiteral-off
// BSLLS:MagicNumber-off

#Использовать asserts
#Использовать delegate
#Использовать "helpers"

&Тест
Процедура ТестДолжен_УстановитьИсточникПараметровДляДескриптора() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркБезПараметров");

	// Действие
	Дескриптор.ДобавитьИсточникПараметров("Получить");

	// Проверка
	ИсточникиПараметров = Дескриптор.ИсточникиПараметров();

	Ожидаем.Что(ИсточникиПараметров).ИмеетДлину(1);
	Ожидаем.Что(ИсточникиПараметров[0]).Равно("Получить");
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ОчиститьИсточникПараметровУДескриптора() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	
	Дескриптор = ПарметрыЗапуска.Дескрипторы
		.НайтиПоИмени("БенчмаркБезПараметров")
		.ДобавитьИсточникПараметров("Получить");

	// Действие
	Дескриптор.ОчиститьИсточникиПараметров();

	// Проверка
	ИсточникиПараметров = Дескриптор.ИсточникиПараметров();

	Ожидаем.Что(ИсточникиПараметров).ИмеетДлину(0);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_УстановитьИсточникПараметровДляПоляКонфигурации() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	
	// Действие
	Конфигурация.ДобавитьИсточникПараметров("Поле", "Получить");

	// Проверка
	ИсточникиПараметров = Конфигурация.ИсточникиПараметров();

	Ожидаем.Что(ИсточникиПараметров).ИмеетДлину(1);
	Ожидаем.Что(ИсточникиПараметров[0].ИмяПоля).Равно("Поле");
	Ожидаем.Что(ИсточникиПараметров[0].Источник).Равно("Получить");
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ОчиститьИсточникиПараметровУКонфигурации() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();

	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Конфигурация.ДобавитьИсточникПараметров("Поле", "Получить");

	// Действие
	Конфигурация.ОчиститьИсточникиПараметров();

	// Проверка
	ИсточникиПараметров = Конфигурация.ИсточникиПараметров();

	Ожидаем.Что(ИсточникиПараметров).ИмеетДлину(0);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровДляОбщегоПоля() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска(Ложь);
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркБезПараметров");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(2);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Поле")]).Равно(100);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Поле")]).Равно(200);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСНесколькимиИсточникамиПараметровДляОбщегоПоля() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска(Ложь);
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркБезПараметров");

	Конфигурация.ДобавитьИсточникПараметров("Поле", "ПолучитьЗначенияПараметровИзБулево");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	ИмяКолонки = КолонкиОтчетаБенчмарков.Параметр("Поле");
	Значения = Результат.Отчет.Таблица.ВыгрузитьКолонку(ИмяКолонки);

	Ожидаем.Что(Значения).ИмеетДлину(4);
	Ожидаем.Что(Значения).Содержит(Ложь);
	Ожидаем.Что(Значения).Содержит(Истина);
	Ожидаем.Что(Значения).Содержит(100);
	Ожидаем.Что(Значения).Содержит(200);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровДляОбщегоПоляИзСтруктуры() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркБезПараметров");

	Конфигурация.ДобавитьИсточникПараметров("Поле", "ПолучитьЗначенияПараметровДляБенчмаркаИзСтруктуры");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(1);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Поле")]).ИмеетТип("Структура");
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Поле")].Парам1).Равно(13);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Поле")].Парам2).Равно(14);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровМетодаБенчмаркаСОднимПараметром() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркОдинПараметр");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(2);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам")]).Равно(Ложь);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам")]).Равно(Истина);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровМетодаБенчмаркаИзКоллекцииМассивов() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркДваПараметра");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(2);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(13);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(14);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(15);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(16);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровМетодаБенчмаркаИзКоллекцииСтруктур() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы
		.НайтиПоИмени("БенчмаркДваПараметра")
		.ОчиститьИсточникиПараметров()
		.ДобавитьИсточникПараметров("ПолучитьЗначенияПараметровДляБенчмаркаИзКоллекцииСтруктур");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(2);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(1);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(2);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(3);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(4);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровМетодаБенчмаркаИзКоллекцииСоответствий() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркДваПараметра")
		.ОчиститьИсточникиПараметров()
		.ДобавитьИсточникПараметров("ПолучитьЗначенияПараметровДляБенчмаркаИзКоллекцииСоответствий");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(2);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(5);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(6);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(7);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(8);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровМетодаБенчмаркаИзТаблицы() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркДваПараметра")
		.ОчиститьИсточникиПараметров()
		.ДобавитьИсточникПараметров("ПолучитьЗначенияПараметровДляБенчмаркаИзТаблицыЗначений");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(2);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(9);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(10);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(11);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(12);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСНесколькимиИсточникамиПараметровМетодаБенчмарка() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркДваПараметра")
		.ОчиститьИсточникиПараметров()
		.ДобавитьИсточникПараметров("ПолучитьЗначенияПараметровДляБенчмаркаИзКоллекцииСтруктур")
		.ДобавитьИсточникПараметров("ПолучитьЗначенияПараметровДляБенчмаркаИзКоллекцииСоответствий");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(4);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(1);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(2);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(3);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(4);
	Ожидаем.Что(Результат.Отчет.Таблица[2][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(5);
	Ожидаем.Что(Результат.Отчет.Таблица[2][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(6);
	Ожидаем.Что(Результат.Отчет.Таблица[3][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(7);
	Ожидаем.Что(Результат.Отчет.Таблица[3][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(8);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровМетодаБенчмаркаИзСтруктуры() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркДваПараметра")
		.ОчиститьИсточникиПараметров()
		.ДобавитьИсточникПараметров("ПолучитьЗначенияПараметровДляБенчмаркаИзСтруктуры");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(1);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам1")]).Равно(13);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам2")]).Равно(14);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровИзМодуля() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркСОднимПараметромИИсточникомИзМодуля");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(2);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам")]).Равно(300);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам")]).Равно(400);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСИсточникомПараметровИзКласса() Экспорт
	
	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы.НайтиПоИмени("БенчмаркСОднимПараметромИИсточникомИзКласса");

	// Действие
	Результат = Бенчмаркинг.Запустить(Дескриптор, Конфигурация);

	// Проверка
	Ожидаем.Что(Результат.Отчет.Таблица).ИмеетДлину(2);
	Ожидаем.Что(Результат.Отчет.Таблица[0][КолонкиОтчетаБенчмарков.Параметр("Парам")]).Равно(500);
	Ожидаем.Что(Результат.Отчет.Таблица[1][КолонкиОтчетаБенчмарков.Параметр("Парам")]).Равно(600);
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ВыброситьИсключениеСНевернымТипомЭлементаКоллекцииИсточникаПараметров() Экспорт

	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы
		.НайтиПоИмени("БенчмаркДваПараметра")
		.ДобавитьИсточникПараметров("ПолучитьНевалидныеЗначенияПараметровДляМетодаБенчмаркаСДвумяПараметрами");

	ПараметрыМетода = Новый Массив();
	ПараметрыМетода.Добавить(Дескриптор);
	ПараметрыМетода.Добавить(Конфигурация);

	// Действие и Проверка
	Ожидаем
		.Что(Бенчмаркинг)
		.Метод("Запустить", ПараметрыМетода)
		.ВыбрасываетИсключение("Не поддерживается тип <Строка> для передачи параметров в метод бенчмарка <БенчмаркДваПараметра>");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ВыброситьИсключениеСНевернымКоличествомПараметров() Экспорт

	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы
		.НайтиПоИмени("БенчмаркДваПараметра")
		.ДобавитьИсточникПараметров("ПолучитьЗначенияПараметровДляБенчмаркаИзКоллекцииМассивовСНевернымКоличеством");

	ПараметрыМетода = Новый Массив();
	ПараметрыМетода.Добавить(Дескриптор);
	ПараметрыМетода.Добавить(Конфигурация);

	// Действие и Проверка
	Ожидаем
		.Что(Бенчмаркинг)
		.Метод("Запустить", ПараметрыМетода)
		.ВыбрасываетИсключение("Количество переданных параметров (1) не соответствует ожидаемому количеству (2) для метода бенчмарка <БенчмаркДваПараметра>.");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ВыброситьИсключениеСОтсутствующимПараметромВСтруктуреИсточникаПараметров() Экспорт

	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы
		.НайтиПоИмени("БенчмаркДваПараметра")
		.ДобавитьИсточникПараметров("ПолучитьЗначенияПараметровДляБенчмаркаИзКоллекцииСтруктурБезОдногоПараметра");

	ПараметрыМетода = Новый Массив();
	ПараметрыМетода.Добавить(Дескриптор);
	ПараметрыМетода.Добавить(Конфигурация);

	// Действие и Проверка
	Ожидаем
		.Что(Бенчмаркинг)
		.Метод("Запустить", ПараметрыМетода)
		.ВыбрасываетИсключение("Отсутствует параметр <Парам2> метода бенчмарка <БенчмаркДваПараметра> в переданной коллекции <Структура>");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ВыброситьИсключениеСОтсутствующимПараметромВТаблицеИсточникаПараметров() Экспорт

	// Подготовка
	ПарметрыЗапуска = ПолучитьПараметрыЗапуска();
	Конфигурация = ПарметрыЗапуска.Конфигурация;
	Дескриптор = ПарметрыЗапуска.Дескрипторы
		.НайтиПоИмени("БенчмаркДваПараметра")
		.ДобавитьИсточникПараметров("ПолучитьЗначенияПараметровДляБенчмаркаИзТаблицыЗначенийБезОдногоПараметра");

	ПараметрыМетода = Новый Массив();
	ПараметрыМетода.Добавить(Дескриптор);
	ПараметрыМетода.Добавить(Конфигурация);

	// Действие и Проверка
	Ожидаем
		.Что(Бенчмаркинг)
		.Метод("Запустить", ПараметрыМетода)
		.ВыбрасываетИсключение("Отсутствует параметр <Парам2> метода бенчмарка <БенчмаркДваПараметра> в переданной таблице");

КонецПроцедуры

Функция ПолучитьВалидныеЗначенияПараметровДляМетодаБенчмаркаСОднимПараметром() Экспорт
		
	Значения = Новый Массив();
	Значения.Добавить("Тест");
	Значения.Добавить(100);

	Возврат Значения;

КонецФункции

Функция ПолучитьПараметрыЗапуска(ОчищатьИсточникиПолей = Истина)

	Тип = Тип("БенчмаркСИсточникамиПараметров");

	Конфигурация = Новый КонфигурацияБенчмарков(Тип);
	ТестированиеБенчмарков.НастроитьКонфигурациюПодТесты(Конфигурация);

	Если ОчищатьИсточникиПолей Тогда
		Конфигурация.ОчиститьИсточникиПараметров();
	КонецЕсли;

	ДескрипторыБенчмарков = Новый КоллекцияДескрипторовБенчмарков(Тип);

	Возврат Новый Структура("Конфигурация, Дескрипторы", Конфигурация, ДескрипторыБенчмарков);

КонецФункции