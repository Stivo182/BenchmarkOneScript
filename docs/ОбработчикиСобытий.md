# Обработчики событий

Для изоляции логики подготовки и завершения от измеряемого кода реализован механизм обработчиков событий.
Используйте специальные аннотации, чтобы определить методы, которые будут выполняться в разных фазах жизненного цикла бенчмарков.

## Конфигурирование 

### Аннотации

#### Перед всеми
Вызывается **перед запуском всех** бенчмарков.
Используется для подготовки данных, инициализации ресурсов или настройки окружения перед выполнением бенчмарков.

```bsl
// Параметры:
//   * Контекст - Структура:
//     ** Конфигурация - КонфигурацияБенчмарков
//     ** ДескрипторыБенчмарков - КоллекцияДескрипторовБенчмарков
&ПередВсеми
Процедура ПередВсеми(Контекст) Экспорт
```

#### После всех
Вызывается **после запуска всех** бенчмарков.
Используется для финализации: освобождения ресурсов (закрытие соединений), записи итоговых отчетов или выполнения других действий.

```bsl
// Параметры:
//   * Контекст - Структура:
//     ** Конфигурация - КонфигурацияБенчмарков
//     ** ДескрипторыБенчмарков - КоллекцияДескрипторовБенчмарков
//     ** Запуски - Массив из РезультатЗапускаБенчмаркаДто
//     ** Отчет - ОтчетБенчмарков
//     ** СредаОкружения - СредаОкруженияБенчмарков
&ПослеВсех
Процедура ПослеВсех(Контекст) Экспорт
```

#### Перед каждым
Вызывается **перед каждым запуском бенчмарка**. 
Позволяет подготовить контекст для отдельного теста - например, сбросить состояние объекта, сгенерировать тестовые данные или выполнить предварительные проверки.

```bsl
// Параметры:
//   * Контекст - Структура:
//     ** ДескрипторБенчмарка - ДескрипторБенчмарка
//     ** ПараметрыМетода - Массив из Произвольный
&ПередКаждым
Процедура ПередКаждым(Контекст) Экспорт
```

#### После каждого
Вызывается **после каждого выполнения бенчмарка**. 
Он используется для постобработки: валидации результатов, сброса временных значений или освобождения ресурсов, которые создавались для конкретного теста.

```bsl
// Параметры:
//   * Контекст - Структура:
//     ** ДескрипторБенчмарка - ДескрипторБенчмарка
//     ** Параметры - Массив из ПараметрБенчмарка
//     ** Замеры - Массив из РезультатИтерацииБенчмаркаДто
//     ** Статистика - СтатистикаБенчмарка
&ПослеКаждого
Процедура ПослеКаждого(Контекст) Экспорт
```

### API

```bsl
// Регистрация обработчика из произвольного объекта через делегат
Конфигурация.ДобавитьОбработчикСобытия(
	СобытияБенчмарков.ПередВсеми,
	Новый Делегат(Объект, "Инициализация")
);

// Регистрация обработчика из экземпляра класса бенчмарка
Конфигурация.ДобавитьОбработчикСобытия
	СобытияБенчмарков.ПослеВсех,
	"Завершение"
);

// Удаление обработчиков для события
Конфигурация.УдалитьОбработчикиСобытия(СобытияБенчмарков.ПередКаждым);

// Удаление всех обработчиков
Конфигурация.УдалитьОбработчикиСобытий();
```

## Пример

```bsl
Перем ДвоичныеДанные;

&ПередВсеми
Процедура Инициализация(Контекст) Экспорт
	Сообщить("Вызван обработчик перед запуском всех бенчмарков");
	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзСтроки("Много данных");
КонецПроцедуры

&ПослеВсех
Процедура Завершение(Контекст) Экспорт
	Сообщить("Вызван обработчик после завершения всех бенчмарков");
	ДвоичныеДанные = Неопределено;
КонецПроцедуры

&ПередКаждым
Процедура ПередКаждым(Контекст) Экспорт	
	Сообщить("Вызван обработчик перед запуском бенчмарка");
КонецПроцедуры

&ПослеКаждого
Процедура ПослеКаждого(Контекст) Экспорт
	Сообщить("Вызван обработчик после запуска бенчмарка");
КонецПроцедуры

&Бенчмарк
Процедура SHA256() Экспорт
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA256);
	ХешированиеДанных.Добавить(ДвоичныеДанные);
	ХешСумма = ХешированиеДанных.ХешСумма;
КонецПроцедуры

&Бенчмарк
Процедура MD5() Экспорт
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.Добавить(ДвоичныеДанные);
	ХешСумма = ХешированиеДанных.ХешСумма;
КонецПроцедуры
```
