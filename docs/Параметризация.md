# Параметризация

Параметризация позволяет тестировать производительность кода с различными входными данными.

## Конфигурирование

### Аннотации

**Параметры для поля класса, заданные напрямую**

Используйте аннотацию `&Параметры` для указания фиксированного набора значений для поля класса. Можно указать значения в одной аннотации или в нескольких.

```bsl
// Вариант 1: Все значения в одной аннотации
&Параметры("MD5", "SHA256")
Перем АлгоритмХеширования Экспорт;

// Вариант 2: Отдельные аннотации для каждого значения
&Параметры("MD5")
&Параметры("SHA256")
Перем АлгоритмХеширования Экспорт;
```

**Параметры для поля класса, полученные из функции**

Используйте аннотацию `&ИсточникПараметров` для указания экспортной функции, возвращающей коллекцию значений для поля. 
Функция может быть определена в классе бенчмарка или в другом модуле/классе.

```bsl
&ИсточникПараметров("ПолучитьАлгоритмыХеширования") // Экспортная функция внутри класса бенчмарков
&ИсточникПараметров("ХранилищеАлгоритмов.ПолучитьАлгоритмыХеширования") // Экспортная функция класса ХранилищеАлгоритмов
Перем АлгоритмХеширования Экспорт;
```

```bsl
Функция ПолучитьАлгоритмыХеширования() Экспорт
	Значения = Новый Массив();
	Значения.Добавить("MD5");
	Значения.Добавить("SHA256");
	Возврат Значения;
КонецФункции
```

**Параметры для метода бенчмарка, заданные напрямую**

Используйте аннотацию `&Параметры` для указания фиксированного набора параметров для метода бенчмарка. Каждая аннотация задает новый тестовый случай.

```bsl
&Параметры(50, 100) // Парам1=50, Парам2=100
&Параметры(75, 150) // Парам1=75, Парам2=150
&Бенчмарк
Процедура Бенчмарк(Парам1, Парам2) Экспорт
	// Логика бенчмарка
КонецПроцедуры
```

**Параметры для метода бенчмарка, полученные из функции**

Используйте аннотацию `&ИсточникПараметров` для указания функции, возвращающей коллекцию наборов параметров.
Функция должна возвращать `Массив` или `ТаблицаЗначений`, где каждый элемент соответствует набору параметров.
Функция может быть определена в классе бенчмарка или в другом модуле/классе.

```bsl
&ИсточникПараметров("ПолучитьПараметрыБенчмарка") // Экспортная функция внутри класса бенчмарков
&ИсточникПараметров("ЭкспортерПараметров.ПолучитьПараметрыБенчмарка") // Экспортная функция класса ЭкспортерПараметров
&Бенчмарк
Процедура Бенчмарк(Парам1, Парам2) Экспорт
	// Логика бенчмарка
КонецПроцедуры
```

```bsl
// Вариант 1: Таблица значений
Функция ПолучитьПараметрыБенчмарка() Экспорт
	Наборы = Новый ТаблицаЗначений;
	Наборы.Колонки.Добавить("Парам1");
	Наборы.Колонки.Добавить("Парам2");

	Набор = Наборы.Добавить();
	Набор.Парам1 = 50;
	Набор.Парам2 = 100;

	Набор = Наборы.Добавить();
	Набор.Парам1 = 75;
	Набор.Парам2 = 150;

	Возврат Наборы;
КонецФункции

// Вариант 2: Массив массивов
Функция ПолучитьПараметрыБенчмарка() Экспорт
	Наборы = Новый Массив;

	Набор = Новый Массив;
	Набор.Добавить(50);
	Набор.Добавить(100);
	Наборы.Добавить(Набор);

	Набор = Новый Массив;
	Набор.Добавить(75);
	Набор.Добавить(150);
	Наборы.Добавить(Набор);

	Возврат Наборы;
КонецФункции

// Вариант 3: Массив структур
Функция ПолучитьПараметрыБенчмарка() Экспорт
	Наборы = Новый Массив;
	Наборы.Добавить(Новый Структура("Парам1, Парам2", 50, 100));
	Наборы.Добавить(Новый Структура("Парам1, Парам2", 75, 150));
	Возврат Наборы;
КонецФункции
```

### API

**Добавление параметров для поля класса**

```bsl
Конфигурация
	.ДобавитьПараметр("АлгоритмХеширования", "MD5")
	.ДобавитьПараметр("АлгоритмХеширования", "SHA256");
```

**Указание источника параметров для поля класса по имени функции**

```bsl
Конфигурация.ДобавитьИсточникПараметров("АлгоритмХеширования", "ПолучитьАлгоритмыХеширования"); // Экспортная функция внутри класса бенчмарков
Конфигурация.ДобавитьИсточникПараметров("АлгоритмХеширования", "ХранилищеАлгоритмов.ПолучитьАлгоритмыХеширования"); // Экспортная функция класса ХранилищеАлгоритмов
```

**Добавление параметров для метода бенчмарка**

```bsl
// Набор 1
НаборПараметров = Новый Массив();
НаборПараметров.Добавить(50);
НаборПараметров.Добавить(100);
ДескрипторБенчмарка.ДобавитьПараметры(НаборПараметров);

// Набор 2
НаборПараметров = Новый Массив();
НаборПараметров.Добавить(75);
НаборПараметров.Добавить(150);
ДескрипторБенчмарка.ДобавитьПараметры(НаборПараметров);
```

**Указание источника параметров для метода бенчмарка по имени функции**

```bsl
ДескрипторБенчмарка.ДобавитьИсточникПараметров("ПолучитьПараметрыБенчмарка"); // Экспортная функция внутри класса бенчмарков
ДескрипторБенчмарка.ДобавитьИсточникПараметров("ЭкспортерПараметров.ПолучитьПараметрыБенчмарка"); // Экспортная функция класса ЭкспортерПараметров
```

## Примеры

### Пример 1: Использование аннотации `&Параметры`

```bsl
&Параметры(5, 0)
Перем ДобавляемаяЗадержка Экспорт; // Число

&Бенчмарк
&Параметры(100, 10)
&Параметры(100, 20)
&Параметры(200, 10)
&Параметры(200, 20)
Процедура Бенчмарк(Задержка1, Задержка2) Экспорт
	Приостановить(Задержка1 + Задержка2 + ДобавляемаяЗадержка);
КонецПроцедуры
```

#### Результаты выполнения

| Method   | ДобавляемаяЗадержка | Задержка1 | Задержка2 |     Mean |   StdErr |    StdDev |  Op/s |
|----------|--------------------:|----------:|----------:|---------:|---------:|----------:|------:|
| Бенчмарк |                   0 |       100 |        10 | 110.9 ms | 20.43 us |  79.13 us | 9.017 |
| Бенчмарк |                   0 |       100 |        20 | 120.9 ms | 25.94 us | 100.45 us | 8.273 |
| Бенчмарк |                   0 |       200 |        10 | 210.8 ms | 32.87 us | 127.32 us | 4.743 |
| Бенчмарк |                   0 |       200 |        20 | 220.9 ms | 21.70 us |  84.04 us | 4.527 |
| Бенчмарк |                   5 |       100 |        10 | 115.8 ms | 22.05 us |  85.41 us | 8.632 |
| Бенчмарк |                   5 |       100 |        20 | 125.9 ms | 23.00 us |  89.07 us | 7.941 |
| Бенчмарк |                   5 |       200 |        10 | 215.9 ms | 17.41 us |  67.43 us | 4.631 |
| Бенчмарк |                   5 |       200 |        20 | 225.9 ms | 28.13 us | 108.95 us | 4.427 |

### Пример 2: Использование аннотации `&ИсточникПараметров`

```bsl
&ИсточникПараметров("ПолучитьМножители")
Перем Множитель Экспорт;

&МониторингПамяти
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&Бенчмарк
&ИсточникПараметров("ПолучитьПараметрыБенчмарка")
Процедура БенчмаркФакториала(Число, ТипАлгоритма) Экспорт

	Если ТипАлгоритма = "Итеративный" Тогда
		Результат = ФакториалИтеративный(Число) * Множитель;
	ИначеЕсли ТипАлгоритма = "Рекурсивный" Тогда
		Результат = ФакториалРекурсивный(Число) * Множитель;
	КонецЕсли;

КонецПроцедуры

// Итеративный алгоритм вычисления факториала
Функция ФакториалИтеративный(Число)
	Результат = 1;
	Для Сч = 1 По Число Цикл
		Результат = Результат * Сч;
	КонецЦикла;
	Возврат Результат;
КонецФункции

// Рекурсивный алгоритм вычисления факториала
Функция ФакториалРекурсивный(Число)
	Если Число <= 1 Тогда
		Возврат 1;
	КонецЕсли;
	Возврат Число * ФакториалРекурсивный(Число - 1);
КонецФункции

// Метод, возвращающий коллекцию множителей
Функция ПолучитьМножители() Экспорт
	Множители = Новый Массив();
	Множители.Добавить(1);
	Множители.Добавить(100);
	Возврат Множители;
КонецФункции

// Метод, возвращающий наборы параметров для бенчмарка
Функция ПолучитьПараметрыБенчмарка() Экспорт

	Наборы = Новый ТаблицаЗначений();
	Наборы.Колонки.Добавить("Число");
	Наборы.Колонки.Добавить("ТипАлгоритма");
	
	// Набор 1: Маленькое число, итеративный алгоритм
	Набор = Наборы.Добавить();
	Набор.Число = 10;
	Набор.ТипАлгоритма = "Итеративный";
	
	// Набор 2: Среднее число, рекурсивный алгоритм
	Набор = Наборы.Добавить();
	Набор.Число = 15;
	Набор.ТипАлгоритма = "Рекурсивный";
	
	// Набор 3: Большое число, итеративный алгоритм
	Набор = Наборы.Добавить();
	Набор.Число = 20;
	Набор.ТипАлгоритма = "Итеративный";
	
	Возврат Наборы;

КонецФункции
```

#### Результаты выполнения

| Method             | Множитель | Число | ТипАлгоритма |      Mean |    StdErr |    StdDev |    Op/s | Allocated |
|--------------------|----------:|------:|--------------|----------:|----------:|----------:|--------:|----------:|
| БенчмаркФакториала |         1 |    10 | Итеративный  |  6.489 us | 103.42 ns | 400.56 ns | 154,115 |  1.489 KB |
| БенчмаркФакториала |         1 |    15 | Рекурсивный  | 11.853 us |  22.26 ns |  86.20 ns |  84,367 |  4.851 KB |
| БенчмаркФакториала |         1 |    20 | Итеративный  |  9.746 us |  51.22 ns | 198.37 ns | 102,607 |  2.272 KB |
| БенчмаркФакториала |       100 |    10 | Итеративный  |  6.610 us | 142.17 ns | 550.62 ns | 151,295 |  1.489 KB |
| БенчмаркФакториала |       100 |    15 | Рекурсивный  | 12.316 us | 198.95 ns | 770.54 ns |  81,192 |  4.850 KB |
| БенчмаркФакториала |       100 |    20 | Итеративный  |  9.768 us |  38.11 ns | 147.59 ns | 102,378 |  2.272 KB |