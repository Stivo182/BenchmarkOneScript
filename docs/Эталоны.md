# Эталоны

## Эталонные бенчмарки 

Результаты выполнения эталонного бенчмарка используются как базовая метрика для оценки эффективности других тестов.

### Конфигурирование

#### Аннотация

```bsl
&Эталон
Процедура Бенчмарк() Экспорт
```

#### API

```bsl
// Установка текущего бенчмарка в качестве эталона
ДескрипторБенчмарка.ИспользоватьКакЭталон();

// Снятие эталонного статуса с бенчмарка
ДескрипторБенчмарка.ИспользоватьКакЭталон(Ложь);
```

### Пример

```bsl
&Бенчмарк
Процедура Задержка50() Экспорт
	Приостановить(50);
КонецПроцедуры

&Бенчмарк
&Эталон
Процедура Задержка100() Экспорт
	Приостановить(100);
КонецПроцедуры

&Бенчмарк
Процедура Задержка150() Экспорт
	Приостановить(150);
КонецПроцедуры
```

#### Результаты выполнения

В отчете автоматически добавляется колонка `Ratio`, показывающая среднее значение соотношений времени выполнения относительно эталона и в некоторых ситуациях колонка [`RatioSD`](#ratiosd).

| Method      | Baseline |      Mean |   StdErr |    StdDev | Ratio |    Median |   Op/s |
|-------------|----------|----------:|---------:|----------:|------:|----------:|-------:|
| Задержка100 | true     | 100.93 ms | 18.96 us |  73.45 us | 1.000 | 100.95 ms |  9.908 |
| Задержка150 | false    | 150.88 ms | 30.44 us | 117.89 us | 1.495 | 150.83 ms |  6.628 |
| Задержка50  | false    |  50.91 ms | 19.17 us |  74.26 us | 0.504 |  50.93 ms | 19.644 |

**Интерпретация:**

- Метод `Задержка150` (Ratio 1.495) на 49.5% медленнее эталона
- Метод `Задержка50` (Ratio 0.504) в 2 раза быстрее эталона

## Множественные эталоны через категории

Для использования нескольких эталонных бенчмарков в одном классе применяйте категории. Каждая категория может иметь собственный эталон.

### Пример

```bsl
&Бенчмарк
&Категория("Быстрый")
&Эталон
Процедура Задержка50() Экспорт
	Приостановить(50);
КонецПроцедуры

&Бенчмарк
&Категория("Быстрый")
Процедура Задержка100() Экспорт
	Приостановить(100);
КонецПроцедуры

&Бенчмарк
&Категория("Медленный")
&Эталон
Процедура Задержка300() Экспорт
	Приостановить(300);
КонецПроцедуры

&Бенчмарк
&Категория("Медленный")
Процедура Задержка350() Экспорт
	Приостановить(350);
КонецПроцедуры
```

#### Результаты выполнения

| Method      | Categories | Baseline |      Mean |   StdErr |    StdDev | Ratio |    Median |   Op/s |
|-------------|------------|----------|----------:|---------:|----------:|------:|----------:|-------:|
| Задержка50  | Быстрый    | true     |  50.86 ms | 23.53 us |  91.12 us |     1 |  50.87 ms | 19.662 |
| Задержка100 | Быстрый    | false    | 100.93 ms | 19.70 us |  76.28 us |     2 | 100.96 ms |  9.908 |
| Задержка300 | Медленный  | true     | 300.85 ms | 37.90 us | 146.77 us |     1 | 300.91 ms |  3.324 |
| Задержка350 | Медленный  | false    | 350.89 ms | 21.07 us |  81.60 us |     1 | 350.91 ms |  2.850 |

## RatioSD

В некоторых ситуациях автоматически добавляется колонка `RatioSD` (стандартное отклонение соотношений времени выполнения относительно эталона). Метрика помогает выявить аномалии в измерениях, когда отдельные выбросы искажают общую картину. В следующем примере эталонный бенчмарк испорчен одиночным выбросом.

### Пример

```bsl
Перем Счетчик; // Число

&СтратегияХолодныйЗапуск
&КоличествоИтераций(10)
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ПередВсеми
Процедура Инициализация() Экспорт
	Счетчик = 0;
КонецПроцедуры

&Бенчмарк
&Эталон
Процедура Базовый() Экспорт

	Приостановить(100);
	Счетчик = Счетчик + 1;

	// Искусственный выброс на каждой 7-й итерации
	Если Счетчик % 7 = 0 Тогда
		Приостановить(5000);
	КонецЕсли;

КонецПроцедуры

&Бенчмарк
Процедура Медленный() Экспорт
	Приостановить(200);
КонецПроцедуры

&Бенчмарк
Процедура Быстрый() Экспорт
	Приостановить(50);
КонецПроцедуры
```

#### Результаты выполнения

| Method    | Baseline |      Mean |        StdErr |         StdDev | Ratio | RatioSD |    Median |   Op/s |
|-----------|----------|----------:|--------------:|---------------:|------:|--------:|----------:|-------:|
| Базовый   | true     | 600.84 ms | 500,097.32 us | 1,581,446.6 us |  1.00 |    0.00 | 100.71 ms |  1.664 |
| Медленный | false    | 200.68 ms |      91.60 us |       289.7 us |  1.80 |    0.59 | 200.80 ms |  4.983 |
| Быстрый   | false    |  50.75 ms |     100.97 us |       319.3 us |  0.45 |    0.15 |  50.84 ms | 19.706 |

Единичный выброс данных исказил результаты метрик. Рассмотрим бенчмарки `Базовый` и `Медленный`. Для метрики `Mean` зафиксированы значения **600 мс** (Базовый) и **200 мс** (Медленный), что дает соотношение 0.3.  Для метрики `Median` результаты противоположны: **100 мс** (Базовый) и **200 мс** (Медленный), что приводит к соотношению 2.0. 

Обе метрики демонстрируют противоречивую картину из-за влияния выброса. Для решения этой проблемы инструмент BenchmarkOneScript вводит метрику `RatioSD`, которая количественно оценивает нестабильность тестов. 

Принцип интерпретации: **чем выше значение `RatioSD`, тем больше вариативность результатов**, что указывает на потенциальные проблемы в воспроизводимости тестирования. 


## Эталонные среды выполнения

### Конфигурирование

#### Аннотация

```bsl
&ИсполняющаяСреда(
    Версия = "1.9.3", 
    Наименование = "Стабильная сборка",
    ЭтоЭталон = Истина
)
Процедура ПриСозданииОбъекта()
```

#### API

```bsl
Конфигурация.ДобавитьВерсиюИсполняющейСреды("stable", "Стабильная сборка", Истина);
```

### Пример выполнения

| Method | Runtime               | Baseline |      Mean |   StdErr |   StdDev | Ratio | RatioSD |    Op/s |
|--------|-----------------------|----------|----------:|---------:|---------:|------:|--------:|--------:|
| SHA256 | 2.0.0-rc.8+608        | No       |  9.048 us | 93.81 ns | 363.3 ns |  0.67 |    0.03 | 110,523 |
| SHA256 | Стабильная (1.9.3.15) | Yes      | 13.561 us | 70.97 ns | 274.9 ns |  1.00 |    0.00 |  73,742 |

Подробнее см. в разделе [Среда выполнения](СредаВыполнения.md)